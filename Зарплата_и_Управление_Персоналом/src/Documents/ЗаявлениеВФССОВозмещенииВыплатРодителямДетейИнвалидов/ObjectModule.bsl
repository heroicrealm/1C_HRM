#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КПП");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбъектЗафиксирован() Экспорт
	Возврат НЕ ПрямыеВыплатыПособийСоциальногоСтрахования.СтатусПозволяетРедактироватьДокумент(СтатусДокумента);
КонецФункции

Функция ОбновитьВторичныеДанныеДокумента(ДанныеОрганизации = Истина, ДанныеОплат = Истина) Экспорт
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	ПараметрыФиксации = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Метаданные().ПолноеИмя()).ПараметрыФиксацииВторичныхДанных();
	
	Если ДанныеОрганизации И ОбновитьДанныеОрганизации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ДанныеОплат И ОбновитьДанныеОплат(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован
КонецФункции

Функция ОбновитьДанныеОрганизации(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыДокумента = Новый Структура(
		"РегистрационныйНомерФСС,ДополнительныйКодФСС,КодПодчиненностиФСС,НаименованиеТерриториальногоОрганаФСС,ИНН,КПП,
		|Руководитель,ДолжностьРуководителя,ОснованиеПодписиРуководителя,ТелефонСоставителя,АдресОрганизации,АдресЭлектроннойПочтыОрганизации,
		|НаименованиеБанка,НомерЛицевогоСчета,НомерСчета,БИКБанка");
	
	СведенияОПодписях = ПодписиДокументов.СведенияОПодписяхПоУмолчаниюДляОбъектаМетаданных(Метаданные(), Организация);
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, СведенияОПодписях);
	
	ИменаПолей =
	"РегистрационныйНомерФСС,
	|КодПодчиненностиФСС,
	|ДополнительныйКодФСС,
	|НаименованиеТерриториальногоОрганаФСС,
	|ИННЮЛ,
	|КППЮЛ,
	|БанкСчетНомер,
	|БанкСчетНаимБанка,
	|БанкСчетБИКБанка,
	|АдрЮР_JSON,
	|ТелОрганизации_JSON,
	|АдресЭлектроннойПочтыОрганизации";
	СведенияОбОрганизации = СЭДОФСС.СведенияОСтрахователе(Организация, ИменаПолей, Дата);
	
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, СведенияОбОрганизации);
	
	РеквизитыДокумента.ИНН                = СведенияОбОрганизации.ИННЮЛ;
	РеквизитыДокумента.КПП                = СведенияОбОрганизации.КППЮЛ;
	РеквизитыДокумента.НомерСчета         = СведенияОбОрганизации.БанкСчетНомер;
	РеквизитыДокумента.НаименованиеБанка  = СведенияОбОрганизации.БанкСчетНаимБанка;
	РеквизитыДокумента.БИКБанка           = СведенияОбОрганизации.БанкСчетБИКБанка;
	РеквизитыДокумента.АдресОрганизации   = СведенияОбОрганизации.АдрЮР_JSON;
	РеквизитыДокумента.ТелефонСоставителя = СведенияОбОрганизации.ТелОрганизации_JSON;
	РеквизитыДокумента.АдресЭлектроннойПочтыОрганизации = СведенияОбОрганизации.АдресЭлектроннойПочтыОрганизации;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыДокумента, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ОбновитьДанныеОплат(ПараметрыФиксации)
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросОплатДляВозмещенияВыплатРодителямДетейИнвалидов(ЭтотОбъект);
	Если Запрос = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	ПоследнийЗапрос = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ПоследнийЗапрос.ТаблицаДляПомещения = "ВТВторичныеДанные";
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Запрос.Выполнить();
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(
		Запрос.МенеджерВременныхТаблиц,
		ЭтотОбъект,
		"Оплаты",
		"ВТВторичныеДанные",
		ПараметрыФиксации);
КонецФункции

Процедура ЗаполнитьДокумент() Экспорт
	Оплаты.Очистить();
	
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросОплатДляВозмещенияВыплатРодителямДетейИнвалидов(ЭтотОбъект);
	
	Если Запрос <> Неопределено Тогда
		Оплаты.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
КонецПроцедуры

Функция ДанныеСтроки(ДокументОснование = Неопределено) Экспорт
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросОплатДляВозмещенияВыплатРодителямДетейИнвалидов(ЭтотОбъект, ДокументОснование);
	
	ДанныеСтроки = Запрос.Выполнить().Выбрать();
	Если ДанныеСтроки.Следующий() Тогда
		Возврат ДанныеСтроки;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли