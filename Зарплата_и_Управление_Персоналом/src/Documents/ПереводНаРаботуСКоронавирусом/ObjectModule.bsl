#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ДанныеДляПроведения();
	СформироватьДвиженияПериодовРаботыСКоронавирусом(Движения, ДанныеДляПроведения.ПериодыРаботыСКоронавирусом);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ИсключаемыеРеквизиты = Новый Массив;
	НастройкиКадровогоУчета = КадровыйУчетРасширенный.НастройкиКадровогоУчета();
	
	Если НастройкиКадровогоУчета.ПолучательСтимулирующихВыплатФСС = Перечисления.ПолучательСтимулирующихВыплатФСС.МедицинскаяОрганизация Тогда
		ИсключаемыеРеквизиты.Добавить("Сотрудники.ДатаНачала");
		ИсключаемыеРеквизиты.Добавить("Сотрудники.ДатаОкончания");
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, НачалоПериода, "Объект.НачалоПериода", Отказ, НСтр("ru='Начало периода'"), , , Ложь);
	
	Если ЗначениеЗаполнено(ОкончаниеПериода) И ОкончаниеПериода < НачалоПериода Тогда
		
		ТекстСообщения = НСтр("ru='Не верно задано окончание периода'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ОкончаниеПериода", , Отказ);
		
	КонецЕсли;
	
	КадровыйУчет.ПроверитьРаботающихСотрудниковТабличнойЧастиДокумента(ЭтотОбъект, "Сотрудники", "ДатаНачала", "ДатаОкончания", Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, ИсключаемыеРеквизиты);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПереводНаРаботуСКоронавирусомСотрудники.ДатаНачала КАК ДатаНачала,
		|	ПереводНаРаботуСКоронавирусомСотрудники.ДатаОкончания КАК ДатаОкончания,
		|	ПереводНаРаботуСКоронавирусомСотрудники.Сотрудник КАК Сотрудник,
		|	ПереводНаРаботуСКоронавирусомСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПереводНаРаботуСКоронавирусомСотрудники.Ссылка.Организация КАК Организация
		|ИЗ
		|	Документ.ПереводНаРаботуСКоронавирусом.Сотрудники КАК ПереводНаРаботуСКоронавирусомСотрудники
		|ГДЕ
		|	ПереводНаРаботуСКоронавирусомСотрудники.Ссылка = &Ссылка";
	
	ПериодыРаботыСКоронавирусом = Запрос.Выполнить().Выгрузить();
	Данные = Новый Структура("ПериодыРаботыСКоронавирусом", ПериодыРаботыСКоронавирусом);
	
	Возврат Данные;
	
КонецФункции

Процедура СформироватьДвиженияПериодовРаботыСКоронавирусом(Движения, ПериодыРаботыСКоронавирусом)
	Если ПериодыРаботыСКоронавирусом.Количество() > 0 Тогда
		
		Движения.ПериодыРаботыСКоронавирусом.Загрузить(ПериодыРаботыСКоронавирусом);
		Движения.ПериодыРаботыСКоронавирусом.Записывать = Истина;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли