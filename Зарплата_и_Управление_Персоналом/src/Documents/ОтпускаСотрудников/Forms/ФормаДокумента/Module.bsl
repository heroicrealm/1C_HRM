
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступенРасчетОтпусков = Пользователи.РолиДоступны("ДобавлениеИзменениеНачисленнойЗарплатыРасширенная");
	ДоступноОформлениеОтпусков = ДоступенРасчетОтпусков Или Пользователи.РолиДоступны("ДобавлениеИзменениеОтпусков");
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	Если Параметры.Ключ.Пустая() Тогда
		
		// Заполнение нового документа.
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", "Объект.Организация", "Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ЗаполнитьДанныеФормыПоОрганизации();
		
		ИнициализироватьФорму();
		ПриПолученииДанныхНаСервере();
		
		УстановитьИнформационнуюНадпись();
		
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужбаФормы");
		Модуль.УстановитьПараметрыВыбораСотрудников(ЭтаФорма, "СотрудникиСотрудник");
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов") Тогда
		МодульУчетОригиналовПервичныхДокументов = ОбщегоНазначения.ОбщийМодуль("УчетОригиналовПервичныхДокументов");
		МодульУчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект, Элементы.Шапка);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ЗаписанДокументОтпуск" 
		Или ИмяСобытия = "ЗаписанДокументОтпускБезСохраненияОплаты")
		И Источник.ВладелецФормы = ЭтаФорма Тогда
		ЗаполнитьРасчетныеДокументы();
	ИначеЕсли ИмяСобытия = "ИзмененыОбщиеПериодыОтпусков" И Источник = ЭтаФорма Тогда
		ОбработатьОбщиеПериодыОтпусков(Параметр);
	ИначеЕсли ИмяСобытия = "ЗаполнениеПоГрафикуОтпусков" И Источник = ЭтаФорма Тогда
		ЗаполнитьПоГрафикуОтпусковНаСервере(Параметр);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов") Тогда
		МодульУчетОригиналовПервичныхДокументовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УчетОригиналовПервичныхДокументовКлиент");
		МодульУчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаДокумента(ИмяСобытия, ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ИнициализироватьФорму();
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НеРассчитанныеСтрокиОтпусков = Объект.Сотрудники.НайтиСтроки(Новый Структура("Рассчитан", Ложь));
	Для Каждого НеРассчитаннаяСтрока Из НеРассчитанныеСтрокиОтпусков Цикл
		
		Если ЗначениеЗаполнено(НеРассчитаннаяСтрока.Отпуск) Тогда
			
			Если НеРассчитаннаяСтрока.ВидОтпуска <> НеРассчитаннаяСтрока.ВидОтпускаПрежний
				Или НеРассчитаннаяСтрока.ДатаНачала <> НеРассчитаннаяСтрока.ДатаНачалаПрежняя
				Или НеРассчитаннаяСтрока.ДатаОкончания <> НеРассчитаннаяСтрока.ДатаОкончанияПрежняя
				Или НеРассчитаннаяСтрока.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск <> НеРассчитаннаяСтрока.НачалоПериодаЗаКоторыйПредоставляетсяОтпускПрежний
				Или НеРассчитаннаяСтрока.КонецПериодаЗаКоторыйПредоставляетсяОтпуск <> НеРассчитаннаяСтрока.КонецПериодаЗаКоторыйПредоставляетсяОтпускПрежний
				Или НеРассчитаннаяСтрока.КоличествоДнейКомпенсации <> НеРассчитаннаяСтрока.КоличествоДнейКомпенсацииПрежнее
				Или НеРассчитаннаяСтрока.Основание <> НеРассчитаннаяСтрока.ОснованиеПрежнее Тогда
				
				Отбор = Новый Структура("Отпуск", НеРассчитаннаяСтрока.Отпуск);
				СтрокиОтпуска = Объект.Сотрудники.НайтиСтроки(Отбор);
				
				ДанныеОтпусков = Новый Массив;
				Для Каждого СтрокаОтпуска Из СтрокиОтпуска Цикл 
					ДанныеОтпусков.Добавить(ДанныеЗаполненияСтроки(СтрокаОтпуска));
				КонецЦикла;
				
				ПараметрыЗаполнения = ОбщиеДанныеЗаполнения(Объект, НеРассчитаннаяСтрока.Сотрудник, НеРассчитаннаяСтрока.Отпуск);
				ПараметрыЗаполнения.Вставить("ДанныеОтпусков", ДанныеОтпусков);
				
				РезультатЗаполнения = РасчетныйДокументПоПараметрыЗаполнения(НеРассчитаннаяСтрока.Сотрудник, ПараметрыЗаполнения);
				НеРассчитаннаяСтрока.Отпуск = РезультатЗаполнения.Отпуск;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Отказ);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗакрытием(ЭтотОбъект, Объект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриПолученииДанныхНаСервере();
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписанДокументОтпускаСотрудников", , ЭтаФорма);
	Оповестить("Запись_ОтпускаСотрудников", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ЗаполнитьДанныеФормыПоОрганизации();
КонецПроцедуры

// Обработчик подсистемы "ПодписиДокументов".
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент) 
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент) 
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец Обработчик подсистемы "ПодписиДокументов".

// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
&НаКлиенте
Процедура Подключаемый_ДекорацияСостояниеОригиналаНажатие()

	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов") Тогда
		МодульУчетОригиналовПервичныхДокументовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УчетОригиналовПервичныхДокументовКлиент");
		МодульУчетОригиналовПервичныхДокументовКлиент.ОткрытьМенюВыбораСостояния(ЭтотОбъект,Элементы.ДекорацияСостояниеОригинала);
	КонецЕсли;
	
КонецПроцедуры
//Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СотрудникиОтпуск" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ДополнительныеПараметры = Новый Структура("ПерезаполнитьНачисления", Ложь);
		
		Если Модифицированность Или Не ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
			
			Оповещение = Новый ОписаниеОповещения("СотрудникиВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ДобавитьОтпускКДокументу(Оповещение);
			ОповеститьОЗаписиДокументаОтпуск();
			
		Иначе
			СотрудникиВыборЗавершение(ТекущиеДанные, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыборЗавершение(ТекущиеДанные, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.Отпуск);
		ПараметрыОткрытия.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ПараметрыОткрытия.Вставить("ПерезаполнитьНачисления", ДополнительныеПараметры.ПерезаполнитьНачисления И Не ТекущиеДанные.Рассчитан);
		
		ОткрытьФорму("Документ." + ИмяДокументаОтпуск(ТекущиеДанные.ВидДокументаОтпуск) + ".ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ТекущиеДанные.Рассчитан Тогда
		
		Если Не ДоступенРасчетОтпусков Тогда
			
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сотруднику %1 уже начислены отпускные и оформлено отсутствие.'"),
					ТекущиеДанные.Сотрудник);
					
			Если ЗначениеЗаполнено(ТекущиеДанные.ОтпускРассчитал) Тогда
				ТекстПредупрежденияДополнительно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Для отмены начислений обратитесь к расчетчику, работающему под именем ""%1"".'"),
					ТекущиеДанные.ОтпускРассчитал);
			Иначе
				ТекстПредупрежденияДополнительно = НСтр("ru='Для отмены начислений обратитесь к расчетчику.'");
			КонецЕсли;
			
			Отказ = Истина;
			ПоказатьПредупреждение(, ТекстПредупреждения + Символы.ПС + ТекстПредупрежденияДополнительно);
			
		Иначе
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сотруднику %1 уже начислены отпускные и оформлено отсутствие.
					|Для удаления этой строки необходимо также отменить выполненное начисление.
					|Отменить начисления?'"),
					ТекущиеДанные.Сотрудник);
					
			Оповещение = Новый ОписаниеОповещения("СотрудникиПередУдалениемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
			
			Отказ = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		
		Если ТекущиеДанные.Проведен Тогда
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Отсутствие сотрудника %1 уже оформлено.
					|Для удаления этой строки необходимо также отменить оформление отсутствия на работе.
					|Отменить оформленное отсутствие?'"),
					ТекущиеДанные.Сотрудник);
					
			Оповещение = Новый ОписаниеОповещения("СотрудникиПередУдалениемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
			
		Иначе
			СотрудникиПередУдалениемЗавершение(КодВозвратаДиалога.Да)
		КонецЕсли;
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалениемЗавершение(Ответ, ДополнительныеПараметры = Неопределено) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Сотрудники.ТекущаяСтрока;
	
	Отказ = Ложь;
	ОтменитьДокумент(ТекущаяСтрока, Отказ);
	
	Если Не Отказ Тогда
		Объект.Сотрудники.Удалить(Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока));
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		
		Элемент.ТекущиеДанные.ВидОтпуска = ПредопределенноеЗначение("Справочник.ВидыОтпусков.ПустаяСсылка");
		Элемент.ТекущиеДанные.ОтпускЯвляетсяЕжегодным = Ложь;
		Элемент.ТекущиеДанные.СпособРасчетаПоКалендарнымДням = Ложь;
		Элемент.ТекущиеДанные.ДатаНачала = '00010101';
		Элемент.ТекущиеДанные.ДатаОкончания = '00010101';
		Элемент.ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		Элемент.ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		Элемент.ТекущиеДанные.Рассчитан = Ложь;
		Элемент.ТекущиеДанные.Проведен = Ложь;
		Элемент.ТекущиеДанные.Отпуск = ПредопределенноеЗначение("Документ.Отпуск.ПустаяСсылка");
		Элемент.ТекущиеДанные.ОтпускРассчитал = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		
		СотрудникиСотрудникПриИзмененииНаСервере();
		
		ТекущийЭлемент = Элементы.СотрудникиВидОтпуска;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	СотрудникиПриОкончанииРедактированияНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	
	СотрудникиСотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВидОтпускаПриИзменении(Элемент)
	
	СотрудникиВидОтпускаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиДатаНачалаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) Тогда
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания) Тогда
			
			ТекущиеДанные.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(
				ТекущиеДанные.Сотрудник,
				ТекущиеДанные.ДатаНачала,
				ТекущиеДанные.КоличествоДней,
				Новый Структура("СпособРасчетаПоКалендарнымДням, ЕжегодныйОтпуск", ТекущиеДанные.СпособРасчетаПоКалендарнымДням, ТекущиеДанные.ОтпускЯвляетсяЕжегодным));
			
		КонецЕсли;
		
		УстановитьКоличествоДнейНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
		
	Иначе
		ТекущиеДанные.КоличествоДней = 0;
		ТекущиеДанные.КоличествоДнейКомпенсации = 0;
	КонецЕсли;
	
	ЗаполнитьОтпускВРабочихДняхПоДоговору(ТекущиеДанные.ПолучитьИдентификатор());
	
	РассчитатьПараметрыПериодаОтпускаНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиДатаОкончанияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания) Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) Тогда
			УстановитьКоличествоДнейНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
		КонецЕсли;
	Иначе
		ТекущиеДанные.КоличествоДней = 0;
		ТекущиеДанные.КоличествоДнейКомпенсации = 0;
	КонецЕсли;
	
	РассчитатьПараметрыПериодаОтпускаНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиКоличествоДнейПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.КоличествоДней > 0 И ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) Тогда
		ТекущиеДанные.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(
			ТекущиеДанные.Сотрудник,
			ТекущиеДанные.ДатаНачала,
			ТекущиеДанные.КоличествоДней,
			Новый Структура("СпособРасчетаПоКалендарнымДням, ЕжегодныйОтпуск", ТекущиеДанные.СпособРасчетаПоКалендарнымДням, ТекущиеДанные.ОтпускЯвляетсяЕжегодным));
	КонецЕсли;
	
	РассчитатьПараметрыПериодаОтпускаНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиКоличествоДнейКомпенсацииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.КоличествоДнейКомпенсации <> 0 Тогда
		СотрудникиКоличествоДнейКомпенсацииПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ОткрытьФормуСпискаЗначенийСтроковогоРеквизита(Элемент, 
		СтандартнаяОбработка, "ОснованиеОтпуска", НСтр("ru = 'Основания отпуска'"), НСтр("ru = 'Основание отпуска'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОснованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда 
			ТекущиеДанные.Основание = ВыбранноеЗначение;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОснованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ПодборЗначенияСтроковогоРеквизита(Текст, ДанныеВыбора, СтандартнаяОбработка, "ОснованиеОтпуска");
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаПодбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбработчикиСобытийПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Команда, Объект)
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Контекст, Объект);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийСледующемуНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СформироватьРасчетныеДокументы(Команда)
	
	СформироватьРасчетныеДокументыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПравильностьОформленныхОтпусков(Команда)
	
	ДатыОстатков = Новый Соответствие;
	ДанныеОРасхождениях = ДанныеОРасхожденияхСУчетом(ДатыОстатков);
	
	Если ДанныеОРасхождениях.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Документ оформлен правильно'"));
	Иначе
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ДанныеОРасхождениях", ДанныеОРасхождениях);
		ПараметрыОткрытия.Вставить("ДатыОстатков", ДатыОстатков);
		
		ОткрытьФорму("Документ.ОтпускаСотрудников.Форма.ФормаПроверкиПравильностиОформления", ПараметрыОткрытия, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Подбор возможен только при указанной организации'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.Организация");
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Неопределено;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ГосударственнаяСлужбаКлиент");
		Модуль.УточнитьПараметрыОткрытияФормыВыбораСотрудников(ПараметрыОткрытия);
	КонецЕсли;
	
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.Сотрудники,
		Объект.Организация,
		,
		Объект.Дата,
		Объект.Дата,
		,
		АдресСпискаПодобранныхСотрудников(),
		ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодыОтпусков(Команда)
	
	ПараметрыФормы = Новый Структура;       
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ДатаАктуальности", Объект.Дата);
	
	ОткрытьФорму("ОбщаяФорма.ВводОбщегоПериодаОтпуска", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоГрафикуОтпусков(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указана организация.'"));
		Возврат;
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура("ДатаДокумента", Объект.Дата);
	ОткрытьФорму("Документ.ОтпускаСотрудников.Форма.ЗаполнитьПоГрафикуОтпусков", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ЗаполнитьВторичныеДанныеСотрудниковВФорме();
	ЗаполнитьРасчетныеДокументы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПрежниеЗначения()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Для Каждого СтрокаСотрудники Из Объект.Сотрудники Цикл
			ЗаполнитьПрежниеЗначенияСтроки(СтрокаСотрудники);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПрежниеЗначенияСтроки(СтрокаСотрудники)
	
	Если ЗначениеЗаполнено(СтрокаСотрудники.Отпуск) Тогда
		
		СтрокаСотрудники.ВидОтпускаПрежний = СтрокаСотрудники.ВидОтпуска;
		СтрокаСотрудники.ДатаНачалаПрежняя = СтрокаСотрудники.ДатаНачала;
		СтрокаСотрудники.ДатаОкончанияПрежняя = СтрокаСотрудники.ДатаОкончания;
		СтрокаСотрудники.НачалоПериодаЗаКоторыйПредоставляетсяОтпускПрежний = СтрокаСотрудники.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск;
		СтрокаСотрудники.КонецПериодаЗаКоторыйПредоставляетсяОтпускПрежний = СтрокаСотрудники.КонецПериодаЗаКоторыйПредоставляетсяОтпуск;
		СтрокаСотрудники.КоличествоДнейКомпенсацииПрежнее = СтрокаСотрудники.КоличествоДнейКомпенсации;
		СтрокаСотрудники.ОснованиеПрежнее = СтрокаСотрудники.Основание;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	УстанавливаемыеПараметры = Новый Структура;
	УстанавливаемыеПараметры.Вставить("Организация", Объект.Организация);
	
	УстановитьПараметрыФункциональныхОпцийФормы(УстанавливаемыеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ЗаполнитьДанныеФормыПоОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПоОрганизации()
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиСотрудникПриИзмененииНаСервере()
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ВидОтпуска = Справочники.ВидыОтпусков.ПустаяСсылка();
	ТекущиеДанные.ДатаНачала = '00010101';
	ТекущиеДанные.ДатаОкончания = '00010101';
	ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
	ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
	ТекущиеДанные.КоличествоДней = 0;
	ТекущиеДанные.КоличествоДнейКомпенсации = 0;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		ЗаполнитьСтрокуСотрудника(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПодбораНаСервере(Сотрудники)
	
	Для Каждого Сотрудник Из Сотрудники Цикл
		
		СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
		
		Если СтрокиПоСотруднику.Количество() = 0 Тогда
			НоваяСтрока = Объект.Сотрудники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
			
			ЗаполнитьСтрокуСотрудника(НоваяСтрока);
			
			ЭтаФорма.Модифицированность = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуСотрудника(ТекущиеДанные)
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ВидОтпуска) Тогда
		ТекущиеДанные.ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной");
	КонецЕсли;
	
	ДанныеПоОтпускамСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ТекущиеДанные.Сотрудник));
	ДанныеПоОтпускамСотрудника.Удалить(ДанныеПоОтпускамСотрудника.Найти(ТекущиеДанные));
	
	НачалоПериодаОтпуска = '00010101';
	ОкончаниеПериодаОтпуска = '00010101';
	МаксимальнаяДатаОкончания = '00010101';
	Для Каждого ДанныеОтпуска Из ДанныеПоОтпускамСотрудника Цикл
		
		Если МаксимальнаяДатаОкончания <= ДанныеОтпуска.ДатаОкончания Тогда
			МаксимальнаяДатаОкончания = КонецДня(ДанныеОтпуска.ДатаОкончания) + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	ТекущиеДанные.ДатаНачала = МаксимальнаяДатаОкончания;
	
	ЗаполнитьОтпускВРабочихДняхПоДоговору(ТекущиеДанные.ПолучитьИдентификатор());
	
	ЗаполнитьВторичныеПризнакиОтпуска(ТекущиеДанные.ПолучитьИдентификатор());
	
	ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеДанныеСотрудниковВФорме()
	
	ЗаполнитьОтпускВРабочихДняхПоДоговору();
	ЗаполнитьВторичныеПризнакиОтпусков();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтпускВРабочихДняхПоДоговору(ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		Строки = Объект.Сотрудники;
	Иначе
		Строки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока));
	КонецЕсли;
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Для Каждого Строка Из Строки Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаСотрудников.Добавить();
		НоваяСтрока.Сотрудник = Строка.Сотрудник;
		НоваяСтрока.Период = Строка.ДатаНачала;
		
	КонецЦикла;
	
	ТрудовыеДоговоры = ОстаткиОтпусков.ДоговорыСотрудниковСОтпускомВРабочихДнях(ТаблицаСотрудников);
	
	Отбор = Новый Структура("Сотрудник,Период");
	Для Каждого Строка Из Строки Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		Отбор.Сотрудник  = Строка.Сотрудник;
		Отбор.Период = Строка.ДатаНачала;
		Строка.ОтпускВРабочихДняхПоДоговору = Ложь;
		НайденныеСтроки = ТрудовыеДоговоры.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Строка.ОтпускВРабочихДняхПоДоговору = НайденныеСтроки[0].ОтпускВРабочихДняхПоДоговору;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьПараметрыПериодаОтпускаНаСервере(ТекущийИдентификатор)
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(ТекущийИдентификатор);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Сотрудник)
		Или Не ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания)
		Или Не ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ДатаНачала > ТекущиеДанные.ДатаОкончания Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные); 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасчетныеДокументы()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокОтпусков", Объект.Сотрудники.Выгрузить().ВыгрузитьКолонку("Отпуск"));
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Отпуск.Ссылка КАК Отпуск,
	               |	Отпуск.ДокументРассчитан КАК Рассчитан,
	               |	Отпуск.Рассчитал КАК Рассчитал,
	               |	Отпуск.Проведен КАК Проведен,
	               |	ИСТИНА КАК ВидДокументаОтпуск
	               |ИЗ
	               |	Документ.Отпуск КАК Отпуск
	               |ГДЕ
	               |	Отпуск.Ссылка В(&СписокОтпусков)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОтпускБезСохраненияОплаты.Ссылка,
	               |	ОтпускБезСохраненияОплаты.ПерерасчетВыполнен,
	               |	ОтпускБезСохраненияОплаты.Рассчитал,
	               |	ОтпускБезСохраненияОплаты.Проведен,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	               |ГДЕ
	               |	ОтпускБезСохраненияОплаты.Ссылка В(&СписокОтпусков)";
	
	ТаблицаОтпусков = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицыОтпусков Из ТаблицаОтпусков Цикл
		СтрокиОтпусков = Объект.Сотрудники.НайтиСтроки(Новый Структура("Отпуск", СтрокаТаблицыОтпусков.Отпуск));
		Для Каждого СтрокаОтпуска Из СтрокиОтпусков Цикл 
			ЗаполнитьЗначенияСвойств(СтрокаОтпуска, СтрокаТаблицыОтпусков);
		КонецЦикла;
	КонецЦикла;
	
	ЗаполнитьВторичныеПризнакиОтпусков();
	ЗаполнитьПрежниеЗначения();
	
	УстановитьИнформационнуюНадпись();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеПризнакиОтпусков()
	
	ВидыОтпусков = Объект.Сотрудники.Выгрузить().ВыгрузитьКолонку("ВидОтпуска");
	
	ДанныеВидовОтпусков = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВидыОтпусков, "ОтпускЯвляетсяЕжегодным, СпособРасчетаОтпуска, ОтпускБезОплаты");
	
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		Если Не СтрокаСотрудника.ОтпускЯвляетсяЕжегодным Тогда
			ДанныеВидаОтпуска = ДанныеВидовОтпусков.Получить(СтрокаСотрудника.ВидОтпуска);
			ЗаполнитьВторичныеПризнакиОтпуска(СтрокаСотрудника.ПолучитьИдентификатор(), ДанныеВидаОтпуска);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиПриОкончанииРедактированияНаСервере(ИдентификаторСтроки)
	
	ЗаполнитьВторичныеПризнакиОтпуска(ИдентификаторСтроки);
	ПодобратьОтпускСотрудника(ИдентификаторСтроки);
	УстановитьИнформационнуюНадпись();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеПризнакиОтпуска(ТекущийИдентификатор, ДанныеВидаОтпуска = Неопределено)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ТекущийИдентификатор);
	
	Если СтрокаСотрудника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеВидаОтпуска = Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаСотрудника.ВидОтпуска) Тогда
			ДанныеВидаОтпуска = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаСотрудника.ВидОтпуска, "ОтпускЯвляетсяЕжегодным, СпособРасчетаОтпуска, ОтпускБезОплаты");
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаСотрудника.ОтпускЯвляетсяЕжегодным 	= ДанныеВидаОтпуска.ОтпускЯвляетсяЕжегодным;
	СтрокаСотрудника.ОтпускБезСохраненияОплаты 	= ДанныеВидаОтпуска.ОтпускБезОплаты И Не ДанныеВидаОтпуска.ОтпускЯвляетсяЕжегодным;
	
	Если ДанныеВидаОтпуска.СпособРасчетаОтпуска = ПредопределенноеЗначение("Перечисление.СпособыРасчетаОтпуска.ВРабочихДнях") Тогда
		СтрокаСотрудника.СпособРасчетаПоКалендарнымДням = Ложь;
	Иначе
		СтрокаСотрудника.СпособРасчетаПоКалендарнымДням = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодобратьОтпускСотрудника(ИдентификаторСтроки)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если СтрокаСотрудника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСотрудника.Отпуск) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаСотрудника.Сотрудник) Или Не ЗначениеЗаполнено(СтрокаСотрудника.ВидОтпуска)
		Или Не ЗначениеЗаполнено(СтрокаСотрудника.ДатаНачала) Или Не ЗначениеЗаполнено(СтрокаСотрудника.ДатаОкончания) Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудник", СтрокаСотрудника.Сотрудник);
	Запрос.УстановитьПараметр("ВидОтпуска", СтрокаСотрудника.ВидОтпуска);
	Запрос.УстановитьПараметр("ДатаНачала", СтрокаСотрудника.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", СтрокаСотрудника.ДатаОкончания);
	
	Если СтрокаСотрудника.ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной") Тогда 
		Запрос.Текст = "ВЫБРАТЬ
		               |	Отпуск.Ссылка КАК Отпуск,
		               |	Отпуск.ДокументРассчитан КАК Рассчитан,
		               |	Отпуск.Рассчитал КАК Рассчитал,
		               |	Отпуск.Проведен КАК Проведен,
		               |	ИСТИНА КАК ВидДокументаОтпуск
		               |ИЗ
		               |	Документ.Отпуск КАК Отпуск
		               |ГДЕ
		               |	Отпуск.Сотрудник = &Сотрудник
		               |	И Отпуск.ДатаНачалаОсновногоОтпуска = &ДатаНачала
		               |	И Отпуск.ДатаОкончанияОсновногоОтпуска = &ДатаОкончания
		               |	И НЕ Отпуск.ПометкаУдаления
					   |
		               |УПОРЯДОЧИТЬ ПО
		               |	Отпуск.Проведен УБЫВ,
		               |	Отпуск.Ссылка";
	ИначеЕсли СтрокаСотрудника.ОтпускБезСохраненияОплаты Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ОтпускБезСохраненияОплаты.Ссылка КАК Отпуск,
		               |	ОтпускБезСохраненияОплаты.ПерерасчетВыполнен КАК Рассчитан,
		               |	ОтпускБезСохраненияОплаты.Рассчитал КАК Рассчитал,
		               |	ОтпускБезСохраненияОплаты.Проведен КАК Проведен,
		               |	ЛОЖЬ КАК ВидДокументаОтпуск
		               |ИЗ
		               |	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
		               |ГДЕ
		               |	ОтпускБезСохраненияОплаты.Сотрудник = &Сотрудник
		               |	И ОтпускБезСохраненияОплаты.ДатаНачала = &ДатаНачала
		               |	И ОтпускБезСохраненияОплаты.ДатаОкончания = &ДатаОкончания
		               |	И ОтпускБезСохраненияОплаты.ВидОтпуска = &ВидОтпуска
		               |	И НЕ ОтпускБезСохраненияОплаты.ПометкаУдаления
					   |
		               |УПОРЯДОЧИТЬ ПО
		               |	ОтпускБезСохраненияОплаты.Проведен УБЫВ,
		               |	ОтпускБезСохраненияОплаты.Ссылка";
	Иначе 
		Запрос.Текст = "ВЫБРАТЬ
		               |	ДополнительныеОтпуска.Ссылка.Ссылка КАК Отпуск,
		               |	ДополнительныеОтпуска.Ссылка.ДокументРассчитан КАК Рассчитан,
		               |	ДополнительныеОтпуска.Ссылка.Рассчитал КАК Рассчитал,
		               |	ДополнительныеОтпуска.Ссылка.Проведен КАК Проведен,
		               |	ИСТИНА КАК ВидДокументаОтпуск
		               |ИЗ
		               |	Документ.Отпуск.ДополнительныеОтпуска КАК ДополнительныеОтпуска
		               |ГДЕ
		               |	ДополнительныеОтпуска.Ссылка.Сотрудник = &Сотрудник
		               |	И ДополнительныеОтпуска.ДатаНачала = &ДатаНачала
		               |	И ДополнительныеОтпуска.ДатаОкончания = &ДатаОкончания
		               |	И ДополнительныеОтпуска.ВидОтпуска = &ВидОтпуска
		               |	И НЕ ДополнительныеОтпуска.Ссылка.ПометкаУдаления
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ДополнительныеОтпуска.Ссылка.Проведен УБЫВ,
		               |	ДополнительныеОтпуска.Ссылка";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(СтрокаСотрудника, Выборка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбщиеДанныеЗаполнения(Объект, Сотрудник, ДокументОтпуск, ПараметрыЗаполнения = Неопределено)

	Если ПараметрыЗаполнения  = Неопределено Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("ВидДокументаОтпуск", ТипЗнч(ДокументОтпуск) = Тип("ДокументСсылка.Отпуск"));
		ПараметрыЗаполнения.Вставить("ВидДокументаПрежнийОтпуск", Неопределено);
	КонецЕсли;
	
	ПараметрыЗаполнения.Вставить("Действие", "Заполнить");
	ПараметрыЗаполнения.Вставить("ДокументОтпуск", ДокументОтпуск);
	ПараметрыЗаполнения.Вставить("Организация", Объект.Организация);
	ПараметрыЗаполнения.Вставить("Сотрудник", Сотрудник);
	ПараметрыЗаполнения.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыЗаполнения.Вставить("Руководитель", Объект.Руководитель);
	ПараметрыЗаполнения.Вставить("ДолжностьРуководителя", Объект.ДолжностьРуководителя);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДанныеЗаполненияСтроки(СтрокаОтпуска)
	
	СтруктураОтпуска = Новый Структура;
	
	СтруктураОтпуска.Вставить("ВидОтпуска", СтрокаОтпуска.ВидОтпуска);
	СтруктураОтпуска.Вставить("ДатаНачала", СтрокаОтпуска.ДатаНачала);
	СтруктураОтпуска.Вставить("ДатаОкончания", СтрокаОтпуска.ДатаОкончания);
	СтруктураОтпуска.Вставить("КоличествоДней", СтрокаОтпуска.КоличествоДней);
	СтруктураОтпуска.Вставить("КоличествоДнейКомпенсации", СтрокаОтпуска.КоличествоДнейКомпенсации);
	СтруктураОтпуска.Вставить("НачалоПериодаЗаКоторыйПредоставляетсяОтпуск", СтрокаОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск);
	СтруктураОтпуска.Вставить("КонецПериодаЗаКоторыйПредоставляетсяОтпуск", СтрокаОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск);
	СтруктураОтпуска.Вставить("Основание", СтрокаОтпуска.Основание);
	СтруктураОтпуска.Вставить("ПредоставитьЕдиновременнуюВыплатуКОтпуску", СтрокаОтпуска.ПредоставитьЕдиновременнуюВыплатуКОтпуску);
	СтруктураОтпуска.Вставить("ВидОтпускаПрежний", СтрокаОтпуска.ВидОтпускаПрежний);
	СтруктураОтпуска.Вставить("ИндексСтрокиДокумента", СтрокаОтпуска.НомерСтроки - 1);
	СтруктураОтпуска.Вставить("ОтпускБезСохраненияОплаты", СтрокаОтпуска.ОтпускБезСохраненияОплаты);
	
	Возврат СтруктураОтпуска;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОтпускКДокументу(ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗаписатьДокумент", Истина);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Оформить отпуск можно только после записи этого документа.
							|Записать этот документ?'");
							
		Оповещение = Новый ОписаниеОповещения("ДобавитьОтпускКДокументуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
		
	Иначе
		
		ДополнительныеПараметры.ЗаписатьДокумент = Ложь;
		ДобавитьОтпускКДокументуЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтпускКДокументуЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗаписатьДокумент И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ДобавитьОтпускКДокументуНаСервере();
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
		ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОтпускКДокументуНаСервере()
	
	ТекущаяСтрока = Элементы.Сотрудники.ТекущаяСтрока;
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		ПодобратьОтпускСотрудника(ТекущаяСтрока);
		Если ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		
		ПараметрыЗаполненияСтроки = ПараметрыЗаполнения(ТекущиеДанные.Сотрудник, Элементы.Сотрудники.ТекущаяСтрока);
		Если ПараметрыЗаполненияСтроки.Количество() > 0 Тогда
			РезультатЗаполнения = РасчетныйДокументПоПараметрыЗаполнения(ТекущиеДанные.Сотрудник, ПараметрыЗаполненияСтроки[0]);
			Если РезультатЗаполнения <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ТекущиеДанные, РезультатЗаполнения);
				ЗаполнитьПрежниеЗначенияСтроки(ТекущиеДанные);
				Модифицированность = Истина;
				// Если добавление отпуска доступно, но нет прав на изменение 
				// документа - записываем в привилегированном режиме.
				Если ТолькоПросмотр Тогда 
					УстановитьПривилегированныйРежим(Истина);
					Записать();
					УстановитьПривилегированныйРежим(Ложь);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасчетныеДокументыНаКлиенте() Экспорт
	
	ДополнительныеПараметры = Новый Структура("ЗаписатьДокумент", Истина);
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru='Оформить отпуска можно только после записи этого документа.
							|Записать этот документ?'");
							
		Оповещение = Новый ОписаниеОповещения("СформироватьРасчетныеДокументыНаКлиентеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
		
	Иначе
		
		ДополнительныеПараметры.ЗаписатьДокумент = Ложь;
		СформироватьРасчетныеДокументыНаКлиентеЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасчетныеДокументыНаКлиентеЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗаписатьДокумент И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	СформироватьРасчетныеДокументыНаСервере();
	ПоказатьПредупреждение(, НСтр("ru = 'Оформление отпусков завершено'"));
	ОповеститьОЗаписиДокументаОтпуск();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоДнейНаСервере(ТекущийИдентификатор)
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(ТекущийИдентификатор);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеВидаОтпуска = ОстаткиОтпусков.ОписаниеВидаОтпуска(ТекущиеДанные.ВидОтпуска, ТекущиеДанные.ОтпускВРабочихДняхПоДоговору);
	ТекущиеДанные.КоличествоДней = УчетРабочегоВремениРасширенный.ДлительностьИнтервала(ТекущиеДанные.Сотрудник, ТекущиеДанные.ДатаНачала, ТекущиеДанные.ДатаОкончания, ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням, ОписаниеВидаОтпуска.ЕжегодныйОтпуск);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьКоличествоДнейНаСервере()
	
	УстановитьКоличествоДнейНаСервере(Элементы.Сотрудники.ТекущаяСтрока);
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	ДатаОкончания = ТекущиеДанные.ДатаОкончания;
	
	СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ТекущиеДанные.Сотрудник));
	ИндексСтрокиТекущегоОтпуска = СтрокиПоСотруднику.Найти(ТекущиеДанные);
	Если ИндексСтрокиТекущегоОтпуска <> Неопределено Тогда
		
		Для ИндексСтроки = ИндексСтрокиТекущегоОтпуска + 1 По СтрокиПоСотруднику.Количество() - 1 Цикл
			
			ДанныеСтроки = СтрокиПоСотруднику[ИндексСтроки];
			Если ДанныеСтроки.ДатаНачала <= ДатаОкончания Тогда
				
				ДанныеСтроки.ДатаНачала = КонецДня(ДатаОкончания) + 1;
				ДанныеСтроки.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(
					ДанныеСтроки.Сотрудник,
					ДанныеСтроки.ДатаНачала,
					ДанныеСтроки.КоличествоДней,
					Новый Структура("СпособРасчетаПоКалендарнымДням, ЕжегодныйОтпуск", ДанныеСтроки.СпособРасчетаПоКалендарнымДням, ДанныеСтроки.ОтпускЯвляетсяЕжегодным));
				ДатаОкончания = ДанныеСтроки.ДатаОкончания;
			Иначе
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнформационнуюНадпись()
	
	НеоформленныеОтпуска = Новый Массив;
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
		Если Не ЗначениеЗаполнено(СтрокаСотрудника.Отпуск) Тогда 
			НеоформленныеОтпуска.Добавить(СтрокаСотрудника);
		КонецЕсли;
	КонецЦикла;
	
	КоличествоСтрок = Объект.Сотрудники.Количество();
	КоличествоНеоформленных = НеоформленныеОтпуска.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяПусто";
	ИначеЕсли КоличествоНеоформленных = КоличествоСтрок Тогда
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяВсеНеОформлены";
	ИначеЕсли КоличествоНеоформленных > 0 Тогда
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяОформленыНеВсе";
	Иначе
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяВсеОформлены";
	КонецЕсли;
	
	ТекущаяСтраница = Элементы.Найти(ИмяИнформационнойСтраницы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаИнформация",
		"ТекущаяСтраница",
		ТекущаяСтраница);
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиКоличествоДнейКомпенсацииПриИзмененииНаСервере()
	
	Если Элементы.Сотрудники.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные)
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ВидОтпуска) Тогда
		
		ДанныеВидаОтпуска = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущиеДанные.ВидОтпуска, "ОтпускЯвляетсяЕжегодным, ОтпускБезОплаты");
		
		ТекущиеДанные.ОтпускЯвляетсяЕжегодным 	= ДанныеВидаОтпуска.ОтпускЯвляетсяЕжегодным;
		ТекущиеДанные.ОтпускБезСохраненияОплаты = ДанныеВидаОтпуска.ОтпускБезОплаты И Не ДанныеВидаОтпуска.ОтпускЯвляетсяЕжегодным;
		
	Иначе
		
		ТекущиеДанные.ОтпускЯвляетсяЕжегодным 	= Ложь;
		ТекущиеДанные.ОтпускБезСохраненияОплаты = Ложь;
		
	КонецЕсли;
	
	Если ТекущиеДанные.ОтпускЯвляетсяЕжегодным 
		И (ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала)
		И ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания)
		Или ЗначениеЗаполнено(ТекущиеДанные.КоличествоДнейКомпенсации)) Тогда
		
		СтруктураПараметров = ОстаткиОтпусков.ПараметрыПолученияРабочегоПериодаОтпуска();
		СтруктураПараметров.Сотрудник = ТекущиеДанные.Сотрудник;
		СтруктураПараметров.ТекущийРегистратор = Объект.Ссылка;
		СтруктураПараметров.ВидОтпуска = ТекущиеДанные.ВидОтпуска;
		СтруктураПараметров.ДатаНачала = ТекущиеДанные.ДатаНачала;
		СтруктураПараметров.ДатаОкончания = ТекущиеДанные.ДатаОкончания;
		СтруктураПараметров.ДатаКомпенсации = ?(ЗначениеЗаполнено(Объект.Дата), НачалоМесяца(Объект.Дата), НачалоМесяца(ТекущаяДатаСеанса()));
		СтруктураПараметров.КоличествоДнейКомпенсации = ТекущиеДанные.КоличествоДнейКомпенсации;
		ПериодОсновногоОтпуска = ОстаткиОтпусков.РабочийПериодОтпуска(СтруктураПараметров);
		
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск	= ПериодОсновногоОтпуска.РабочийГодС;
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск	= ПериодОсновногоОтпуска.РабочийГодПо;
		
	Иначе
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьРасчетныеДокументыНаСервере()
	
	НерассчитанныеСотрудники = Новый Соответствие;
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
		Если Не ЗначениеЗаполнено(СтрокаСотрудника.Отпуск) Тогда 
			СтрокиСотрудников = НерассчитанныеСотрудники[СтрокаСотрудника.Сотрудник];
			Если СтрокиСотрудников = Неопределено Тогда 
				СтрокиСотрудников = Новый Массив;
				НерассчитанныеСотрудники.Вставить(СтрокаСотрудника.Сотрудник, СтрокиСотрудников);
			КонецЕсли;
			СтрокиСотрудников.Добавить(СтрокаСотрудника);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из НерассчитанныеСотрудники Цикл
		Сотрудник = КлючИЗначение.Ключ;
		СтрокиСотрудника = КлючИЗначение.Значение;
		ПараметрыЗаполненияДокументов = ПараметрыЗаполнения(Сотрудник, , СтрокиСотрудника);
		Для Каждого ПараметрЗаполненияДокумента Из ПараметрыЗаполненияДокументов Цикл
			РезультатЗаполнения = РасчетныйДокументПоПараметрыЗаполнения(Сотрудник, ПараметрЗаполненияДокумента);
			Для Каждого ДанныеОтпуска Из ПараметрЗаполненияДокумента.ДанныеОтпусков Цикл
				Отбор = Новый Структура("Сотрудник,ДатаНачала", Сотрудник, ДанныеОтпуска.ДатаНачала);
				НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Отбор);
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
					НайденнаяСтрока.Отпуск = РезультатЗаполнения.Отпуск;
					ЗаполнитьПрежниеЗначенияСтроки(НайденнаяСтрока);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаполнитьРасчетныеДокументы();
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗаполнения(Сотрудник, ИдентификаторСтрокиСотрудника = Неопределено, НезаполненныеСтроки = Неопределено)
	
	ПараметрыЗаполненияДокументов = Новый Массив;
	
	// Формирование массива обрабатываемых строк.
	Если ИдентификаторСтрокиСотрудника = Неопределено Тогда
		СтрокиСотрудника = НезаполненныеСтроки;
	Иначе 
		СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтрокиСотрудника);
		Если СтрокаСотрудника <> Неопределено Тогда
			СтрокиСотрудника = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаСотрудника);
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокиСотрудника = Неопределено Тогда 
		Возврат ПараметрыЗаполненияДокументов;
	КонецЕсли;
	
	Если СтрокиСотрудника.Количество() > 0 Тогда
		
		СтрокиПоДокументам = Новый Соответствие;
		
		// Формирования списка уже оформленных документов Отпуск.
		ВсеСтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
		
		ДокументыОтпуск = Новый Массив;
		Для Каждого СтрокаСотрудника Из ВсеСтрокиСотрудника Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаСотрудника.Отпуск) 
				Или (СтрокаСотрудника.Рассчитан И СтрокаСотрудника.Проведен)
				Или Не СтрокаСотрудника.ВидДокументаОтпуск Тогда
				Продолжить;
			КонецЕсли;
			
			ДокументыОтпуск.Добавить(СтрокаСотрудника.Отпуск);
			
			СтрокиПоДокументу = СтрокиПоДокументам[СтрокаСотрудника.Отпуск];
			Если СтрокиПоДокументу = Неопределено Тогда
				СтрокиПоДокументу = Новый Массив;
				СтрокиПоДокументам.Вставить(СтрокаСотрудника.Отпуск, СтрокиПоДокументу);
			КонецЕсли;
			СтрокиПоДокументу.Добавить(СтрокаСотрудника);
			
		КонецЦикла;
		
		// Получение данных оформленных документов.
		ИменаРеквизитовДокумента = "ДатаНачалаПериодаОтсутствия,ДатаОкончанияПериодаОтсутствия,Проведен";
		ДанныеДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДокументыОтпуск, ИменаРеквизитовДокумента);
		
		Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл
			ДанныеДокумента.Значение.Вставить("БезОплаты", Ложь);
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОтпускДополнительныеОтпуска.Ссылка,
		|	ОтпускДополнительныеОтпуска.ВидОтпуска
		|ИЗ
		|	Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
		|ГДЕ
		|	ОтпускДополнительныеОтпуска.Ссылка В(&ДокументыОтпуск)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Отпуск.Ссылка,
		|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной)
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	Отпуск.Ссылка В(&ДокументыОтпуск)
		|	И Отпуск.ПредоставитьОсновнойОтпуск
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтпускБезСохраненияОплаты.Ссылка,
		|	ОтпускБезСохраненияОплаты.ВидОтпуска
		|ИЗ
		|	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
		|ГДЕ
		|	ОтпускБезСохраненияОплаты.Ссылка В(&ДокументыОтпуск)";
		
		Запрос.УстановитьПараметр("ДокументыОтпуск", ДокументыОтпуск);
		
		ВидыОтпусковДокументов  = Новый Соответствие;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			ВидыОтпусков = ВидыОтпусковДокументов[Выборка.Ссылка];
			Если ВидыОтпусков = Неопределено Тогда 
				ВидыОтпусков = Новый Массив;
				ВидыОтпусковДокументов.Вставить(Выборка.Ссылка, ВидыОтпусков);
			КонецЕсли;
			ВидыОтпусков.Добавить(Выборка.ВидОтпуска);
		КонецЦикла;
			
		// Отнесение обрабатываемых строк к оформленному документу.
		Для Каждого СтрокаСотрудника Из СтрокиСотрудника Цикл
			
			ПодходящийДокумент = Неопределено;
			Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл
				
				ВидыОтпусков = ВидыОтпусковДокументов[ДанныеДокумента.Ключ];
				Если ВидыОтпусков <> Неопределено И ВидыОтпусков.Найти(СтрокаСотрудника.ВидОтпуска) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ЗначенияРеквизитовДокумента = ДанныеДокумента.Значение;
				Если (КонецДня(ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия) + 1 = СтрокаСотрудника.ДатаНачала И Не ЗначенияРеквизитовДокумента.БезОплаты)
					Или (КонецДня(СтрокаСотрудника.ДатаОкончания) + 1 = ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия И Не СтрокаСотрудника.ОтпускБезСохраненияОплаты) Тогда
					
					ЗначенияРеквизитовДокумента.БезОплаты = Ложь;
					
					ПодходящийДокумент = ДанныеДокумента.Ключ;
					Если ЗначенияРеквизитовДокумента.Проведен = Истина Тогда
						Прервать;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			// Обновление данных оформленного документа, если подходящего документа нет
			// отпуск относится к пустому документу.
			ЗначенияРеквизитовДокумента = ДанныеДокументов.Получить(ПодходящийДокумент);
			Если ЗначенияРеквизитовДокумента = Неопределено Тогда
				
				ЗначенияРеквизитовДокумента = Новый Структура(ИменаРеквизитовДокумента);
				ЗначенияРеквизитовДокумента.Вставить("БезОплаты", СтрокаСотрудника.ОтпускБезСохраненияОплаты);
				
				ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия = СтрокаСотрудника.ДатаНачала;
				ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия = СтрокаСотрудника.ДатаОкончания;
				
			Иначе
				
				Если КонецДня(ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия) + 1 = СтрокаСотрудника.ДатаНачала Тогда
					ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия = СтрокаСотрудника.ДатаОкончания;
				ИначеЕсли КонецДня(СтрокаСотрудника.ДатаОкончания) + 1 = ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия Тогда
					ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия = СтрокаСотрудника.ДатаНачала;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначенияРеквизитовДокумента.Свойство("МассивСтрокДокумента") Тогда
				МассивСтрокДокумента = ЗначенияРеквизитовДокумента.МассивСтрокДокумента;
			Иначе
				МассивСтрокДокумента = Новый Массив;
			КонецЕсли;
			
			МассивСтрокДокумента.Добавить(СтрокаСотрудника);
			ЗначенияРеквизитовДокумента.Вставить("МассивСтрокДокумента", МассивСтрокДокумента);
			
			// Если подходящего документа не было найдено, создадим уникальный ключ
			// для списка связанных датами строк.
			Если ПодходящийДокумент = Неопределено Тогда
				ПодходящийДокумент = Новый УникальныйИдентификатор();
				ВидыОтпусковДокументов.Вставить(ПодходящийДокумент, Новый Массив);
			КонецЕсли;
			
			ВидыОтпусков = ВидыОтпусковДокументов[ПодходящийДокумент];
			Если ВидыОтпусков <> Неопределено Тогда 
				ВидыОтпусков.Добавить(СтрокаСотрудника.ВидОтпуска);
			КонецЕсли;
			
			ДанныеДокументов.Вставить(ПодходящийДокумент, ЗначенияРеквизитовДокумента);
			
		КонецЦикла;
		
		// Формирование списка параметров заполнения документов Отпуск.
		Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл
			
			Если Не ДанныеДокумента.Значение.Свойство("МассивСтрокДокумента") Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыЗаполнения = Новый Структура();
			
			ДанныеОтпусков = Новый Массив;
			Для Каждого СтрокаДокумента Из ДанныеДокумента.Значение.МассивСтрокДокумента Цикл
				ДанныеОтпусков.Добавить(ДанныеЗаполненияСтроки(СтрокаДокумента));
			КонецЦикла;
			
			СтрокиПоДокументу = СтрокиПоДокументам[ДанныеДокумента.Ключ];
			Если СтрокиПоДокументу <> Неопределено Тогда
				Для Каждого СтрокаПоДокументу Из СтрокиПоДокументу Цикл 
					ДанныеОтпусков.Добавить(ДанныеЗаполненияСтроки(СтрокаПоДокументу));
				КонецЦикла;
			КонецЕсли;
			
			ВидДокументаОтпуск = ДанныеОтпусков.Количество() > 1 Или Не ДанныеОтпусков[0].ОтпускБезСохраненияОплаты;
			ПараметрыЗаполнения.Вставить("ВидДокументаОтпуск", ВидДокументаОтпуск);
			
			ВидДокументаПрежнийОтпуск = Неопределено;
			Если ТипЗнч(ДанныеДокумента.Ключ) = Тип("УникальныйИдентификатор") Тогда
				ДокументОтпуск = Документы[ИмяДокументаОтпуск(ВидДокументаОтпуск)].ПустаяСсылка();
			Иначе
				ДокументОтпуск = ДанныеДокумента.Ключ;
				ВидДокументаПрежнийОтпуск = ТипЗнч(ДанныеДокумента.Ключ) = Тип("ДокументСсылка.Отпуск");
			КонецЕсли;
			
			ПараметрыЗаполнения.Вставить("ВидДокументаПрежнийОтпуск", ВидДокументаПрежнийОтпуск);
			ПараметрыЗаполнения = ОбщиеДанныеЗаполнения(Объект, Сотрудник, ДокументОтпуск, ПараметрыЗаполнения);
			
			ПараметрыЗаполнения.Вставить("ДанныеОтпусков", ДанныеОтпусков);
			ПараметрыЗаполненияДокументов.Добавить(ПараметрыЗаполнения);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПараметрыЗаполненияДокументов;
	
КонецФункции

&НаСервере
Функция РасчетныйДокументПоПараметрыЗаполнения(Сотрудник, ПараметрыЗаполнения)
	
	РезультатЗаполнения = Новый Структура("Рассчитан,Проведен,ОтпускРассчитал,Отпуск", Ложь, Ложь);
	УстановитьПривилегированныйРежим(Истина);
	
	ПодходящийДокумент 		= ПараметрыЗаполнения.ДокументОтпуск;
	ИмяДокумента	   		= ИмяДокументаОтпуск(ПараметрыЗаполнения.ВидДокументаОтпуск);
	ИмяРеквизитаРассчитан 	= ?(ПараметрыЗаполнения.ВидДокументаОтпуск, "ДокументРассчитан", "ПерерасчетВыполнен");
	
	Если ЗначениеЗаполнено(ПодходящийДокумент) Тогда
		СоздатьНовыйДокумент = Ложь;
		
		Если ПараметрыЗаполнения.ВидДокументаПрежнийОтпуск <> Неопределено 
			И ПараметрыЗаполнения.ВидДокументаОтпуск <> ПараметрыЗаполнения.ВидДокументаПрежнийОтпуск Тогда
			
			ДокументОбъект = ПодходящийДокумент.ПолучитьОбъект();
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
			
			СоздатьНовыйДокумент = Истина;
			
		КонецЕсли;
		
		Если СоздатьНовыйДокумент Тогда
			ДокументОтпуск = Документы[ИмяДокумента].СоздатьДокумент();
		Иначе
			ДокументОтпуск = ПодходящийДокумент.ПолучитьОбъект();
		КонецЕсли;
	Иначе
		ДокументОтпуск = Документы[ИмяДокумента].СоздатьДокумент();
	КонецЕсли;
	
	ДокументОтпуск.Заполнить(ПараметрыЗаполнения);
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьМногофункциональностьДокументовЗарплатаКадры") Тогда
		ДокументОтпуск[ИмяРеквизитаРассчитан] = Ложь;
	КонецЕсли;
	
	Отказ = Ложь;
	
	Документы[ИмяДокумента].ПроверитьРаботающих(ДокументОтпуск, Отказ);
	
	Если Не Отказ Тогда
		ДокументОтпуск.Записать(РежимЗаписиДокумента.Запись);
		Если ПолучитьФункциональнуюОпцию("ИспользоватьМногофункциональностьДокументовЗарплатаКадры") Тогда
			Попытка
				Если Не ТранзакцияАктивна() Или ДокументОтпуск.Проведен Тогда
					ДокументОтпуск.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли;
			Исключение
				Инфо = ИнформацияОбОшибке();
				ВызватьИсключение НСтр("ru='Не удалось записать '") + Строка(ДокументОтпуск.Ссылка);
			КонецПопытки;
		КонецЕсли;
	Иначе
		СообщенияПроверкиЗаполнения = ПолучитьСообщенияПользователю(Истина);
		Если СообщенияПроверкиЗаполнения <> Неопределено Тогда
			Для Каждого СообщениеПроверки Из СообщенияПроверкиЗаполнения Цикл
				Если СообщениеПроверки.Поле = "Сотрудник" Тогда
					СообщениеПроверки.Поле = "Сотрудники[" + ПараметрыЗаполнения.ДанныеОтпусков[0].ИндексСтрокиДокумента + "]." + СообщениеПроверки.Поле;
				КонецЕсли;
				СообщениеПроверки.Сообщить();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	РезультатЗаполнения.Вставить("Отпуск", 				ДокументОтпуск.Ссылка);
	РезультатЗаполнения.Вставить("Проведен", 			ДокументОтпуск.Проведен);
	РезультатЗаполнения.Вставить("Рассчитан", 			ДокументОтпуск[ИмяРеквизитаРассчитан] И ДокументОтпуск.Проведен);
	РезультатЗаполнения.Вставить("ОтпускРассчитал", 	ДокументОтпуск.Рассчитал);
	РезультатЗаполнения.Вставить("ВидДокументаОтпуск", 	ПараметрыЗаполнения.ВидДокументаОтпуск);
	
	УстановитьПривилегированныйРежим(Ложь);

	Возврат РезультатЗаполнения;
	
КонецФункции

&НаСервере
Функция ДанныеОРасхожденияхСУчетом(ДатыОстатков)
	
	Расхождения = Новый Массив;
	
	ТаблицаСотрудников = Объект.Сотрудники.Выгрузить(, "Сотрудник");
	ТаблицаСотрудников.Свернуть("Сотрудник");
	Для Каждого СтрокСотрудника Из ТаблицаСотрудников Цикл
		СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник,ОтпускЯвляетсяЕжегодным", СтрокСотрудника.Сотрудник, Истина));
		Если СтрокиПоСотруднику.Количество() > 0 Тогда
			ДатаОстатков = '00010101';
			Для Каждого СтрокаПоСотруднику Из СтрокиПоСотруднику Цикл
				ДатаОстатков = Макс(СтрокаПоСотруднику.ДатаНачала, ДатаОстатков);
			КонецЦикла;
			Если Не ЗначениеЗаполнено(ДатаОстатков) Тогда 
				ДатаОстатков = Объект.Дата;
			КонецЕсли;
			ДатыОстатков.Вставить(СтрокСотрудника.Сотрудник, ДатаОстатков);
			ОстаткиОтпуска = ОстаткиОтпусков.ОстатокОтпускаСотрудникаНаДату(СтрокСотрудника.Сотрудник, ДатаОстатков, Объект.Ссылка);
			Для Каждого СтрокаПоСотруднику Из СтрокиПоСотруднику Цикл
				СтрокаСоответствует = Неопределено;
				Для Каждого ОстатокОтпуска Из ОстаткиОтпуска.ОстаткиВРазрезеВидовОтпусков Цикл
					Если СтрокаПоСотруднику.ВидОтпуска = ОстатокОтпуска.ВидЕжегодногоОтпуска Тогда
						СтрокаСоответствует = Истина;
						Если СтрокаПоСотруднику.КоличествоДней <> ОстатокОтпуска.КоличествоДней Тогда
							СтрокаСоответствует = Ложь;
							ОписаниеОшибки = Новый Структура("Сотрудник,ВидОтпуска");
							ЗаполнитьЗначенияСвойств(ОписаниеОшибки, СтрокаПоСотруднику);
							Если СтрокаПоСотруднику.КоличествоДней > ОстатокОтпуска.КоличествоДней Тогда
								ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Предоставляется отпуск на %1 дн. больше, чем осталось по данным учета'"),
									СтрокаПоСотруднику.КоличествоДней - ОстатокОтпуска.КоличествоДней);
							Иначе
								ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='По данным учета остаток дней отпуска на %1 дн. больше'"),
									ОстатокОтпуска.КоличествоДней - СтрокаПоСотруднику.КоличествоДней);
							КонецЕсли;
							ОписаниеОшибки.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
							Расхождения.Добавить(ОписаниеОшибки);
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если СтрокаСоответствует = Неопределено Тогда
					ОписаниеОшибки = Новый Структура("Сотрудник,ВидОтпуска");
					ЗаполнитьЗначенияСвойств(ОписаниеОшибки, СтрокаПоСотруднику);
					ПредставлениеОшибки = НСтр("ru='Не найдено остатков отпуска'");
					ОписаниеОшибки.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
					Расхождения.Добавить(ОписаниеОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Расхождения;
	
КонецФункции

&НаСервере
Процедура ОтменитьДокумент(ИдентификаторТекущейСтроки, Отказ)
	
	Если ИдентификаторТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	
	Попытка
		
		ДокументОбъект = ТекущиеДанные.Отпуск.ПолучитьОбъект();
		
		Если ТекущиеДанные.ВидДокументаОтпуск Тогда
			
			Если ТекущиеДанные.ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной") Тогда
				
				ДокументОбъект.ПредоставитьОсновнойОтпуск = Ложь;
				ДокументОбъект.ДатаНачалаОсновногоОтпуска = '00010101';
				ДокументОбъект.ДатаОкончанияОсновногоОтпуска = '00010101';
				ДокументОбъект.КоличествоДнейОсновногоОтпуска = 0;
				ДокументОбъект.КоличествоДнейКомпенсацииОсновногоОтпуска = 0;
				
			Иначе
				
				СтрокиДополнительных = ДокументОбъект.ДополнительныеОтпуска.НайтиСтроки(Новый Структура("ВидОтпуска", ТекущиеДанные.ВидОтпуска));
				Для каждого СтрокаДопОтпуска Из СтрокиДополнительных Цикл
					ДокументОбъект.ДополнительныеОтпуска.Удалить(СтрокаДопОтпуска);
				КонецЦикла;
				
				Если ДокументОбъект.ДополнительныеОтпуска.Количество() = 0 Тогда
					ДокументОбъект.ПредоставитьДополнительныйОтпуск = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
			Если НЕ ДокументОбъект.ПредоставитьОсновнойОтпуск И НЕ ДокументОбъект.ПредоставитьДополнительныйОтпуск Тогда
				ДокументОбъект.ПометкаУдаления = Истина;
			КонецЕсли;
			
			ДокументОбъект.ДокументРассчитан = Ложь;
			
		Иначе
			
			ДокументОбъект.ПометкаУдаления = Истина;
			ДокументОбъект.ПерерасчетВыполнен = Ложь;
		
		КонецЕсли;
		
		ДокументОбъект.Записать(?(ДокументОбъект.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись));
		
	Исключение
		
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru='Не удалось отменить отпуск. '") + ИнформацияОбОшибке().Описание, , , ,Отказ);
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьФорму()
	
	УстановитьФункциональныеОпцииФормы();
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура СотрудникиВидОтпускаПриИзмененииНаСервере(ИдентификаторСтроки = Неопределено)
	
	Если ИдентификаторСтроки = Неопределено Тогда
		ИдентификаторСтроки = Элементы.Сотрудники.ТекущаяСтрока;
	КонецЕсли;
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		Отбор = Новый Структура("Отпуск", ТекущиеДанные.Отпуск);
		СтрокиОтпуска = Объект.Сотрудники.НайтиСтроки(Отбор);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(СтрокиОтпуска, ТекущиеДанные);
		Если СтрокиОтпуска.Количество() > 0 Тогда 
			Для Каждого СтрокаОтпуска Из СтрокиОтпуска Цикл 
				СтрокаОтпуска.ДатаОкончанияПрежняя = Неопределено;
			КонецЦикла;
			ТекущиеДанные.Отпуск = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ВидОтпуска) Тогда
		
		ТекущиеДанные.ОтпускЯвляетсяЕжегодным = Ложь;
		ТекущиеДанные.СпособРасчетаПоКалендарнымДням = Ложь;
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.ОтпускБезСохраненияОплаты = Ложь;
		ТекущиеДанные.ВидДокументаОтпуск = Истина;
		
	Иначе
		ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	КонецЕсли;
	
	СформироватьКоличествоДнейНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОЗаписиДокументаОтпуск()
	
	Оповестить ("Запись_Отпуск");
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДокументаОтпуск(ВидДокументаОтпуск)
	
	Возврат ?(ВидДокументаОтпуск, "Отпуск", "ОтпускБезСохраненияОплаты");
	
КонецФункции

&НаСервере
Процедура ОбработатьОбщиеПериодыОтпусков(Результат)
	
	ОбщиеПериоды = ПолучитьИзВременногоХранилища(Результат.АдресВХранилище);
	ОбщиеПериоды.Сортировать("Сотрудник, Начало");
	
	ДатыОстатков = Новый Соответствие;
	Для Каждого ПериодОтпуска Из ОбщиеПериоды Цикл 
		ДатыОстатков.Вставить(ПериодОтпуска.Сотрудник, ПериодОтпуска.Начало - 86400);
	КонецЦикла;
	
	СотрудникиПоДатамОстатков = Новый Соответствие;
	Для Каждого КлючИЗначение Из ДатыОстатков Цикл
		СписокСотрудников = СотрудникиПоДатамОстатков[КлючИЗначение.Значение];
		Если СписокСотрудников = Неопределено Тогда
			СписокСотрудников = Новый Массив;
			СотрудникиПоДатамОстатков.Вставить(КлючИЗначение.Значение, СписокСотрудников);
		КонецЕсли;
		СписокСотрудников.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	ОстаткиОтпусковСотрудников = Неопределено;
	Для Каждого КлючИЗначение Из СотрудникиПоДатамОстатков Цикл
		ПараметрыОстатков = ОстаткиОтпусков.ОписаниеПараметровДляОстаткиОтпусков();
		ПараметрыОстатков.Сотрудники = КлючИЗначение.Значение;
		ПараметрыОстатков.ДатаОстатков = КлючИЗначение.Ключ;
		Если ОстаткиОтпусковСотрудников = Неопределено Тогда
			ОстаткиОтпусковСотрудников = ОстаткиОтпусков.ОстаткиОтпусков(ПараметрыОстатков);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ОстаткиОтпусков.ОстаткиОтпусков(ПараметрыОстатков), ОстаткиОтпусковСотрудников);
		КонецЕсли;
	КонецЦикла;
	
	Если ОстаткиОтпусковСотрудников = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОстаткиОтпусковСотрудников.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число"));
	
	ОсновнойОтпуск = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной");
	ОписаниеВидаОтпуска = ОстаткиОтпусков.ОписаниеВидаОтпуска(ОсновнойОтпуск);
	ОписанияВидовОтпусков = Новый Соответствие;
	
	НайденныеСтроки = ОстаткиОтпусковСотрудников.НайтиСтроки(Новый Структура("ВидОтпуска", ОсновнойОтпуск));
	Для Каждого СтрокаОстатка Из НайденныеСтроки Цикл 
		СтрокаОстатка.Приоритет = 1;
	КонецЦикла;
	
	ОстаткиОтпусковСотрудников.Сортировать("Сотрудник, РабочийГодДатаНачала, Приоритет Убыв, ВидОтпуска");
	ОстаткиОтпусковСотрудников.Индексы.Добавить("Сотрудник");
	
	УчестьВведенныеВДокументеОтпуска(ОстаткиОтпусковСотрудников, ДатыОстатков);
	
	Для Каждого ПериодОтпуска Из ОбщиеПериоды Цикл
		
		Отбор = Новый Структура("Сотрудник", ПериодОтпуска.Сотрудник);
		ОстаткиОтпусковСотрудника = ОстаткиОтпусковСотрудников.НайтиСтроки(Отбор);
		КоличествоДнейОтпуска = УчетРабочегоВремениРасширенный.ДлительностьИнтервала(
			ПериодОтпуска.Сотрудник,
			ПериодОтпуска.Начало,
			ПериодОтпуска.Окончание,
			ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням,
			ОписаниеВидаОтпуска.ЕжегодныйОтпуск);
		ВидыОтпусков = ОстаткиОтпусков.РаспределениеОбщегоПериодаОтпускаПоВидамОтпусков(ОстаткиОтпусковСотрудника, КоличествоДнейОтпуска);
		
		ПродолжительностьОтпуска = ВидыОтпусков[ОсновнойОтпуск];
		НачалоОтпуска = ПериодОтпуска.Начало;
		Если ПродолжительностьОтпуска <> Неопределено Тогда
			НоваяСтрока = Объект.Сотрудники.Добавить();
			НоваяСтрока.Сотрудник = ПериодОтпуска.Сотрудник;
			НоваяСтрока.ВидОтпуска = ОсновнойОтпуск;
			НоваяСтрока.ДатаНачала = ПериодОтпуска.Начало;
			НоваяСтрока.КоличествоДней = ПродолжительностьОтпуска;
			НоваяСтрока.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(ПериодОтпуска.Сотрудник, НоваяСтрока.ДатаНачала, НоваяСтрока.КоличествоДней, ОписаниеВидаОтпуска);
			НоваяСтрока.ОтпускЯвляетсяЕжегодным = ОписаниеВидаОтпуска.ЕжегодныйОтпуск;
			НоваяСтрока.СпособРасчетаПоКалендарнымДням = ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням;
			НоваяСтрока.ВидДокументаОтпуск = Истина;
			НачалоОтпуска = НоваяСтрока.ДатаОкончания + 86400;
			ЗаполнитьПериодыПредоставляемогоОтпуска(НоваяСтрока);
			ПодобратьОтпускСотрудника(НоваяСтрока.ПолучитьИдентификатор());
		КонецЕсли;
		
		Для Каждого КлючИЗначение Из ВидыОтпусков Цикл
			ВидОтпуска = КлючИЗначение.Ключ;
			ПродолжительностьОтпуска = КлючИЗначение.Значение;
			Если ВидОтпуска = ОсновнойОтпуск Тогда 
				Продолжить;
			КонецЕсли;
			ОписаниеВидаОтпуска = ОписанияВидовОтпусков[ВидОтпуска];
			Если ОписаниеВидаОтпуска = Неопределено Тогда
				ОписаниеВидаОтпуска = ОстаткиОтпусков.ОписаниеВидаОтпуска(ВидОтпуска);
				ОписанияВидовОтпусков.Вставить(ВидОтпуска, ОписаниеВидаОтпуска);
			КонецЕсли;
			НоваяСтрока = Объект.Сотрудники.Добавить();
			НоваяСтрока.Сотрудник = ПериодОтпуска.Сотрудник;
			НоваяСтрока.ВидОтпуска = ВидОтпуска;
			НоваяСтрока.ДатаНачала = НачалоОтпуска;
			НоваяСтрока.КоличествоДней = ПродолжительностьОтпуска;
			НоваяСтрока.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(ПериодОтпуска.Сотрудник, НоваяСтрока.ДатаНачала, НоваяСтрока.КоличествоДней, ОписаниеВидаОтпуска);
			НоваяСтрока.ОтпускЯвляетсяЕжегодным = ОписаниеВидаОтпуска.ЕжегодныйОтпуск;
			НоваяСтрока.СпособРасчетаПоКалендарнымДням = ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням;
			НоваяСтрока.ВидДокументаОтпуск = Истина;
			НачалоОтпуска = НоваяСтрока.ДатаОкончания + 86400;
			ЗаполнитьПериодыПредоставляемогоОтпуска(НоваяСтрока);
			ПодобратьОтпускСотрудника(НоваяСтрока.ПолучитьИдентификатор());
		КонецЦикла;
		
	КонецЦикла;
	
	УстановитьИнформационнуюНадпись();
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УчестьВведенныеВДокументеОтпуска(ОстаткиОтпусковСотрудников, ДатыОстатков)
	
	ОтпускаДокумента = Новый ТаблицаЗначений;
	ОтпускаДокумента.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ОтпускаДокумента.Колонки.Добавить("ВидОтпуска", Новый ОписаниеТипов("СправочникСсылка.ВидыОтпусков"));
	ОтпускаДокумента.Колонки.Добавить("КоличествоДней", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
		Если ДатыОстатков[СтрокаСотрудника.Сотрудник] <> Неопределено И Не СтрокаСотрудника.Проведен Тогда
			НоваяСтрока = ОтпускаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСотрудника);
			НоваяСтрока.КоличествоДней = СтрокаСотрудника.КоличествоДней + СтрокаСотрудника.КоличествоДнейКомпенсации;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтпускаДокумента.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОтпускаДокумента.Свернуть("Сотрудник, ВидОтпуска", "КоличествоДней");	
	
	ОстаткиОтпусков.УчестьВведенныеВДокументеОтпуска(ОстаткиОтпусковСотрудников, ОтпускаДокумента);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоГрафикуОтпусковНаСервере(Результат)
	
	Подразделение = Результат.Подразделение;
	НачалоПериода = НачалоМесяца(Результат.Месяц);
	ОкончаниеПериода = КонецМесяца(Результат.Месяц);
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.Организация = Объект.Организация;
	ПараметрыПолученияСотрудников.Подразделение = Результат.Подразделение;
	ПараметрыПолученияСотрудников.НачалоПериода = НачалоПериода;
	ПараметрыПолученияСотрудников.ОкончаниеПериода = ОкончаниеПериода;
	
	СотрудникиОрганизации = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудников);
	Сотрудники = СотрудникиОрганизации.ВыгрузитьКолонку("Сотрудник");
	
	ГрафикОтпусков = ОстаткиОтпусков.ГрафикОтпусковСотрудников(Сотрудники, НачалоПериода, ОкончаниеПериода);
	
	Если ГрафикОтпусков.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не обнаружено запланированных отпусков за выбранный период.'"));
		Возврат;
	КонецЕсли;
	
	Если Объект.Сотрудники.Количество() > 0 Тогда 
		СтрокиКУдалению = Новый Массив;
		Для Каждого ДанныеГрафика Из ГрафикОтпусков Цикл 
			Отбор = Новый Структура("Сотрудник, ВидОтпуска, ДатаНачала, ДатаОкончания");
			ЗаполнитьЗначенияСвойств(Отбор, ДанныеГрафика);
			НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокиКУдалению.Добавить(ДанныеГрафика);
			КонецЕсли;
		КонецЦикла;
		Для Каждого ДанныеГрафика Из СтрокиКУдалению Цикл 
			ГрафикОтпусков.Удалить(ДанныеГрафика);
		КонецЦикла;
	КонецЕсли;
	
	ОписанияВидовОтпусков = Новый Соответствие;
	Для Каждого ДанныеГрафика Из ГрафикОтпусков Цикл
		ОписаниеВидаОтпуска = ОписанияВидовОтпусков[ДанныеГрафика.ВидОтпуска];
		Если ОписаниеВидаОтпуска = Неопределено Тогда
			ОписаниеВидаОтпуска = ОстаткиОтпусков.ОписаниеВидаОтпуска(ДанныеГрафика.ВидОтпуска);
			ОписанияВидовОтпусков.Вставить(ДанныеГрафика.ВидОтпуска, ОписаниеВидаОтпуска);
		КонецЕсли;
		НоваяСтрока = Объект.Сотрудники.Добавить();
		НоваяСтрока.Сотрудник = ДанныеГрафика.Сотрудник;
		НоваяСтрока.ВидОтпуска = ДанныеГрафика.ВидОтпуска;
		НоваяСтрока.ДатаНачала = ДанныеГрафика.ДатаНачала;
		НоваяСтрока.ДатаОкончания = ДанныеГрафика.ДатаОкончания;
		НоваяСтрока.КоличествоДней = УчетРабочегоВремениРасширенный.ДлительностьИнтервала(ДанныеГрафика.Сотрудник, ДанныеГрафика.ДатаНачала, ДанныеГрафика.ДатаОкончания, ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням, ОписаниеВидаОтпуска.ЕжегодныйОтпуск);
		НоваяСтрока.ОтпускЯвляетсяЕжегодным = ОписаниеВидаОтпуска.ЕжегодныйОтпуск;
		НоваяСтрока.СпособРасчетаПоКалендарнымДням = ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням;
		НоваяСтрока.ВидДокументаОтпуск = Истина;
		ЗаполнитьПериодыПредоставляемогоОтпуска(НоваяСтрока);
		ПодобратьОтпускСотрудника(НоваяСтрока.ПолучитьИдентификатор());
	КонецЦикла;
	
	УстановитьИнформационнуюНадпись();
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Массив = Новый Массив;
	Массив.Добавить("Объект.Сотрудники");
	Возврат Массив;
КонецФункции

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация", НСтр("ru = 'организации'")));
	Возврат Массив;
КонецФункции

#КонецОбласти
