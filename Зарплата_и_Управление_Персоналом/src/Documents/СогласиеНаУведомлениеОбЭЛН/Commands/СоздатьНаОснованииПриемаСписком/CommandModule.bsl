#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(МассивОснований, ПараметрыВыполненияКоманды)
	Если МассивОснований.Количество() = 0 Тогда
		Возврат;
	Иначе
		МассивСогласий = СоздатьСогласия(МассивОснований);
		ОповеститьОбИзменении(Тип("ДокументСсылка.СогласиеНаУведомлениеОбЭЛН"));
		Оповестить("Запись_СогласиеНаУведомлениеОбЭЛН");
		Отбор = Новый Структура("Ссылка", МассивСогласий);
		ПараметрыФормы = Новый Структура("Отбор", Отбор);
		ОткрытьФорму("Документ.СогласиеНаУведомлениеОбЭЛН.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьСогласия(Знач МассивОснований)
	СозданныеДокументы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриемНаРаботуСписком.Организация КАК Организация,
	|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
	|	ТаблицаСотрудников.ДатаПриема КАК ДатаПриема
	|ПОМЕСТИТЬ СотрудникиИОрганизации
	|ИЗ
	|	Документ.ПриемНаРаботуСписком.Сотрудники КАК ТаблицаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботуСписком КАК ПриемНаРаботуСписком
	|		ПО ТаблицаСотрудников.Ссылка = ПриемНаРаботуСписком.Ссылка
	|ГДЕ
	|	ПриемНаРаботуСписком.Ссылка В(&МассивОснований)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиИОрганизации.Организация КАК Организация,
	|	СотрудникиИОрганизации.Сотрудник КАК Сотрудник,
	|	СотрудникиИОрганизации.ДатаПриема КАК ДатаПриема,
	|	СогласияНаУведомленияОбЭЛН.Согласие КАК Согласие,
	|	СогласияНаУведомленияОбЭЛН.Состояние КАК Состояние
	|ИЗ
	|	СотрудникиИОрганизации КАК СотрудникиИОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СогласияНаУведомленияОбЭЛН КАК СогласияНаУведомленияОбЭЛН
	|		ПО СотрудникиИОрганизации.Сотрудник = СогласияНаУведомленияОбЭЛН.Сотрудник
	|			И СотрудникиИОрганизации.Организация = СогласияНаУведомленияОбЭЛН.Организация";
	Запрос.УстановитьПараметр("МассивОснований", МассивОснований);
	
	Ответственный = Пользователи.ТекущийПользователь();
	ОтветственныйЗаОбработкуПерсональныхДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Ответственный, "ФизическоеЛицо");
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если СтрокаТаблицы.Состояние = Перечисления.СостоянияСогласийНаУведомленияОбЭЛН.ОжидаетПодписания
			Или СтрокаТаблицы.Состояние = Перечисления.СостоянияСогласийНаУведомленияОбЭЛН.Подписано Тогда
			ДокументОбъект = СтрокаТаблицы.Согласие.ПолучитьОбъект();
		Иначе
			ДокументОбъект = Документы.СогласиеНаУведомлениеОбЭЛН.СоздатьДокумент();
			ДокументОбъект.Дата = СтрокаТаблицы.ДатаПриема;
		КонецЕсли;
		ДокументОбъект.Страхователь   = СтрокаТаблицы.Организация;
		ДокументОбъект.Сотрудник     = СтрокаТаблицы.Сотрудник;
		ДокументОбъект.Ответственный = Ответственный;
		ДокументОбъект.ОтветственныйЗаОбработкуПерсональныхДанных = ОтветственныйЗаОбработкуПерсональныхДанных;
		ДокументОбъект.ОбновитьВторичныеДанные();
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		СозданныеДокументы.Добавить(ДокументОбъект.Ссылка);
		
	КонецЦикла;
	
	Возврат СозданныеДокументы;
КонецФункции

#КонецОбласти

