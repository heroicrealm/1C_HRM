#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ИзвещениеФСС, ПараметрыВыполненияКоманды)
	Форма = ОбщегоНазначенияБЗККлиентСервер.ЗначениеСвойства(ПараметрыВыполненияКоманды, "Источник");
	ИдентификаторФормы = ОбщегоНазначенияБЗККлиентСервер.ЗначениеСвойства(Форма, "УникальныйИдентификатор");
	
	КонтекстКоманды = СерверныйКонтекстКоманды(ИзвещениеФСС, ИдентификаторФормы);
	
	Если КонтекстКоманды.ДанныеФайла = Неопределено Тогда
		Шаблон = НСтр("ru = 'Не найден файл ""%1"". Возможно извещение еще не загружено.'");
		ИнформированиеПользователяКлиент.Предупредить(СтрШаблон(Шаблон, Строка(ИзвещениеФСС)));
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбработчикВопросаОПодтвержденииПолучения", ЭтотОбъект, КонтекстКоманды);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(1, НСтр("ru = 'Подтвердить получение'"), , БиблиотекаКартинок.Успешно32);
	Кнопки.Добавить(2, НСтр("ru = 'Пропустить'"));
	Кнопки.Добавить(0, НСтр("ru = 'Отмена'"));
	
	Если Кнопки.НайтиПоЗначению(КонтекстКоманды.ОтветНаВопрос) <> Неопределено Тогда
		
		Результат = Новый Структура("Значение, БольшеНеЗадаватьЭтотВопрос", КонтекстКоманды.ОтветНаВопрос, Ложь);
		ВыполнитьОбработкуОповещения(Обработчик, Результат);
		
	Иначе
		
		ТекстВопроса = НСтр("ru = 'В целях уменьшения бумажного документооборота рекомендуется подтвердить получение документа ""%1"".'");
		ТекстВопроса = СтрШаблон(ТекстВопроса, Строка(ИзвещениеФСС));
		
		Параметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		Параметры.Заголовок                            = НСтр("ru = 'Подтверждение получения'");
		Параметры.КнопкаПоУмолчанию                    = 1;
		Параметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос = КонтекстКоманды.ПравоСохранения;
		Параметры.Картинка = БиблиотекаКартинок.ПолезныйСовет32;
		
		// Процедура СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю не позволяет указывать текст флажка,
		// поэтому форма "Вопрос" открывается напрямую.
		Параметры.Вставить("ТекстФлажка",    НСтр("ru = 'Запомнить выбор'"));
		Параметры.Вставить("Кнопки",         Кнопки);
		Параметры.Вставить("ТекстСообщения", ТекстВопроса);
		
		ОткрытьФорму("ОбщаяФорма.Вопрос", Параметры, , , , , Обработчик);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СерверныйКонтекстКоманды(Знач ИзвещениеФСС, ИдентификаторФормы)
	Результат = Новый Структура("ИзвещениеФСС, ДанныеФайла, ПравоСохранения, ОтветНаВопрос");
	
	ИменаРеквизитов = "ВходящийФайл, ДатаОтправкиПодтверждения, Обработано, ПодтверждениеПолученоФСС, МаксимальнаяДатаПодтверждения";
	РеквизитыИзвещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИзвещениеФСС, ИменаРеквизитов);
	
	Результат.ИзвещениеФСС = ИзвещениеФСС;
	
	Если ЗначениеЗаполнено(РеквизитыИзвещения.ВходящийФайл) Тогда
		ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
		ДополнительныеПараметры.ИдентификаторФормы = ИдентификаторФормы;
		Результат.ДанныеФайла = РаботаСФайлами.ДанныеФайла(РеквизитыИзвещения.ВходящийФайл, ДополнительныеПараметры);
	КонецЕсли;
	
	Если Результат.ДанныеФайла = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.ПравоСохранения = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	
	Если РеквизитыИзвещения.Обработано
		Или ПодтверждениеОтправлено(РеквизитыИзвещения)
		Или Не ПодтверждениеТребуетсяОтправлять(РеквизитыИзвещения)
		Или Не ПравоДоступа("Изменение", Метаданные.Документы.ИзвещениеФСС)
		Или Не Результат.ПравоСохранения Тогда
		Результат.ОтветНаВопрос = 2; // Открыть документ без лишних вопросов.
	Иначе
		Результат.ОтветНаВопрос = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ПособияФСС.СЭДО",
			"ОтветНаВопросОПодтвержденииПолучения");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ПодтверждениеОтправлено(РеквизитыИзвещения)
	Возврат ЗначениеЗаполнено(РеквизитыИзвещения.ДатаОтправкиПодтверждения)
		Или РеквизитыИзвещения.ПодтверждениеПолученоФСС;
КонецФункции

&НаСервере
Функция ПодтверждениеТребуетсяОтправлять(РеквизитыИзвещения)
	Возврат ЗначениеЗаполнено(РеквизитыИзвещения.МаксимальнаяДатаПодтверждения);
КонецФункции

&НаКлиенте
Процедура ОбработчикВопросаОПодтвержденииПолучения(Ответ, КонтекстКоманды) Экспорт
	
	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответ.Значение = 1 Тогда
		Если Ответ.БольшеНеЗадаватьЭтотВопрос Тогда
			СохранитьОтветНаВопросОПодтвержденииПолучения(Ответ.Значение);
		КонецЕсли;
		Обработчик = Новый ОписаниеОповещения("ОбработчикОтправкиПодтвержденияПолучения", ЭтотОбъект, КонтекстКоманды);
		МассивСсылок = Новый Массив;
		МассивСсылок.Добавить(КонтекстКоманды.ИзвещениеФСС);
		СЭДОФССКлиент.ОтправитьПодтверждениеПолученияВыбранных(МассивСсылок, Обработчик, Ложь);
	ИначеЕсли Ответ.Значение = 2 Тогда
		Если Ответ.БольшеНеЗадаватьЭтотВопрос Тогда
			СохранитьОтветНаВопросОПодтвержденииПолучения(Ответ.Значение);
		КонецЕсли;
		РаботаСФайламиКлиент.ОткрытьФайл(КонтекстКоманды.ДанныеФайла, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОтправкиПодтвержденияПолучения(Результат, КонтекстКоманды) Экспорт
	РаботаСФайламиКлиент.ОткрытьФайл(КонтекстКоманды.ДанныеФайла, Ложь);
КонецПроцедуры

&НаСервере
Процедура СохранитьОтветНаВопросОПодтвержденииПолучения(ВариантОтвета)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПособияФСС.СЭДО",
		"ОтветНаВопросОПодтвержденииПолучения",
		ВариантОтвета);
КонецПроцедуры

#КонецОбласти

