
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Заголовок",			Заголовок);
	Параметры.Свойство("ОрганизацияСсылка",	ОрганизацияСсылка);
	
	Если ПустаяСтрока(Заголовок) Тогда
		Заголовок = Строка(ОрганизацияСсылка);
	КонецЕсли; 
		
	ПрочитатьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(
			ЭтаФорма, ОрганизацияСсылка, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	РедактированиеПериодическихСведений.ПроверитьЗаписьВФорме(ЭтаФорма, "СведенияОбОтветственныхЛицах", ОрганизацияСсылка, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент)
	
	ОписаниеФормыОбъекта = ОписаниеИменРеквизитовФормы();
	СтруктураОписание = ПодписиДокументовКлиент.ОписаниеПоИмениЭлемента(Элемент.Имя);
	ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация = СтруктураОписание.ИмяРеквизитаОрганизация;
	ОписаниеФормыОбъекта.ИмяПосадочнойГруппы = ПодписиДокументовКлиентСервер.ИмяПосадочнойГруппыПоОрганизации(СтруктураОписание.ИмяРеквизитаОрганизация);
	
	СтруктураОписанияПодписанта = ПодписиДокументовКлиент.СтруктураИменРеквизитовПодписанта(ЭтаФорма, СтруктураОписание.Ключ, ОписаниеФормыОбъекта);
	
	ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(ЭтаФорма, СтруктураОписанияПодписанта.ФизическоеЛицо);
	
	ДанныеФизическогоЛица = ПодписиДокументовВызовСервера.ОснованияПолномочийФизическихЛиц(ОрганизацияСсылка, ФизическоеЛицо).Получить(ФизическоеЛицо);
	ПодписиДокументовКлиентСервер.ЗаполнитьОснованияПолномочийВФорме(ЭтаФорма, СтруктураОписанияПодписанта, ДанныеФизическогоЛица);
	
	ПодписиДокументовКлиентСервер.УстановитьПредставлениеПодписей(ЭтаФорма, ОписаниеФормыОбъекта);
	ПодписиДокументовКлиентСервер.УстановитьЗаголовокГруппеПодписей(ЭтаФорма, ОписаниеФормыОбъекта);
	
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьОтображениеПолейВвода(
			ЭтаФорма, "СведенияОбОтветственныхЛицах", ОрганизацияСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент)
	
	ОписаниеФормыОбъекта = ОписаниеИменРеквизитовФормы();
	СтруктураОписание = ПодписиДокументовКлиент.ОписаниеПоИмениЭлемента(Элемент.Имя, "РасширеннаяПодсказка");
	ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация = СтруктураОписание.ИмяРеквизитаОрганизация;
	
	ОписаниеПодписи = ПодписиДокументовКлиент.СтруктураИменРеквизитовПодписанта(ЭтаФорма, СтруктураОписание.Ключ, ОписаниеФормыОбъекта);
	
	СтандартнаяОбработка = Истина;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодписиДокументовОснованияПолномочий") Тогда
		МодульПодписиДокументовОснованияПолномочийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодписиДокументовОснованияПолномочийКлиент");
		МодульПодписиДокументовОснованияПолномочийКлиент.ОткрытьФормуНастройкиОснованияПодписи(ЭтаФорма, Элемент.Имя, ОрганизацияСсылка, ОписаниеПодписи, СтандартнаяОбработка);
	КонецЕсли;

	Если СтандартнаяОбработка Тогда
		ПодписиДокументовКлиент.ОткрытьФормуВыбораДолжности(ЭтаФорма, Элемент.Имя, ОрганизацияСсылка, ОписаниеПодписи);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СведенияОбОтветственныхЛицахИстория(Команда)
	
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию(
			"СведенияОбОтветственныхЛицах", ОрганизацияСсылка, ЭтаФорма, ТолькоПросмотр);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьИЗакрытьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьДанные()
	
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(
			ЭтаФорма, "СведенияОбОтветственныхЛицах", ОрганизацияСсылка);
			
	// Создание и заполнение реквизитов и элементов
	
	СоздатьДополнительныеРеквизитыФормыПодписейДокументов(ЭтаФорма);
	ЗаполнитьДополнительныеРеквизитыФормыПодписей(ЭтаФорма);
	
	СоздатьРеквизитыИЭлементыПодписейДокументов(ЭтаФорма);
	
	ЗаполнитьОснованияПодписей(ЭтаФорма);
	
	ПодписиДокументовКлиентСервер.УстановитьПредставлениеПодписей(ЭтаФорма, ОписаниеИменРеквизитовФормы());
	
КонецПроцедуры

#Область ЗаписьОтветственныхЛиц

&НаСервере
Процедура ЗаписатьСведенияОбОтветственныхЛицах(ТекущийОбъектСсылка)
	
	РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(
			ЭтаФорма, "СведенияОбОтветственныхЛицах", ТекущийОбъектСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияСведенийОбОтветственныхЛицах(ДатаИзменения, Отказ, ОповещениеЗавершения)
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ЗапроситьРежимИзмененияСведенийОбОтветственныхЛицахЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстКнопкиДа = НСтр("ru = 'Изменились сведения об ответственных лицах'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'При редактировании Вы изменили сведения об ответственных лицах.
			|Если Вы исправили прежние сведения (они были ошибочны), нажмите ""Исправлена ошибка"".
			|Если сведения об ответственных лицах изменились с %1, нажмите ""%2""'"), 
		Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"),
		ТекстКнопкиДа);
	
	РедактированиеПериодическихСведенийКлиент.ЗапроситьРежимИзмененияРегистра(
			ЭтаФорма, "СведенияОбОтветственныхЛицах", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанные(Отказ, ОповещениеЗавершения = Неопределено)

	ЗапроситьРежимИзмененияСведенийОбОтветственныхЛицах(СведенияОбОтветственныхЛицах.Период, Отказ, ОповещениеЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияСведенийОбОтветственныхЛицахЗавершение(Отказ, ДополнительныеПараметры) Экспорт 
	
	СохранитьДанныеЗавершение(Отказ, ДополнительныеПараметры.ОповещениеЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанныеЗавершение(Отказ, ОповещениеЗавершения) 

	Если Не Отказ Тогда
		СохранитьДанныеНаСервере(Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПроверитьПараметрыПодключенияК1СОтчетности(СведенияОбОтветственныхЛицах.Руководитель, ЭтаФорма);
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПроверитьПараметрыПодключенияК1СОтчетности(СведенияОбОтветственныхЛицах.ГлавныйБухгалтер, ЭтаФорма);
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеНаСервере(Отказ)
	
	Если ПроверитьЗаполнение() Тогда
		ЗаписатьСведенияОбОтветственныхЛицах(ОрганизацияСсылка);
		Модифицированность = Ложь;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Модифицированность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат, ДополнительныеПараметры) Экспорт 
	
	СохранитьИЗакрытьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиенте()  

	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрытьНаКлиентеЗавершение", ЭтотОбъект);
	СохранитьДанные(Ложь, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиентеЗавершение(Отказ, ДополнительныеПараметры) Экспорт 

	Если Не Отказ Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область СозданиеЭлементовИРеквизитов
	
&НаСервереБезКонтекста
Процедура СоздатьДополнительныеРеквизитыФормыПодписейДокументов(Форма)

	// Стандартные реквизиты
	ПодписиДокументовФормы.СоздатьДополнительныеРеквизитыФормыПодписейДокументов(Форма);
	
	// Дополняем ИдентификаторыОтветственныхЛиц
	МассивРеквизитов = Новый Массив;
	
	// Список поддерживаемых идентификаторов.
	РеквизитСведенияОбОтветственныхРаботниках = Новый РеквизитФормы("ИдентификаторыОтветственныхЛиц", Новый ОписаниеТипов());
	МассивРеквизитов.Добавить(РеквизитСведенияОбОтветственныхРаботниках);
	
	// Создание реквизитов.
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, МассивРеквизитов, МассивИменРеквизитовФормы);

КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыИЭлементыПодписейДокументов(Форма)

	СоздатьРеквизитыПоПоддерживаемымИдентификаторам(Форма);
	
	СоздатьЭлементыПодписей(Форма);

КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыПоПоддерживаемымИдентификаторам(Форма)

	МассивРеквизитов = Новый Массив;
	
	// Заполнение массива создаваемых реквизитов.
	Для каждого ИдентификаторОтветственногоЛица Из Форма.ИдентификаторыОтветственныхЛиц Цикл
		ДополнитьМассивРеквизитовДолжностьюИОснованиемОтветственногоЛица(МассивРеквизитов, ИдентификаторОтветственногоЛица);
	КонецЦикла; 

	// Создание реквизитов.
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, МассивРеквизитов, МассивИменРеквизитовФормы);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьМассивРеквизитовДолжностьюИОснованиемОтветственногоЛица(МассивРеквизитов, ИдентификаторОтветственногоЛица)

	// Должность.
	МассивРеквизитов.Добавить(
		Новый РеквизитФормы(ИдентификаторОтветственногоЛица.Должность, Новый ОписаниеТипов("СправочникСсылка.Должности")));
	
	// Основание подписи.
	МассивРеквизитов.Добавить(
		Новый РеквизитФормы(ИдентификаторОтветственногоЛица.ОснованиеПодписи, Новый ОписаниеТипов("Строка")));

КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыПодписей(Форма)

	Для каждого ОписаниеПодписи Из Форма.ОписаниеПодписейДокумента Цикл
		ПодписиДокументовФормы.СоздатьЭлементПодписиВФорме(
			Форма,
			ОписаниеИменРеквизитовФормы(),
			ОписаниеПодписи,
			Элементы.ГруппаОтветственныеЛица);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область ЧтениеДанных

&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, ИмяРегистра, ВедущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизитыФормыПодписей(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		"ИдентификаторыОтветственныхЛиц",
		Новый ФиксированныйМассив(МассивИдентификаторовОтветственныхЛицОрганизаций()));
		
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		"ИспользоватьОснованияПолномочийПодписейДокументов",
		ПодписиДокументов.ИспользоватьОснованияПолномочий());
		
	// ЕстьПравоИзмененияОснованийПолномочий
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		"ЕстьПравоИзмененияОснованийПолномочий",
		ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ОснованияПолномочийОтветственныхЛиц));
		
	ЗаполнитьРеквизитОписаниеПодписейВФорме(Форма);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитОписаниеПодписейВФорме(Форма)

	ОписаниеПодписантов = ОписаниеПодписейОтветственныхЛиц(Форма);
	МассивСтруктурОписаний = ОбщегоНазначения.ТаблицаЗначенийВМассив(ОписаниеПодписантов);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		"ОписаниеПодписейДокумента",
		Новый ФиксированныйМассив(МассивСтруктурОписаний));

КонецПроцедуры

&НаСервере
Функция ОписаниеПодписейОтветственныхЛиц(Форма) 

	Подписанты = ПодписиДокументов.ОписаниеТаблицыПодписей();
	Для каждого ИдентификаторПодписи Из Форма.ИдентификаторыОтветственныхЛиц Цикл
		ПодписиДокументов.ДобавитьОписаниеПодписейОрганизации(Подписанты, ИдентификаторПодписи.Ключ);
	КонецЦикла; 
		
	Для каждого ОписаниеПодписанта Из Подписанты Цикл
		ОписаниеПодписанта.ФизическоеЛицо = "СведенияОбОтветственныхЛицах." + ОписаниеПодписанта.ФизическоеЛицо;
		ОписаниеПодписанта.Организация = "ОрганизацияСсылка";
	КонецЦикла; 
		
	Возврат Подписанты;

КонецФункции

&НаСервере
Процедура ЗаполнитьОснованияПодписей(Форма)

	Для каждого ОписаниеПодписанта Из Форма.ОписаниеПодписейДокумента Цикл
		ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПодписанта.ФизическоеЛицо);
		ДанныеФизическогоЛица = ПодписиДокументов.ОснованияПолномочийФизическихЛиц(Форма.ОрганизацияСсылка, ФизическоеЛицо).Получить(ФизическоеЛицо);
		
		ПодписиДокументовКлиентСервер.ЗаполнитьОснованияПолномочийВФорме(Форма, ОписаниеПодписанта, ДанныеФизическогоЛица);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеИменРеквизитовФормы()
	
	ОписаниеИменРеквизитов = ПодписиДокументовКлиентСервер.ОписаниеФормыОбъектаДляОрганизации();
	
	ОписаниеИменРеквизитов.ПрефиксФормыОбъект = "";
	ОписаниеИменРеквизитов.ИмяРеквизитаОрганизация = "ОрганизацияСсылка";
	ОписаниеИменРеквизитов.ИмяПосадочнойГруппы = "ГруппаОтветственныеЛица";
	
	Возврат ОписаниеИменРеквизитов;
	
КонецФункции

&НаСервереБезКонтекста
Функция МассивИдентификаторовОтветственныхЛицОрганизаций() 

	ТаблицаОтветственныхЛиц = СведенияОбОтветственныхЛицах.ТаблицаОтветственныхЛицОрганизаций();
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаОтветственныхЛиц);

КонецФункции

#КонецОбласти
