
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики) Экспорт
	
	//ПодключаемоеОборудование
	Обработчики.Вставить("РабочееМестоКлиента", "МенеджерОборудованияВызовСервера.УстановитьПараметрыСеансаПодключаемогоОборудования");
	//Конец ПодключаемоеОборудование
	
КонецПроцедуры

Процедура НастроитьСканерШтрихкода(Форма) Экспорт 
	
	ПользовательАвторизовался = ТипЗнч(Пользователи.АвторизованныйПользователь()) = Тип("СправочникСсылка.Пользователи");
	ИспользоватьСканерШтрихкода = ИспользоватьСканерШтрихкода();
	
	Форма.ИспользоватьПодключаемоеОборудование = ПользовательАвторизовался И ИспользоватьСканерШтрихкода;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИспользоватьСканерШтрихкода()
	
	УстановитьПривилегированныйРежим(Истина);
	
	РабочееМесто = МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента();
	
	Если Не ЗначениеЗаполнено(РабочееМесто) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
	Запрос.УстановитьПараметр("ТипОборудования", Перечисления.ТипыПодключаемогоОборудования.СканерШтрихкода);
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	               |ГДЕ
	               |	ПодключаемоеОборудование.УстройствоИспользуется
	               |	И НЕ ПодключаемоеОборудование.ПометкаУдаления
	               |	И ПодключаемоеОборудование.РабочееМесто = &РабочееМесто
	               |	И ПодключаемоеОборудование.ТипОборудования = &ТипОборудования";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти
