////////////////////////////////////////////////////////////////////////////////
// Подсистема "Займы сотрудникам".
// Процедуры и функции, предназначенные для обслуживания выданных сотрудникам займов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Определяет необходимость заполнения формы расчетов в документах выдачи/погашения займов.
//
// Параметры:
//	Организация
//
// Возвращаемое значение - булево.
//
Функция УточнятьФормуРасчетовПриВыдачеПогашенииЗайма(Организация) Экспорт
	
	// Уточняем форму расчетов, если используется обмен с бухгалтерией.
	
	ОбменИспользуется = Ложь;
	// ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиЗарплата3Бухгалтерия3");
		ОбменИспользуется = Модуль.ОбменИспользуется(Организация);
	КонецЕсли;
	// Конец ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3
	
	// ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиУниверсальныйФормат");
		ОбменИспользуется = ОбменИспользуется Или Модуль.ИспользуетсяОбменБП3(Организация);
	КонецЕсли;
	// Конец ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат

	
	Возврат ОбменИспользуется;
	
КонецФункции

// Возвращает массив документов ВыдачаЗаймаСотруднику, оформленных по договору.
//
// Параметры:
//	ДоговорЗайма - ссылка на документ ДоговорЗаймаСотруднику.
//
Функция ДокументыВыдачиЗайма(ДоговорЗайма) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДоговорЗайма", ДоговорЗайма);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВыдачаЗаймаСотруднику.Ссылка
	               |ИЗ
	               |	Документ.ВыдачаЗаймаСотруднику КАК ВыдачаЗаймаСотруднику
	               |ГДЕ
	               |	ВыдачаЗаймаСотруднику.Проведен
	               |	И ВыдачаЗаймаСотруднику.ДоговорЗайма = &ДоговорЗайма";
				   
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf6c-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ВыдачаЗаймаСотруднику);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "c091b4ed-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ПогашениеЗаймаСотруднику);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Документы.ВыдачаЗаймаСотруднику, Истина);
	Списки.Вставить(Метаданные.Справочники.ВыдачаЗаймаСотрудникуПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Документы.ПогашениеЗаймаСотруднику, Истина);
	Списки.Вставить(Метаданные.Справочники.ПогашениеЗаймаСотрудникуПрисоединенныеФайлы, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет объекты, в которых есть процедура ДобавитьКомандыПечати().
// Подробнее см. УправлениеПечатьюПереопределяемый.
//
// Параметры:
//  СписокОбъектов - Массив - список менеджеров объектов.
//
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.ВыдачаЗаймаСотруднику);
	СписокОбъектов.Добавить(Документы.ПогашениеЗаймаСотруднику);
	
КонецПроцедуры

#КонецОбласти