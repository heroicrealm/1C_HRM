////////////////////////////////////////////////////////////////////////////////
// ОрганизацииКлиентСервер: методы, обслуживающие работу формы организации и
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьОтображениеПоляИНН(Элемент, ИНН, ЮридическоеФизическоеЛицо, Форма, ОбособленноеПодразделение = Ложь) Экспорт
	
	СообщенияПроверки = "";
	Форма.ИННУказанПравильно = Ложь;
	
	ЭтоЮридическоеЛицо = (ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	
	Если Не ОбособленноеПодразделение И Не ПустаяСтрока(ИНН) Тогда
	
		Форма.ИННУказанПравильно = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ЭтоЮридическоеЛицо, СообщенияПроверки);
		
		СообщенияПроверки = ?(ПустаяСтрока(СообщенияПроверки), НСтр("ru = 'ИНН указан правильно'"), СообщенияПроверки);
		
		Если Форма.ИННУказанПравильно Тогда
			Картинка = БиблиотекаКартинок.ОперацияВыполненаУспешно;
			ТекстНадписи  = "";
			ЭлементЦветТекста = Форма.ЦветСтиляЦветТекстаПоля;
		Иначе
			Картинка = БиблиотекаКартинок.Предупреждение;
			ТекстНадписи  = НСтр("ru = 'Ошибка'");
			ЭлементЦветТекста = Форма.ЦветСтиляПоясняющийОшибкуТекст;
		КонецЕсли;
		
	Иначе
		
		Если ОбособленноеПодразделение Тогда
			СообщенияПроверки = "";
		Иначе
			СообщенияПроверки = НСтр("ru = 'Не указан ИНН'");
		КонецЕсли;
		 
		ТекстНадписи  = "";
		ЭлементЦветТекста = Форма.ЦветСтиляЦветТекстаПоля;
		Картинка = Новый Картинка;
			
	КонецЕсли;
	
	Форма.ОрганизацияИННКартинка = Картинка;
	Форма.ОрганизацияИНННадпись  = ТекстНадписи;
	Форма.Элементы.ОрганизацияИНННадпись.Подсказка = СообщенияПроверки;
	Форма.Элементы.ОрганизацияИНННадпись.ЦветТекста = ЭлементЦветТекста;
	Элемент.Подсказка  = СообщенияПроверки;
	Элемент.ЦветТекста = ЭлементЦветТекста;
	
КонецПроцедуры

Процедура ОбработатьОтображениеПоляОГРН(Элемент, ОГРН, ЮридическоеФизическоеЛицо, Форма) Экспорт
	
	СообщенияПроверки = "";
	Форма.ОГРНУказанПравильно = Ложь;
	
	ЭтоЮридическоеЛицо = (ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	
	Если НЕ ПустаяСтрока(ОГРН) Тогда
	
		Форма.ОГРНУказанПравильно = РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ОГРН, ЭтоЮридическоеЛицо, СообщенияПроверки);
		
		СообщенияПроверки = ?(ПустаяСтрока(СообщенияПроверки), НСтр("ru = 'ОГРН указан правильно'"), СообщенияПроверки);
		
		Если Форма.ОГРНУказанПравильно Тогда
			Картинка = БиблиотекаКартинок.ОперацияВыполненаУспешно;
			ТекстНадписи  = "";
			ЭлементЦветТекста = Форма.ЦветСтиляЦветТекстаПоля;
		Иначе
			Картинка = БиблиотекаКартинок.Предупреждение;
			ТекстНадписи  = НСтр("ru = 'Ошибка'");
			ЭлементЦветТекста = Форма.ЦветСтиляПоясняющийОшибкуТекст;
		КонецЕсли;
		
	Иначе
		
		СообщенияПроверки = НСтр("ru = 'Не указан ОГРН'");
		ТекстНадписи  = "";
		ЭлементЦветТекста = Форма.ЦветСтиляЦветТекстаПоля;
		Картинка = Новый Картинка;
			
	КонецЕсли;
	
	Форма.ОрганизацияОГРНКартинка = Картинка;
	Форма.ОрганизацияОГРННадпись  = ТекстНадписи;
	Форма.Элементы.ОрганизацияОГРННадпись.Подсказка = СообщенияПроверки;
	Форма.Элементы.ОрганизацияОГРННадпись.ЦветТекста = ЭлементЦветТекста;
	Элемент.Подсказка  = СообщенияПроверки;
	Элемент.ЦветТекста = ЭлементЦветТекста;
	
КонецПроцедуры

Процедура ОбработатьОтображениеПоляОКПО(Элемент, ОКПО, ЮридическоеФизическоеЛицо, ОбособленноеПодразделение, Форма) Экспорт
	
	СообщенияПроверки = "";
	Форма.ОКПОУказанПравильно = Ложь;
	
	ЭтоЮридическоеЛицо = (ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	
	Если НЕ ПустаяСтрока(ОКПО) Тогда
	
		Если ОбособленноеПодразделение Тогда
			Форма.ОКПОУказанПравильно = ИдентификационныйНомерСоответствуетТребованиям(ОКПО, СообщенияПроверки);
		Иначе
			Форма.ОКПОУказанПравильно = РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(ОКПО, ЭтоЮридическоеЛицо, СообщенияПроверки);
		КонецЕсли;
		
		СообщенияПроверки = ?(ПустаяСтрока(СообщенияПроверки), НСтр("ru = 'ОКПО указан правильно'"), СообщенияПроверки);
		
		Если Форма.ОКПОУказанПравильно Тогда
			Картинка = БиблиотекаКартинок.ОперацияВыполненаУспешно;
			ТекстНадписи  = "";
			ЭлементЦветТекста = Форма.ЦветСтиляЦветТекстаПоля;
		Иначе
			Картинка = БиблиотекаКартинок.Предупреждение;
			ТекстНадписи  = НСтр("ru = 'Ошибка'");
			ЭлементЦветТекста = Форма.ЦветСтиляПоясняющийОшибкуТекст;
		КонецЕсли;
		
	Иначе
		
		СообщенияПроверки = НСтр("ru = 'Не указан ОКПО'");
		ТекстНадписи  = "";
		ЭлементЦветТекста = Форма.ЦветСтиляЦветТекстаПоля;
		Картинка = Новый Картинка;
			
	КонецЕсли;
	
	Форма.ОрганизацияОКПОКартинка = Картинка;
	Форма.ОрганизацияОКПОНадпись  = ТекстНадписи;
	Форма.Элементы.ОрганизацияОКПОНадпись.Подсказка = СообщенияПроверки;
	Форма.Элементы.ОрганизацияОКПОНадпись.ЦветТекста = ЭлементЦветТекста;
	Элемент.Подсказка  = СообщенияПроверки;
	Элемент.ЦветТекста = ЭлементЦветТекста;
	
КонецПроцедуры

Функция ИдентификационныйНомерСоответствуетТребованиям(Знач ПроверяемыйКод, ТекстСообщения = "")
	
	ПроверяемыйКод = СокрЛП(ПроверяемыйКод);
	ТекстСообщения = "";
	ДлинаКода = СтрДлина(ПроверяемыйКод);

	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПроверяемыйКод) Тогда
		ТекстСообщения = ТекстСообщения + НСтр("ru = 'Код ОКПО должен состоять только из цифр.'") + Символы.ПС;
	КонецЕсли;

	Если ДлинаКода <> 14 Тогда
		ТекстСообщения = ТекстСообщения 
			+ НСтр("ru = 'Идентификационный номер Росстата обособленного подразделения должен состоять из 14 цифр.'") + Символы.ПС;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		ТекстСообщения = СокрЛП(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
