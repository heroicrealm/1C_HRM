////////////////////////////////////////////////////////////////////////////////
// Подсистема "КонфигурацииЗарплатаКадрыРасширенный".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.БазоваяФункциональность

// См. ОбщегоНазначенияПереопределяемый.ПриОпределенииОбщихПараметровБазовойФункциональности.
Процедура ПриОпределенииОбщихПараметровБазовойФункциональности(ОбщиеПараметры) Экспорт
	
	КонфигурацииЗарплатаКадрыБазовый.ПриОпределенииОбщихПараметровБазовойФункциональности(ОбщиеПараметры);
	ОбщиеПараметры.ИмяФормыПерсональныхНастроек = "ОбщаяФорма.ПерсональныеНастройки";	

КонецПроцедуры

// Конец СтандартныеПодсистемы.БазоваяФункциональность

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.ОпределитьРазделыСВариантамиОтчетов.
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Грейды");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КлючевыеПоказателиЭффективности") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КлючевыеПоказателиЭффективности");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОхранаТруда");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда.МедицинскиеОсмотры") Тогда
		МодульМедицинскиеОсмотры = ОбщегоНазначения.ОбщийМодуль("МедицинскиеОсмотры");
		МодульМедицинскиеОсмотры.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОценкаПерсонала") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОценкаПерсонала");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.МедицинскоеСтрахование") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("МедицинскоеСтрахование");
		Модуль.ОпределитьРазделыСВариантамиОтчетов(Разделы);
	КонецЕсли;
	
КонецПроцедуры

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Грейды");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КлючевыеПоказателиЭффективности") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КлючевыеПоказателиЭффективности");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОхранаТруда");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда.МедицинскиеОсмотры") Тогда
		МодульМедицинскиеОсмотры = ОбщегоНазначения.ОбщийМодуль("МедицинскиеОсмотры");
		МодульМедицинскиеОсмотры.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОценкаПерсонала") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОценкаПерсонала");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АнализТекучестиПерсонала") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("АнализТекучестиПерсонала");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие.ЭлектронноеОбучениеВХО") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеОбучениеЗарплатаКадрыРасширенный");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЭлектронноеОбучение") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеОбучение");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Чатботы") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Чатботы");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.МедицинскоеСтрахование") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("МедицинскоеСтрахование");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ИспытательныйСрокСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ИспытательныйСрокСотрудников");
		Модуль.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыйУчет.ДистанционнаяРабота") Тогда
		МодульДистанционнаяРабота = ОбщегоНазначения.ОбщийМодуль("ДистанционнаяРабота");
		МодульДистанционнаяРабота.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровоеПланирование") Тогда
		МодульКадровоеПланирование = ОбщегоНазначения.ОбщийМодуль("КадровоеПланирование");
		МодульКадровоеПланирование.НастроитьВариантыОтчетов(Настройки);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// См. ДополнительныеОтчетыИОбработкиПереопределяемый.ОпределитьРазделыСДополнительнымиОбработками.
Процедура ОпределитьРазделыСДополнительнымиОбработками(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Кадры);
	Разделы.Добавить(Метаданные.Подсистемы.Зарплата);
	Разделы.Добавить(Метаданные.Подсистемы.ВыплатыПеречисления);
	Разделы.Добавить(Метаданные.Подсистемы.НалогиИВзносы);
	Разделы.Добавить(Метаданные.Подсистемы.Отчетность);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.МотивацияПерсонала") Тогда
		МодульМотивацияПерсонала = ОбщегоНазначения.ОбщийМодуль("МотивацияПерсонала");
		МодульМотивацияПерсонала.ОпределитьРазделыСДополнительнымиОбработками(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитие = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		МодульОбучениеРазвитие.ОпределитьРазделыСДополнительнымиОбработками(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		МодульОхранаТруда = ОбщегоНазначения.ОбщийМодуль("ОхранаТруда");
		МодульОхранаТруда.ОпределитьРазделыСДополнительнымиОбработками(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		МодульПодборПерсонала.ОпределитьРазделыСДополнительнымиОбработками(Разделы);
	КонецЕсли;
	
КонецПроцедуры

// См. ДополнительныеОтчетыИОбработкиПереопределяемый.ОпределитьРазделыСДополнительнымиОтчетами.
Процедура ОпределитьРазделыСДополнительнымиОтчетами(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Кадры);
	Разделы.Добавить(Метаданные.Подсистемы.Зарплата);
	Разделы.Добавить(Метаданные.Подсистемы.ВыплатыПеречисления);
	Разделы.Добавить(Метаданные.Подсистемы.НалогиИВзносы);
	Разделы.Добавить(Метаданные.Подсистемы.Отчетность);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.МотивацияПерсонала") Тогда
		МодульМотивацияПерсонала = ОбщегоНазначения.ОбщийМодуль("МотивацияПерсонала");
		МодульМотивацияПерсонала.ОпределитьРазделыСДополнительнымиОтчетами(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитие = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		МодульОбучениеРазвитие.ОпределитьРазделыСДополнительнымиОтчетами(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		МодульОхранаТруда = ОбщегоНазначения.ОбщийМодуль("ОхранаТруда");
		МодульОхранаТруда.ОпределитьРазделыСДополнительнымиОтчетами(Разделы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		МодульПодборПерсонала.ОпределитьРазделыСДополнительнымиОтчетами(Разделы);
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияС1СДокументооборотПереопределяемый.ПриОпределенииСокращенногоНаименованияКонфигурации.
Процедура ПриОпределенииСокращенногоНаименованияКонфигурации(СокращенноеНаименование) Экспорт
	
	КраткоеНазваниеПрограммы = КонфигурацииЗарплатаКадры.КраткоеНазваниеПрограммы();
	МассивСтрок = СтрРазделить(Метаданные.Версия, ".");
	СокращенноеНаименование = КраткоеНазваниеПрограммы + " " + МассивСтрок[0] + "." + МассивСтрок[1];
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// ИнтернетПоддержкаПользователей.Новости

// См. ОбработкаНовостейПереопределяемый.ЗначениеПредопределеннойКатегории.
Процедура ЗначениеПредопределеннойКатегории(Категория, Значение) Экспорт
	Если ТипЗнч(Категория) = Тип("ПланВидовХарактеристикСсылка.КатегорииНовостей") Тогда
		КодВРег = ВРег(СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Категория, "Код")));
	ИначеЕсли ТипЗнч(Категория) = Тип("Строка") Тогда
		КодВРег = ВРег(СокрЛП(Категория));
	Иначе
		Возврат;
	КонецЕсли;
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.Новости

// СтандартныеПодсистемы.ОбменДанными

// См. ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата
Процедура ПриПолученииДоступныхВерсийФормата(ВерсииФормата) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиУниверсальныйФормат");
		Модуль.ПриПолученииДоступныхВерсийФормата(ВерсииФормата);
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменДаннымиПереопределяемый.РегистрацияИзмененийНачальнойВыгрузкиДанных
Процедура РегистрацияИзмененийНачальнойВыгрузкиДанных(Знач Получатель, СтандартнаяОбработка, Отбор) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиУниверсальныйФормат");
		Модуль.РегистрацияИзмененияДляНачальнойВыгрузки(Получатель, СтандартнаяОбработка, Отбор);
	КонецЕсли;

КонецПроцедуры

// Конец СтандартныеПодсистемы.ОбменДанными

// ИнтеграцияС1СДокументооборот

// См. ИнтеграцияС1СДокументооборотПереопределяемый.ЗаполнитьРеквизитыИзПотребителя.
Процедура ЗаполнитьРеквизитыИзПотребителя(Прокси, ОбъектXDTO, СсылкаНаПотребитель) Экспорт
	
	Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMCorrespondent")
		И ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаПотребитель, "ИНН, КПП, ЮридическоеФизическоеЛицо");
		
		ОбъектXDTO.inn = Реквизиты.ИНН;
		ОбъектXDTO.kpp = Реквизиты.КПП;
		ОбъектXDTO.legalPrivatePerson = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMLegalPrivatePerson");
		ОбъектXDTO.legalPrivatePerson.name = Строка(Реквизиты.ЮридическоеФизическоеЛицо);
		ОбъектXDTO.legalPrivatePerson.objectId = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		ОбъектXDTO.legalPrivatePerson.objectId.id = ОбщегоНазначения.ИмяЗначенияПеречисления(Реквизиты.ЮридическоеФизическоеЛицо);
		ОбъектXDTO.legalPrivatePerson.objectId.type = "DMLegalPrivatePerson";
		
	ИначеЕсли ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMOrganization")
		И ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка.Организации") Тогда
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
			СсылкаНаПотребитель,
			,
			"ИННЮЛ, КППЮЛ");
		СведенияОбОрганизации.Свойство("ИННЮЛ", ОбъектXDTO.inn);
		СведенияОбОрганизации.Свойство("КППЮЛ", ОбъектXDTO.kpp);
		
	ИначеЕсли ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMBank")
		И ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка.КлассификаторБанков") Тогда
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаПотребитель, "Код");
		
		ОбъектXDTO.BIC = Реквизиты.Код;
		
	ИначеЕсли ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMPrivatePerson")
		И ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаПотребитель, "ФИО, ДатаРождения");
		
		ОбъектXDTO.name = Реквизиты.ФИО;
		ОбъектXDTO.birthDay = Реквизиты.ДатаРождения;
		
	ИначеЕсли ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMBankAccount")
		И ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов") Тогда
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаПотребитель, "Владелец, Банк, НомерСчета");
		
		ОбъектXDTO.number = Реквизиты.НомерСчета;
		
		Если ТипЗнч(Реквизиты.Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
			ОбъектXDTO.owner = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMCorrespondent");
		ИначеЕсли ТипЗнч(Реквизиты.Владелец) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ОбъектXDTO.owner = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMPrivatePerson");
		КонецЕсли;
		ИнтеграцияС1СДокументооборот.ЗаполнитьРеквизитыИзПотребителя(Прокси, ОбъектXDTO.owner, Реквизиты.Владелец);
		
		ОбъектXDTO.bank = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMBank");
		ИнтеграцияС1СДокументооборот.ЗаполнитьРеквизитыИзПотребителя(Прокси, ОбъектXDTO.bank, Реквизиты.Банк);
		
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияС1СДокументооборотПереопределяемый.ПриОпределенииЗначенияРеквизитаПоОбъектуXDTO.
Процедура ПриОпределенииЗначенияРеквизитаПоОбъектуXDTO(Результат, ОбъектXDTO, ТипРеквизита, ИмяРеквизита,
	ТипОбъекта, Объект, ЭтоДополнительноеСвойство, Свойство, ПараметрыВыбора) Экспорт
	
	Если ТипРеквизита = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов")
		И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "number") Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	БанковскиеСчетаКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
		|ГДЕ
		|	БанковскиеСчетаКонтрагентов.НомерСчета = &НомерСчета");
		Запрос.УстановитьПараметр("НомерСчета", ОбъектXDTO.number);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияС1СДокументооборотПереопределяемый.ПриСозданииПоСсылке.
Процедура ПриСозданииПоСсылке(ОбъектИС, ОбъектXDTO, ЗаполняемыйОбъектИС) Экспорт
	
	Если ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.Организации")
			Или ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.Контрагенты") Тогда
		
		ОбъектИС.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		
		Если ОбъектXDTO.objectId.type = "DMOrganization"
				Или ОбъектXDTO.objectId.type = "DMCorrespondent" Тогда
			
			Если ОбъектXDTO.Установлено("legalPrivatePerson") Тогда
				Если ОбъектXDTO.legalPrivatePerson.objectId.id = "ЮридическоеЛицо" Тогда
					ОбъектИС.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
				ИначеЕсли ОбъектXDTO.legalPrivatePerson.objectId.id = "ФизическоеЛицо" Тогда
					ОбъектИС.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
				КонецЕсли;
			КонецЕсли;
			
			ОбъектИС.ИНН = ОбъектXDTO.inn;
			ОбъектИС.КодПоОКПО = ОбъектXDTO.okpo;
			ОбъектИС.НаименованиеПолное = ОбъектXDTO.fullName;
			
			Если ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.Контрагенты") Тогда
				ОбъектИС.КПП = ОбъектXDTO.kpp;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбъектXDTO.objectId.type = "DMPrivatePerson"
		И ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.ФизическиеЛица") Тогда
		ОбъектИС.ФИО = ОбъектXDTO.name;
		ОбъектИС.ДатаРождения = ОбъектXDTO.birthDay;
	КонецЕсли;
	
	Если ОбъектXDTO.objectId.type = "DMBank"
		И ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.КлассификаторБанков") Тогда
		ОбъектИС.Код = ОбъектXDTO.BIC;
	КонецЕсли;
	
	Если ОбъектXDTO.objectId.type = "DMBankAccount"
		И ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.БанковскиеСчетаКонтрагентов") Тогда
		ОбъектИС.НомерСчета = ОбъектXDTO.number;
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияС1СДокументооборотПереопределяемый.ПриОпределенииПодходящегоТипа.
Процедура ПриОпределенииПодходящегоТипа(ТипОбъектаXDTO, ТипыКандидаты, ПодходящийТип) Экспорт
	
	Если ТипОбъектаXDTO = "DMUser"
		И ТипыКандидаты.Найти(Тип("СправочникСсылка.Пользователи")) <> Неопределено Тогда
		
		ПодходящийТип = Тип("СправочникСсылка.Пользователи");
		
	ИначеЕсли ТипОбъектаXDTO = "DMCorrespondent"
		И ТипыКандидаты.Найти(Тип("СправочникСсылка.Контрагенты")) <> Неопределено Тогда
		
		ПодходящийТип = Тип("СправочникСсылка.Контрагенты");
		
	ИначеЕсли ТипОбъектаXDTO = "DMOrganization"
		И ТипыКандидаты.Найти(Тип("СправочникСсылка.Организации")) <> Неопределено Тогда
		
		ПодходящийТип = Тип("СправочникСсылка.Организации");
		
	ИначеЕсли ТипОбъектаXDTO = "DMPrivatePerson"
		И ТипыКандидаты.Найти(Тип("СправочникСсылка.ФизическиеЛица")) <> Неопределено Тогда
		
		ПодходящийТип = Тип("СправочникСсылка.ФизическиеЛица");
		
	ИначеЕсли ТипОбъектаXDTO = "DMBank"
		И ТипыКандидаты.Найти(Тип("СправочникСсылка.КлассификаторБанков")) <> Неопределено Тогда
		
		ПодходящийТип = Тип("СправочникСсылка.КлассификаторБанков");
		
	ИначеЕсли ТипОбъектаXDTO = "DMBankAccount"
		И ТипыКандидаты.Найти(Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов")) <> Неопределено Тогда
		
		ПодходящийТип = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов");
		
	Иначе
		
		ПодходящийТип = ТипыКандидаты[0];
		
	КонецЕсли;
	
КонецПроцедуры

// Конец ИнтеграцияС1СДокументооборот

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИБ

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт

КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.ОрганизацииПрисоединенныеФайлы, Истина);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		МодульОрганизационнаяСтруктура = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		МодульОрганизационнаяСтруктура.ПриЗаполненииСписковСОграничениемДоступа(Списки);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		МодульПодборПерсонала.ЗаполнитьСпискиСОграничениемДоступаВзаимодействий(Списки);
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Список, Ограничение) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		МодульПодборПерсонала.ЗаполнитьОграниченияДоступаВзаимодействий(Список, Ограничение);
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииПоставляемыхПрофилейГруппДоступа.
Процедура ЗаполнитьПоставляемыеПрофилиГруппДоступа(ОписанияПрофилей, ПараметрыОбновления) Экспорт
	
	ОписаниеПрофиля = ОписаниеПрофиляУправлениеДатамиЗапретаИзменения();
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

// Обслуживание подсистемы УправлениеДоступом
// см. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовДоступа.
//
Процедура ПриЗаполненииВидовДоступа(ВидыДоступа) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		МодульОрганизационнаяСтруктура = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		МодульОрганизационнаяСтруктура.ПриЗаполненииВидовДоступа(ВидыДоступа);
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииИспользованияВидаДоступа.
Процедура ПриЗаполненииИспользованияВидаДоступа(ИмяВидаДоступа, Использование) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ПриЗаполненииИспользованияВидаДоступа(ИмяВидаДоступа, Использование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьОписаниеПрофиля(ОписаниеПрофиля) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ДополнитьОписаниеПрофиля(ОписаниеПрофиля);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИдентификацияПриложения

// Подробнее см. ЗарплатаКадрыРасширенный.ИдентификацияПриложенияДляGoogle()
Процедура ПриОпределенииИдентификацииПриложенияДляGoogle(ИдентификаторПриложения) Экспорт
	
	Конфигурация = СведенияОКонфигурации();
	
	Если Конфигурация.Корпоративная Тогда
		Если Конфигурация.Хозрасчетная Тогда
			ИдентификаторПриложения.client_id		= "343079858966-v45dvp0falnq4ko8vfta1cu68d7f35nc.apps.googleusercontent.com";
			ИдентификаторПриложения.client_secret	= "6Ezbx1cJUyYg3UT1uhKgY1RB";
		ИначеЕсли Конфигурация.Бюджетная Тогда
			ИдентификаторПриложения.client_id		= "1095591915921-jf55iolc9n1oibefrcmuum7lomqhnr2o.apps.googleusercontent.com";
			ИдентификаторПриложения.client_secret	= "W8iSq26cm5lic3XrAlSCyNW6";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее см. ЗарплатаКадрыРасширенный.ИдентификацияПриложенияДляHeadHunter()
Процедура ПриОпределенииИдентификацииПриложенияДляHeadHunter(ИдентификаторПриложения) Экспорт
	
	Конфигурация = СведенияОКонфигурации();
	
	Если Конфигурация.Корпоративная Тогда
		Если Конфигурация.Хозрасчетная Тогда
			ИдентификаторПриложения.client_id		= "LP0NMCI0VMIFIP6FTE9J4GB6O2JPD55DKH8S3MFF5QRI2MPIQCBUM1N1F5HG127C";
			ИдентификаторПриложения.client_secret	= "KQMUC6DFB77IH09PHMML8PJD7BVH1QA29SB2TADO50M41N667OTFR7NH38EGPPVQ";
			ИдентификаторПриложения.redirect_uri	= "https://oauth-integration.1c.ru/hrmcorp-hhru/";
		ИначеЕсли Конфигурация.Бюджетная Тогда
			ИдентификаторПриложения.client_id		= "S05RUDFV7CSO1CKSL4DF7SSUCS2OLBQ7VELGG9D4IABJ02TIJV1DIVS2ATMAUM86";
			ИдентификаторПриложения.client_secret	= "RUSJ04JAHQ7J3O732QRFRV9L26OLH1QDQ2RBC82EHTPSUN5BLK2FOGG1ARC4JVE6";
			ИдентификаторПриложения.redirect_uri	= "https://oauth-integration.1c.ru/statehrmcorp-hhru/";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее см. ЗарплатаКадрыРасширенный.ИдентификацияПриложенияДляZarplata()
Процедура ПриОпределенииИдентификацииПриложенияДляZarplata(ИдентификаторПриложения) Экспорт
	
	Конфигурация = СведенияОКонфигурации();
	
	Если Конфигурация.Корпоративная Тогда
		Если Конфигурация.Хозрасчетная Тогда
			ИдентификаторПриложения.client_id		= "30";
			ИдентификаторПриложения.client_secret	= "5Eo1lHDFcFAouWRr3inxvgQB2Uyrgl/DG3vy9apyJxOU2hmfbAMhcCSm";
			ИдентификаторПриложения.redirect_uri	= "https://oauth-integration.1c.ru/hrmcorp-zarplataru/";
		ИначеЕсли Конфигурация.Бюджетная Тогда
			ИдентификаторПриложения.client_id		= "31";
			ИдентификаторПриложения.client_secret	= "bd1qN9iC5HaUtWxVOnvanS646FSR1xd211Y7KRx1QOfolykTWvFaXX7s";
			ИдентификаторПриложения.redirect_uri	= "https://oauth-integration.1c.ru/statehrmcorp-zarplataru/";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее см. ЗарплатаКадрыРасширенный.ИдентификацияПриложенияДляAvito()
Процедура ПриОпределенииИдентификацииПриложенияДляAvito(ИдентификаторПриложения) Экспорт
	
	Конфигурация = СведенияОКонфигурации();
	
	Если Конфигурация.Корпоративная Тогда
		Если Конфигурация.Хозрасчетная Тогда
			ИдентификаторПриложения.client_id		= "duwV0eQqDIN0qB20YfR0";
			ИдентификаторПриложения.client_secret	= "q7Vig6vA9KZFy20DkMCtpQQ76xWsNaM090G3eAlx";
			ИдентификаторПриложения.redirect_uri	= "https://oauth-integration.1c.ru/hrmcorp-avito";
		ИначеЕсли Конфигурация.Бюджетная Тогда
			ИдентификаторПриложения.client_id		= "U9pZep8eLbRSa-_xcvEp";
			ИдентификаторПриложения.client_secret	= "Mev4BEfamO3TwLtaUA88DeS43FoYauGSaEZn6inR";
			ИдентификаторПриложения.redirect_uri	= "https://oauth-integration.1c.ru/statehrmcorp-avito";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БазовыйКонтракт

// Возвращает сведения о конфигурации.
//
// Возвращаемое значение:
//   Структура - Сведения о конфигурации
//       * Хозрасчетная - Булево
//       * Бюджетная - Булево
//       * Базовая - Булево
//       * Корпоративная - Булево
//
Функция СведенияОКонфигурации() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Сведения = Новый Структура("Хозрасчетная, Бюджетная, Базовая, Корпоративная");
	
	ЕстьУчетБюджетныхУчреждений     = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений");
	ЕстьУчетХозрасчетныхОрганизаций = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетХозрасчетныхОрганизаций");
	ЕстьКорпоративнаяПодсистема     = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы");
	ЕстьБазоваяПодсистема           = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыБазоваяВерсия");
	
	Если ЕстьУчетБюджетныхУчреждений И ЕстьУчетХозрасчетныхОрганизаций И ЕстьКорпоративнаяПодсистема И ЕстьБазоваяПодсистема Тогда
		
		МодульЗарплатаКадрыБазоваяВерсия = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыБазоваяВерсия");
		
		Сведения.Хозрасчетная  = ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации");
		Сведения.Бюджетная     = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
		Сведения.Корпоративная = ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы");
		Сведения.Базовая       = МодульЗарплатаКадрыБазоваяВерсия.ЭтоБазоваяВерсияКонфигурации();
		
	Иначе
		
		Сведения.Хозрасчетная  = ЕстьУчетХозрасчетныхОрганизаций;
		Сведения.Бюджетная     = ЕстьУчетБюджетныхУчреждений;
		Сведения.Корпоративная = ЕстьКорпоративнаяПодсистема;
		Сведения.Базовая       = СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации();
		
	КонецЕсли;
	
	Возврат Сведения;
КонецФункции

#КонецОбласти

// Предназначена для определения необходимости использования структуры предприятия для отражения в учете.
//
// Параметры: 
//	Использование - тип булево
//
Процедура ОпределитьИспользованиеСтруктурыПредприятияДляОтраженияВРегламентированномУчете(Использование) Экспорт
	
	ИспользуетсяОбменУправлениеПредприятием2 = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		ИспользуетсяОбменУправлениеПредприятием2 = Модуль.ИспользуетсяОбменУправлениеПредприятием2();
	КонецЕсли;
	
	Если ИспользуетсяОбменУправлениеПредприятием2 Тогда
		Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура РазрешитьОтправкуВБанкОтдельнойВедомости(Использование) Экспорт
	
	ВедомостиВыгружаютсяКакЗаявки = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат") Тогда 
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиУниверсальныйФормат");
		
		УстановитьПривилегированныйРежим(Истина);
		ВедомостиВыгружаютсяКакЗаявки = Модуль.ВедомостиВыгружаютсяКакЗаявки();
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Если ВедомостиВыгружаютсяКакЗаявки Тогда
		Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
	
	// При переходе на КОРП вариант поставки..
	ПриПереходеНаКОРПВариантПоставки(Обработчики);
	
КонецПроцедуры

// Возникает при обработке данных выбора организации для определения действия с управленческой организацией по умолчанию.
//
// Параметры:
//  ПоказыватьУправленческуюОрганизацию	 - Булево, Неопределено	 - определяет отображение управленческой организации
//  	в полях выбора и списках по умолчанию, если не задано явно.
//
Процедура ПриОпределенииПоказыватьУправленческуюОрганизациюПоУмолчанию(ПоказыватьУправленческуюОрганизацию) Экспорт
	ПоказыватьУправленческуюОрганизацию = Ложь;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкиПользователей

// Переопределяет настройки интерфейса, устанавливаемые для новых пользователей.
// Подробнее см. ПользователиПереопределяемый.ПриУстановкеНачальныхНастроек.
//
Процедура ПриУстановкеНачальныхНастроек(НачальныеНастройки) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		// При большем количестве подсистем, не меняем вертикального расположения разделов.
		Возврат;
	КонецЕсли;
	
	// Устанавливаем горизонтальное расположение разделов.
	Если НачальныеНастройки.НастройкиТакси <> Неопределено Тогда
		НачальныеНастройки.НастройкиТакси = Новый НастройкиИнтерфейсаКлиентскогоПриложения;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// Возвращает описание профиля "Управление датами запрета изменения".
//
Функция ОписаниеПрофиляУправлениеДатамиЗапретаИзменения()
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Идентификатор = ИдентификаторПрофиляУправлениеДатамиЗапретаИзменения();
	ОписаниеПрофиля.Наименование  = НСтр("ru = 'Управление датами запрета изменения'");
	
	ЗарплатаКадрыРасширенный.ДобавитьВОписаниеПрофиляРоль(ОписаниеПрофиля, "ДобавлениеИзменениеДатЗапретаИзменения");
	
	Возврат ОписаниеПрофиля;
	
КонецФункции

Функция ИдентификаторПрофиляУправлениеДатамиЗапретаИзменения()
	Возврат "a40f367d-e603-4cfb-a5d6-63acc4cde883";
КонецФункции

#КонецОбласти

Процедура ПриПереходеНаКОРПВариантПоставки(Обработчики)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	// Перечисляются имена обработчиков, которые необходимо выполнить при переходе на КОРП вариант поставки с других программ.
	ИменаОбработчиков = Новый Массив;
	ИменаОбработчиков.Добавить("ОбновлениеИнформационнойБазыЗарплатаКадрыРасширенный.УстановитьИспользованиеЗарплатаКадрыКорпоративнаяПодсистемы");
	
	// Регистрируем обработчики для всех предыдущих программ, с которых поддерживается обновление на КОРП вариант поставки.
	ИменаПредыдущихКонфигураций = Новый Массив;
	ИменаПредыдущихКонфигураций.Добавить("ЗарплатаИУправлениеПерсоналом");
	ИменаПредыдущихКонфигураций.Добавить("ЗарплатаИКадрыГосударственногоУчреждения");
	ИменаПредыдущихКонфигураций.Добавить("БЗКР");
	
	Для Каждого ПредыдущееИмяКонфигурации Из ИменаПредыдущихКонфигураций Цикл
		Для Каждого ИмяОбработчика Из ИменаОбработчиков Цикл
			Обработчик = Обработчики.Добавить();
			Обработчик.ПредыдущееИмяКонфигурации = ПредыдущееИмяКонфигурации;
			Обработчик.Процедура = ИмяОбработчика;
		КонецЦикла;
	КонецЦикла;
	
	// Выбираем обработчики начального заполнения вновь добавленных подсистем.
	ВсеОбработчики = ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления();
	СобратьОбработчикиКОРППодсистем(ВсеОбработчики);
	ВсеОбработчики.Сортировать("ОбщиеДанные Убыв");
	ОбработчикиНачальногоЗаполнения = ВсеОбработчики.НайтиСтроки(Новый Структура("НачальноеЗаполнение", Истина));
	Для Каждого ПредыдущееИмяКонфигурации Из ИменаПредыдущихКонфигураций Цикл
		Для Каждого ОбработчикНачальногоЗаполнения Из ОбработчикиНачальногоЗаполнения Цикл
			Обработчик = Обработчики.Добавить();
			Обработчик.ПредыдущееИмяКонфигурации = ПредыдущееИмяКонфигурации;
			Обработчик.Процедура = ОбработчикНачальногоЗаполнения.Процедура;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура СобратьОбработчикиКОРППодсистем(Обработчики)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		МодульАдаптацияУвольнение.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.АттестацииСотрудников") Тогда
		МодульАттестацииСотрудников = ОбщегоНазначения.ОбщийМодуль("АттестацииСотрудников");
		МодульАттестацииСотрудников.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда
		МодульГрейды = ОбщегоНазначения.ОбщийМодуль("Грейды");
		МодульГрейды.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКадровыйРезерв = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
		МодульКадровыйРезерв.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КлючевыеПоказателиЭффективности") Тогда
		МодульКлючевыеПоказателиЭффективности = ОбщегоНазначения.ОбщийМодуль("КлючевыеПоказателиЭффективности");
		МодульКлючевыеПоказателиЭффективности.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда
		МодульОперацииРасчетаЗарплаты = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
		МодульОперацииРасчетаЗарплаты.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		МодульОхранаТруда = ОбщегоНазначения.ОбщийМодуль("ОхранаТруда");
		МодульОхранаТруда.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда.МедицинскиеОсмотры") Тогда
		МодульМедицинскиеОсмотры = ОбщегоНазначения.ОбщийМодуль("МедицинскиеОсмотры");
		МодульМедицинскиеОсмотры.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		МодульПодборПерсонала.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.СамообслуживаниеСотрудников") Тогда
		МодульСамообслуживаниеСотрудников = ОбщегоНазначения.ОбщийМодуль("СамообслуживаниеСотрудников");
		МодульСамообслуживаниеСотрудников.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЦепочкиДокументов") Тогда
		МодульЦепочкиДокументов = ОбщегоНазначения.ОбщийМодуль("ЦепочкиДокументов");
		МодульЦепочкиДокументов.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЭлектронноеОбучение") Тогда
		МодульОбновлениеИнформационнойБазыБЭО = ОбщегоНазначения.ОбщийМодуль("ОбновлениеИнформационнойБазыБЭО");
		МодульОбновлениеИнформационнойБазыБЭО.ПриДобавленииОбработчиковОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АнализТекучестиПерсонала") Тогда
		МодульАнализТекучестиПерсонала = ОбщегоНазначения.ОбщийМодуль("АнализТекучестиПерсонала");
		МодульАнализТекучестиПерсонала.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.РеестрГосслужащих") Тогда
		МодульРеестрГосслужащих = ОбщегоНазначения.ОбщийМодуль("РеестрГосслужащих");
		МодульРеестрГосслужащих.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыйУчет.Уведомления") Тогда
		МодульКадровыйУчетУведомления = ОбщегоНазначения.ОбщийМодуль("КадровыйУчетУведомления");
		МодульКадровыйУчетУведомления.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АнкетированиеУведомления") Тогда
		МодульАнкетированиеУведомления = ОбщегоНазначения.ОбщийМодуль("АнкетированиеУведомления");
		МодульАнкетированиеУведомления.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КонтрольСамочувствияУведомления") Тогда
		МодульКонтрольСамочувствияУведомления = ОбщегоНазначения.ОбщийМодуль("КонтрольСамочувствияУведомления");
		МодульКонтрольСамочувствияУведомления.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыйУчет.ДистанционнаяРабота") Тогда
		МодульДистанционнаяРабота = ОбщегоНазначения.ОбщийМодуль("ДистанционнаяРабота");
		МодульДистанционнаяРабота.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыйУчет.Отпуска") Тогда
		МодульКадровыйУчетОтпуска = ОбщегоНазначения.ОбщийМодуль("ОтпускаУведомления");
		МодульКадровыйУчетОтпуска.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитие = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		МодульОбучениеРазвитие.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Уведомления") Тогда
		МодульУведомления = ОбщегоНазначения.ОбщийМодуль("УведомленияСервер");
		МодульУведомления.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПодсистемы.БронированиеКомандировок.РегулярнаяОтправкаСотрудников") Тогда
		МодульРегулярнаяОтправкаСотрудников = ОбщегоНазначения.ОбщийМодуль("РегулярнаяОтправкаСотрудников");
		МодульРегулярнаяОтправкаСотрудников.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПоискФизическихЛиц") Тогда
		МодульПоискФизическихЛиц = ОбщегоНазначения.ОбщийМодуль("ПоискФизическихЛиц");
		МодульПоискФизическихЛиц.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		МодульОрганизационнаяСтруктура = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		МодульОрганизационнаяСтруктура.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ИнтеграцияРекрутинговыхСайтов") Тогда
		МодульИнтеграцияРекрутинговыхСайтов = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияРекрутинговыхСайтов");
		МодульИнтеграцияРекрутинговыхСайтов.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровоеПланирование") Тогда
		МодульКадровоеПланирование = ОбщегоНазначения.ОбщийМодуль("КадровоеПланирование");
		МодульКадровоеПланирование.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти