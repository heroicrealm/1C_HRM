#Область СлужебныйПрограммныйИнтерфейс

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения, Ложь);
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных.
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
		|РегистрСведений.НастройкиСистемыНалогообложения.Чтение.Организации
		|РегистрСведений.НастройкиСистемыНалогообложения.Изменение.Организации";
	
КонецПроцедуры

#КонецОбласти

// См. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.15.94";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("e5ba354b-06d3-11eb-8107-4cedfb95099a");
	Обработчик.Процедура       = "НастройкиСистемыНалогообложения.ОбновитьНастройкиПримененияЕНВД";
	Обработчик.Комментарий     = НСтр("ru = 'Обновление настроек применения ЕНВД.'");
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПереименованияОбъектовМетаданных(Итог) Экспорт
	
	// Номер версии соответствует основной конфигурации.
	ОбщегоНазначения.ДобавитьПереименование(Итог,
		"3.1.15.31",
		"Роль.НастройкаУчетнойПолитики",
		"Роль.НастройкаСистемыНалогообложения");
	
КонецПроцедуры

Процедура ОбновитьНастройкиПримененияЕНВД(ПараметрыОбновления = Неопределено) Экспорт

	ДатаОтменыЕНВД = ЗарплатаКадры.ДатаОтменыЕНВД();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаОтменыЕНВД", ДатаОтменыЕНВД);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|ГДЕ
	|	НастройкиСистемыНалогообложения.Период > &ДатаОтменыЕНВД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложенияСрезПоследних.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&ДатаОтменыЕНВД, ) КАК НастройкиСистемыНалогообложенияСрезПоследних
	|ГДЕ
	|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикЕНВД";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса[0].Пустой() И РезультатЗапроса[1].Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиСистемыНалогообложения.СоздатьНаборЗаписей();
	ОбработкаВыполнена = Истина;
	
	Если Не РезультатЗапроса[0].Пустой() Тогда
		
		Выборка =  РезультатЗапроса[0].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.НастройкиСистемыНалогообложения", "Организация", Выборка.Организация) Тогда
				ОбработкаВыполнена = Ложь;
				Продолжить;
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				НаборЗаписей.Очистить();
				НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
				НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НаборЗаписей);
			КонецЦикла;
			
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не РезультатЗапроса[1].Пустой() Тогда
		
		Выборка =  РезультатЗапроса[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.НастройкиСистемыНалогообложения", "Организация", Выборка.Организация) Тогда
				ОбработкаВыполнена = Ложь;
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей.Очистить();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Период.Установить(ДатаОтменыЕНВД);
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Организация 	= Выборка.Организация;
			ЗаписьНабора.Период 		= ДатаОтменыЕНВД;
			ЗаписьНабора.ПлательщикЕНВД = Ложь;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОбработкаВыполнена Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

