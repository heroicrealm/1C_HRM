
#Область СлужебныйПрограммныйИнтерфейс

Процедура НачатьПодключениеОборудованиеПриОткрытииФормы(Форма) Экспорт 
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ШтрихкодыБольничныхЛистовКлиент);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		Форма,
		"СканерШтрихкода");
	
КонецПроцедуры

Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОтключениеОборудованиеПриЗакрытииФормы(Форма) Экспорт
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ШтрихкодыБольничныхЛистовКлиент);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, Форма);
	
КонецПроцедуры

Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если Источник = "ПодключаемоеОборудование" И ИмяСобытия = "ScanData" И ЕстьНеобработанноеСобытие() Тогда
		ОбработатьШтрихкоды(Форма, ДанныеСоСканера(Параметр));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьШтрихкоды(Форма, Данные)
	
	ОбработатьСобытие();
	
	НомерПозиции = Найти(Данные, "!!8!!");
	Если НомерПозиции = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Данные = Сред(Данные, НомерПозиции);
	
	ЗначенияЗаполнения = ШтрихкодыБольничныхЛистовВызовСервера.ЗначенияЗаполненияБольничного(Данные);
	Если ЗначенияЗаполнения <> Неопределено Тогда 
		ОткрытьФорму("Документ.БольничныйЛист.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), Форма);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеСоСканера(Параметр) 
	
	Если Параметр[1] = Неопределено Тогда
		Данные = Параметр[0];    // Достаем штрихкод из основных данных
	Иначе
		Данные = Параметр[1][1]; // Достаем штрихкод из дополнительных данных
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ЕстьНеобработанноеСобытие() 
	
	Возврат (глШтрихкодыБольничныхЛистовСобытиеОбработано = Ложь);
	
КонецФункции

Процедура ОбработатьСобытие() 
	
	глШтрихкодыБольничныхЛистовСобытиеОбработано = Истина;
	
КонецПроцедуры

#КонецОбласти
