#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыПериодическиеРегистры.КонтрольИзмененийПередЗаписью(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыПериодическиеРегистры.КонтрольИзмененийПриЗаписи(ЭтотОбъект);
	ИзмененияВНаборе = ТаблицаИзменившихсяДанныхНабора();
	
	// Компенсации и внутрисменные отсутствия не отражаются в регистре состояний,
	// поэтому вызываем обновление данных карточки сотрудника
	Сотрудники = Новый Массив;
	УникальныеСотрудники = Новый Соответствие;
	Для Каждого СтрокаИзменений Из ИзмененияВНаборе Цикл 
		Если СтрокаИзменений.НовоеЗначениеКомпенсация = Истина 
			Или СтрокаИзменений.НовоеЗначениеОтсутствиеВТечениеЧастиСмены = Истина
			Или СтрокаИзменений.СтароеЗначениеКомпенсация = Истина 
			Или СтрокаИзменений.СтароеЗначениеОтсутствиеВТечениеЧастиСмены = Истина Тогда 
			Если УникальныеСотрудники[СтрокаИзменений.Сотрудник] = Неопределено Тогда 
				Сотрудники.Добавить(СтрокаИзменений.Сотрудник);
				УникальныеСотрудники.Вставить(СтрокаИзменений.Сотрудник, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОстаткиОтпусков.ОбновитьДанныеОтпусковКарточкиСотрудника(Сотрудники);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает признак сформированности таблицы изменений
//
Функция ТаблицаИзменившихсяДанныхНабораСформирована() Экспорт
	
	Возврат ЗарплатаКадрыПериодическиеРегистры.ТаблицаИзменившихсяДанныхНабораСформирована(ЭтотОбъект);
	
КонецФункции

// Возвращает таблицу изменений регистра
//
Функция ТаблицаИзменившихсяДанныхНабора() Экспорт
	
	Возврат ЗарплатаКадрыПериодическиеРегистры.ТаблицаИзменившихсяДанныхНабора(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли