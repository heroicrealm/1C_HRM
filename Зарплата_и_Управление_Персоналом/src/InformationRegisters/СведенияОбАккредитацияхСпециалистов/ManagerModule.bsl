#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеАккредитацийСпециалиста(ФизическоеЛицоСсылка) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицоСсылка", ФизическоеЛицоСсылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОбАккредитацияхСпециалистов.Специальность КАК Специальность,
		|	МАКСИМУМ(СведенияОбАккредитацияхСпециалистов.ДействуетДо) КАК ДействуетДо
		|ИЗ
		|	РегистрСведений.СведенияОбАккредитацияхСпециалистов КАК СведенияОбАккредитацияхСпециалистов
		|ГДЕ
		|	СведенияОбАккредитацияхСпециалистов.ФизическоеЛицо = &ФизическоеЛицоСсылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАккредитацияхСпециалистов.Специальность";
		
	КоллекцияЗаписей = Запрос.Выполнить().Выгрузить();
	
	ПредставлениеАккредитаций = "";
	Для Каждого СтрокаАккредитации Из КоллекцияЗаписей Цикл
		ПредставлениеАккредитаций 	= 
			ПредставлениеАккредитаций
			+ ?(ПустаяСтрока(ПредставлениеАккредитаций), "", ", ") 
			+ ?(ЗначениеЗаполнено(СтрокаАккредитации.Специальность), Строка(СтрокаАккредитации.Специальность), "")
			+ ?(ЗначениеЗаполнено(СтрокаАккредитации.ДействуетДо), НСтр("ru = ' до '")  // АПК:1299 
			+ Формат(СтрокаАккредитации.ДействуетДо, "ДЛФ=ДД"), "");
	КонецЦикла;
	
	Если ПустаяСтрока(ПредставлениеАккредитаций) Тогда
		ПредставлениеАккредитаций = НСтр("ru = 'Не имеет аккредитаций'");
	КонецЕсли; 
	
	Возврат ПредставлениеАккредитаций;
	
КонецФункции

#КонецОбласти

#КонецЕсли