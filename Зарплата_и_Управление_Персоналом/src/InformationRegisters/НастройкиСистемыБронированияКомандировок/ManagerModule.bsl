#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗарегистрироватьИзмененияНастроекСистемыБронирования(СистемаБронирования, ИспользоватьОтборПоПодразделениям = Истина) Экспорт

	Если СистемаБронирования = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиСистемыБронированияКомандировок.СистемаБронирования КАК СистемаБронирования
		|ИЗ
		|	РегистрСведений.НастройкиСистемыБронированияКомандировок КАК НастройкиСистемыБронированияКомандировок
		|ГДЕ
		|	НастройкиСистемыБронированияКомандировок.СистемаБронирования = &СистемаБронирования";
	Запрос.УстановитьПараметр("СистемаБронирования", СистемаБронирования);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		НаборЗаписей = РегистрыСведений.НастройкиСистемыБронированияКомандировок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СистемаБронирования.Установить(СистемаБронирования);
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.СистемаБронирования = СистемаБронирования;
		НоваяЗапись.ИспользоватьОтборПоПодразделениям = ИспользоватьОтборПоПодразделениям;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЕсли;

КонецПроцедуры

Функция ИспользуетсяОтборПоПодразделениям(СистемаБронирования) Экспорт

	Если СистемаБронирования = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запись = РегистрыСведений.НастройкиСистемыБронированияКомандировок.СоздатьМенеджерЗаписи();
	Запись.СистемаБронирования = СистемаБронирования;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		Возврат Запись.ИспользоватьОтборПоПодразделениям;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

#КонецОбласти

#КонецЕсли