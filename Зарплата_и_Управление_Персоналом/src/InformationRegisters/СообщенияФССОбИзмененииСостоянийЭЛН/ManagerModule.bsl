#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ТекущиеДела

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел.
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН;
	
	Если Не СЭДОФСС.ДоступенОбменЧерезСЭДО() Или Не ПравоДоступа("Просмотр", МетаданныеРегистра) Тогда
		Возврат; // Нет прав.
	КонецЕсли;
	
	МодульТекущиеДелаСервер = ОбщегоНазначения.ОбщийМодуль("ТекущиеДелаСервер");
	Разделы = МодульТекущиеДелаСервер.РазделыДляОбъекта(МетаданныеРегистра.ПолноеИмя());
	Если Разделы.Количество() = 0 Тогда
		Возврат; // Некорректное внедрение.
	КонецЕсли;
	
	ПредставлениеКоманды = МетаданныеРегистра.Команды.СообщенияФССОбИзмененииЭЛН.Представление();
	КоличествоОжидающихПолучения = КоличествоОжидающихПолучения();
	
	Для Каждого Раздел Из Разделы Цикл
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = "ПолучениеУведомленийОбЭЛН" + СтрЗаменить(Раздел.ПолноеИмя(), ".", "_");
		Дело.ЕстьДела       = (КоличествоОжидающихПолучения > 0);
		Дело.Важное         = Ложь;
		Дело.Владелец       = Раздел;
		Дело.Представление  = ПредставлениеКоманды;
		Дело.Количество     = КоличествоОжидающихПолучения;
		Дело.Подсказка      = НСтр("ru = 'Можно получить новые сообщения ФСС об изменении состояний ЭЛН.'");
		Дело.ПараметрыФормы = Новый Структура;
		Дело.Форма          = "РегистрСведений.СообщенияФССОбИзмененииСостоянийЭЛН.Форма.ФормаСписка";
		
	КонецЦикла;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ТекущиеДела

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаСообщенийФСС

Процедура ПриЗагрузкеУведомленияОНовомСообщении(Страхователь, ИдентификаторСообщения) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = НачатьЗаписьНабора(ИдентификаторСообщения);
	Если Набор.Количество() > 0 Тогда
		ОтменитьЗаписьНабора(Набор);
		Возврат;
	КонецЕсли;
	
	Запись = Набор.Добавить();
	Запись.Организация = Страхователь;
	Запись.Страхователь = Страхователь;
	Запись.ИдентификаторСообщения = ИдентификаторСообщения;
	Запись.ТребуетОбработки = Истина; // Требуется получение и расшифровка.
	Запись.Последнее = Истина; // Неизвестно, является-ли сообщение последним.
	
	ЗавершитьЗаписьНабора(Набор);
КонецПроцедуры

Процедура ПослеРасшифровкиСообщенияОбИзмененииСостоянияЭЛН(Страхователь, ИдентификаторСообщения, ТекстXML, Результат) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	// Пример:
	//<lnStateChange>
	//	<ln_code>900000000003</ln_code>
	//	<snils>0000060004</snils>
	//	<ln_state>010</ln_state>
	//</lnStateChange>
	//<eln>
	//	<lnCode>900000000003</ln_code>
	//	<snils>0000060004</snils>
	//	<lnState>010</ln_state>
	//</eln>
	//<v01:changeElnNotice
	//		xmlns:v01="http://www.fss.ru/integration/types/eln/ins/notice/v01"
	//		xmlns:v011="http://www.fss.ru/integration/types/eln/ins/v01"
	//		xmlns:v012="http://www.fss.ru/integration/types/eln/v01">
	//	<v011:snils>0000060004</v011:snils>
	//	<v011:lnCode>900000000003</v011:lnCode>
	//	<v011:lnState>010</v011:lnState>
	//</v01:changeElnNotice>
	
	Набор = НачатьЗаписьНабора(ИдентификаторСообщения);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
	Иначе
		Запись = Набор[0];
	КонецЕсли;
	Если Запись.Страхователь <> Страхователь Тогда
		Запись.Страхователь = Страхователь;
		Запись.Организация  = Страхователь;
	КонецЕсли;
	Запись.ИдентификаторСообщения = ИдентификаторСообщения;
	
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	ЭлементDOM = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'snils']/..");
	Если ЭлементDOM = Неопределено Тогда
		СЭДОФСС.ОшибкаОбработки(Результат, ИдентификаторСообщения, НСтр("ru = 'В xml-содержимом сообщения не удалось найти узел с именем ""snils"".'"));
		ЗавершитьЗаписьНабора(Набор);
		Возврат;
	КонецЕсли;
	
	ФрагментXML = СериализацияБЗК.ОбъектDOMВСтрокуXML(ЭлементDOM);
	ОбъектXDTO = СериализацияБЗК.ОбъектXDTOИзСтрокиXML(ФрагментXML);
	ЗначенияРеквизитов = ОбщегоНазначенияБЗК.ЗначенияСвойств(ОбъектXDTO, "snils, ln_code, ln_state, lnCode, lnState");
	
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.snils) Тогда
		СНИЛСВФорматеИБ = УчетПособийСоциальногоСтрахованияКлиентСервер.СНИЛСВФорматеИБ(ЗначенияРеквизитов.snils);
		РезультатПоиска = ФизическиеЛицаЗарплатаКадры.ФизическоеЛицоПоСНИЛСИлиФИО(СНИЛСВФорматеИБ, "", "", "");
		Запись.СНИЛС          = СНИЛСВФорматеИБ;
		Запись.ФизическоеЛицо = РезультатПоиска.ФизическоеЛицо;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ln_code) Тогда
		Запись.НомерЛН = ЗначенияРеквизитов.ln_code;
	КонецЕсли;
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.lnCode) Тогда
		Запись.НомерЛН = ЗначенияРеквизитов.lnCode;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ln_state) Тогда
		ЗаполнитьСостояниеЭЛН(Запись, ЗначенияРеквизитов.ln_state);
	КонецЕсли;
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.lnState) Тогда
		ЗаполнитьСостояниеЭЛН(Запись, ЗначенияРеквизитов.lnState);
	КонецЕсли;
	
	Запись.ДатаНачалаСобытия = ДатаНачалаСобытия(Запись.НомерЛН, Запись.ИдентификаторСообщения);
	
	Если Не ЗначениеЗаполнено(Запись.ФизическоеЛицо) Тогда
		СЭДОФСС.ОшибкаОбработки(Результат, ИдентификаторСообщения, СтрШаблон(НСтр("ru = 'Не удалось найти физическое лицо по СНИЛС ""%1"".'"), СНИЛСВФорматеИБ));
		ЗавершитьЗаписьНабора(Набор);
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	КадровыеДанные = КадровыйУчет.КадровыеДанныеОсновногоСотрудникаФизическогоЛица(
		Страхователь,
		Запись.ФизическоеЛицо,
		"Организация",
		Запись.ДатаНачалаСобытия,
		Ложь,
		ТекстОшибки);
	Если КадровыеДанные = Неопределено Тогда
		СЭДОФСС.ОшибкаОбработки(Результат, ИдентификаторСообщения, ТекстОшибки);
		Запись.Организация = Страхователь;
	Иначе
		Запись.Организация = КадровыеДанные.Организация;
	КонецЕсли;
	
	Запись.Загружено = Истина;
	
	ЗавершитьЗаписьНабора(Набор);
	Результат.Обработано = Истина;
	
	Попытка
		ОбновитьВторичныеДанные(Запись.Организация, Запись.НомерЛН);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Кратко = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		Подробно = Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Ошибка при обновлении вторичных данных: %1: %2'"), Кратко, Подробно);
		СообщенияБЗК.СообщитьОПроблеме(ТекстОшибки);
	КонецПопытки;
КонецПроцедуры

Функция ДатаНачалаСобытия(НомерЛН, ИдентификаторСообщения)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВложенныйЗапрос.ДатаНачалаСобытия) КАК ДатаНачалаСобытия
	|ИЗ
	|	(ВЫБРАТЬ
	|		СообщенияФССОбИзмененииСостоянийЭЛН.ДатаНачалаСобытия КАК ДатаНачалаСобытия
	|	ИЗ
	|		РегистрСведений.СообщенияФССОбИзмененииСостоянийЭЛН КАК СообщенияФССОбИзмененииСостоянийЭЛН
	|	ГДЕ
	|		СообщенияФССОбИзмененииСостоянийЭЛН.НомерЛН = &НомерЛН
	|		И СообщенияФССОбИзмененииСостоянийЭЛН.ИдентификаторСообщения <> &ИдентификаторСообщения
	|		И СообщенияФССОбИзмененииСостоянийЭЛН.ДатаНачалаСобытия <> &ПустаяДата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ВходящиеСообщенияСЭДОФСС.ДатаСоздания < ВходящиеСообщенияСЭДОФСС.ДатаЗагрузки
	|				ТОГДА ВходящиеСообщенияСЭДОФСС.ДатаСоздания
	|			ИНАЧЕ ВходящиеСообщенияСЭДОФСС.ДатаЗагрузки
	|		КОНЕЦ
	|	ИЗ
	|		РегистрСведений.ВходящиеСообщенияСЭДОФСС КАК ВходящиеСообщенияСЭДОФСС
	|	ГДЕ
	|		ВходящиеСообщенияСЭДОФСС.Идентификатор = &ИдентификаторСообщения
	|		И ВходящиеСообщенияСЭДОФСС.ДатаСоздания <> &ПустаяДата) КАК ВложенныйЗапрос";
	Запрос.УстановитьПараметр("НомерЛН", НомерЛН);
	Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДатаНачалаСобытия;
	Иначе
		Возврат ТекущаяДатаСеанса();
	КонецЕсли;
КонецФункции

Процедура ЗаполнитьСостояниеЭЛН(Запись, LN_STATE)
	СостояниеЭЛНВФСС = Перечисления.СостоянияЭЛНВФСС.НайтиПоКодуФСС(LN_STATE);
	Если ЗначениеЗаполнено(СостояниеЭЛНВФСС) Тогда
		Запись.СостояниеЭЛН = СостояниеЭЛНВФСС;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаСообщения111

Процедура ЗагрузитьУведомлениеОНаличииСообщения111(Страхователь, ИдентификаторСообщения) Экспорт
	ПриЗагрузкеУведомленияОНовомСообщении(Страхователь, ИдентификаторСообщения);
КонецПроцедуры

Процедура ЗагрузитьСообщение111(Страхователь, ИдентификаторСообщения, ТекстXML, Результат) Экспорт
	// Пример:
	//<v01:changeElnNotice
	//		xmlns:v01="http://www.fss.ru/integration/types/eln/ins/notice/v01"
	//		xmlns:v011="http://www.fss.ru/integration/types/eln/ins/v01"
	//		xmlns:v012="http://www.fss.ru/integration/types/eln/v01">
	//	<v011:snils>0000060004</v011:snils>
	//	<v011:lnCode>900000000003</v011:lnCode>
	//	<v011:lnState>010</v011:lnState>
	//</v01:changeElnNotice>
	ПослеРасшифровкиСообщенияОбИзмененииСостоянияЭЛН(Страхователь, ИдентификаторСообщения, ТекстXML, Результат);
КонецПроцедуры

#КонецОбласти

#Область ТекущиеДела

Функция КоличествоОжидающихПолучения() Экспорт
	Если Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН) Тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК Количество
	|ИЗ
	|	РегистрСведений.СообщенияФССОбИзмененииСостоянийЭЛН КАК СообщенияФСС
	|ГДЕ
	|	НЕ СообщенияФСС.Загружено";
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
КонецФункции

#КонецОбласти

#Область ОбновлениеВторичныхДанных

Процедура ОбновитьВторичныеДанные(Организация = Неопределено, НомерЛН = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПриоритетовСостоянийЭЛН.Ссылка КАК Ссылка,
	|	ТаблицаПриоритетовСостоянийЭЛН.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыСостоянийЭЛН
	|ИЗ
	|	&ТаблицаПриоритетовСостоянийЭЛН КАК ТаблицаПриоритетовСостоянийЭЛН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СообщенияФСС.Организация КАК Организация,
	|	СообщенияФСС.ИдентификаторСообщения КАК ИдентификаторСообщения,
	|	ЕСТЬNULL(ПриоритетыСостояний.Приоритет, 99) КАК Приоритет,
	|	СообщенияФСС.НомерЛН КАК НомерЛН,
	|	СообщенияФСС.Загружено КАК Загружено,
	|	ВЫБОР
	|		КОГДА НЕ СообщенияФСС.Загружено
	|			ТОГДА ИСТИНА
	|		КОГДА СообщенияФСС.СостояниеЭЛН = ЗНАЧЕНИЕ(Перечисление.СостоянияЭЛНВФСС.Аннулирован)
	|			ТОГДА НЕ СведенияОбЭЛН.Организация ЕСТЬ NULL
	|		КОГДА СообщенияФСС.СостояниеЭЛН = ЗНАЧЕНИЕ(Перечисление.СостоянияЭЛНВФСС.Закрыт)
	|			ТОГДА ЕСТЬNULL(СведенияОбЭЛН.Больничный, ЗНАЧЕНИЕ(Документ.БольничныйЛист.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.БольничныйЛист.ПустаяСсылка)
	|					И ЕСТЬNULL(СведенияОбЭЛН.СостояниеФСС, ЗНАЧЕНИЕ(Перечисление.СостоянияЭЛНВФСС.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СостоянияЭЛНВФСС.Аннулирован)
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетОбработки,
	|	СообщенияФСС.СостояниеЭЛН КАК СостояниеЭЛН,
	|	СообщенияФСС.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТСообщения
	|ИЗ
	|	РегистрСведений.СообщенияФССОбИзмененииСостоянийЭЛН КАК СообщенияФСС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПриоритетыСостоянийЭЛН КАК ПриоритетыСостояний
	|		ПО СообщенияФСС.СостояниеЭЛН = ПриоритетыСостояний.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбЭЛН КАК СведенияОбЭЛН
	|		ПО СообщенияФСС.НомерЛН = СведенияОбЭЛН.НомерЛисткаНетрудоспособности
	|			И СообщенияФСС.Организация = СведенияОбЭЛН.Организация
	|ГДЕ
	|	СообщенияФСС.Организация = &Организация
	|	И СообщенияФСС.НомерЛН = &НомерЛН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СообщенияФСС.Организация КАК Организация,
	|	СообщенияФСС.ИдентификаторСообщения КАК ИдентификаторСообщения,
	|	МАКСИМУМ(СообщенияФСС.ТребуетОбработки) КАК ТребуетОбработки,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА Сообщения2.Приоритет ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			КОГДА Сообщения2.Приоритет < СообщенияФСС.Приоритет
	|					ИЛИ Сообщения2.ИдентификаторСообщения < СообщенияФСС.ИдентификаторСообщения
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК Последнее
	|ИЗ
	|	ВТСообщения КАК СообщенияФСС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСообщения КАК Сообщения2
	|		ПО СообщенияФСС.НомерЛН = Сообщения2.НомерЛН
	|			И СообщенияФСС.Организация = Сообщения2.Организация
	|			И СообщенияФСС.ФизическоеЛицо = Сообщения2.ФизическоеЛицо
	|			И СообщенияФСС.ИдентификаторСообщения <> Сообщения2.ИдентификаторСообщения
	|
	|СГРУППИРОВАТЬ ПО
	|	СообщенияФСС.ИдентификаторСообщения,
	|	СообщенияФСС.Организация";
	Запрос.УстановитьПараметр("ТаблицаПриоритетовСостоянийЭЛН", ТаблицаПриоритетовСостоянийЭЛН());
	
	Если Организация = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СообщенияФСС.Организация = &Организация", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	Если НомерЛН = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И СообщенияФСС.НомерЛН = &НомерЛН", "");
	Иначе
		Запрос.УстановитьПараметр("НомерЛН", НомерЛН);
	КонецЕсли;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Набор = НачатьЗаписьНабора(СтрокаТаблицы.ИдентификаторСообщения);
		
		ЕстьИзменения = Ложь;
		Для Каждого Запись Из Набор Цикл
			Если Не Запись.Загружено Тогда
				ТребуетОбработки = Истина;
			ИначеЕсли Не СтрокаТаблицы.Последнее Тогда
				ТребуетОбработки = Ложь;
			Иначе
				ТребуетОбработки = СтрокаТаблицы.ТребуетОбработки;
			КонецЕсли;
			Если Запись.ТребуетОбработки <> ТребуетОбработки Тогда
				ЕстьИзменения = Истина;
				Запись.ТребуетОбработки = ТребуетОбработки;
			КонецЕсли;
			Если Запись.Последнее <> СтрокаТаблицы.Последнее Тогда
				ЕстьИзменения = Истина;
				Запись.Последнее = СтрокаТаблицы.Последнее;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьИзменения Тогда
			ЗавершитьЗаписьНабора(Набор);
		Иначе
			ОтменитьЗаписьНабора(Набор);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ТаблицаПриоритетовСостоянийЭЛН()
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияЭЛНВФСС, Число"));
	Таблица.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число"));
	
	Приоритеты = Перечисления.СостоянияЭЛНВФСС.Приоритеты();
	Для Каждого КлючИЗначение Из Приоритеты Цикл
		СтрокаТаблицы = Таблица.Добавить();
		СтрокаТаблицы.Ссылка    = КлючИЗначение.Ключ;
		СтрокаТаблицы.Приоритет = КлючИЗначение.Значение;
	КонецЦикла;
	
	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.Ссылка    = Неопределено;
	СтрокаТаблицы.Приоритет = 11;
	
	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.Ссылка    = Перечисления.СостоянияЭЛНВФСС.ПустаяСсылка();
	СтрокаТаблицы.Приоритет = 10;
	
	Возврат Таблица;
КонецФункции

#КонецОбласти

#Область НаборЗаписей

// АПК:326-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// АПК:325-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// Транзакция открывается в методе НачатьЗаписьНабора, закрывается в ЗавершитьЗаписьНабора, отменяется в ОтменитьЗаписьНабора.

// Транзакционный вариант (с управляемой блокировкой) получения набора записей, соответствующего значениям измерений.
//
// Параметры:
//   ИдентификаторСообщения - Строка - Значение отбора по соответствующему измерению.
//
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей.СообщенияФССОбИзмененииСостоянийЭЛН - Если удалось установить блокировку
//       и прочитать набор записей. При необходимости, открывает свою локальную транзакцию. Для закрытия транзакции
//       следует использовать одну из терминирующих процедур: ЗавершитьЗаписьНабора, либо ОтменитьЗаписьНабора.
//   Неопределено - Если не удалось установить блокировку и прочитать набор записей.
//       Вызывать процедуры ЗавершитьЗаписьНабора, ОтменитьЗаписьНабора не требуется,
//       поскольку если перед блокировкой функции потребовалось открыть локальную транзакцию,
//       то после неудачной блокировки локальная транзакция была отменена.
//
Функция НачатьЗаписьНабора(ИдентификаторСообщения)
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если Не ПолныеПраваИлиПривилегированныйРежим
		И Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Недостаточно прав для изменения регистра ""%1"".'"),
			Метаданные.РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН.Представление());
	КонецЕсли;
	ЛокальнаяТранзакция = Не ТранзакцияАктивна();
	Если ЛокальнаяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Если Не ПолныеПраваИлиПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СообщенияФССОбИзмененииСостоянийЭЛН");
		ЭлементБлокировки.УстановитьЗначение("ИдентификаторСообщения", ИдентификаторСообщения);
		Блокировка.Заблокировать();
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЛокальнаяТранзакция", ЛокальнаяТранзакция);
	Исключение
		Если ЛокальнаяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить уведомление ФСС об изменении состояний ЭЛН %1 по причине: %2'"),
			ИдентификаторСообщения,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН,
			ИдентификаторСообщения,
			ТекстСообщения);
		НаборЗаписей = Неопределено;
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции

// Записывает набор и фиксирует локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.СообщенияФССОбИзмененииСостоянийЭЛН
//
Процедура ЗавершитьЗаписьНабора(НаборЗаписей)
	НаборЗаписей.Записать(Истина);
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Отменяет запись набора и отменяет локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.СообщенияФССОбИзмененииСостоянийЭЛН
//
Процедура ОтменитьЗаписьНабора(НаборЗаписей)
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// АПК:326-вкл.
// АПК:325-вкл.

#КонецОбласти

#КонецОбласти

#КонецЕсли