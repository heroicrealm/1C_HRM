#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Запись.Последнее
				И ЗначениеЗаполнено(Запись.НомерЛН)
				И ЗначениеЗаполнено(Запись.СостояниеЭЛН)
				И ЗначениеЗаполнено(Запись.Организация)
				И ЗначениеЗаполнено(Запись.ФизическоеЛицо)
				И РегистрыСведений.СведенияОбЭЛН.ЭтоЭЛН(Запись.НомерЛН) Тогда
				РегистрыСведений.СведенияОбЭЛН.ЗаписатьПервичныеСведения(
					Запись.НомерЛН,
					Неопределено,
					Запись.СостояниеЭЛН,
					Запись.Организация,
					Запись.ФизическоеЛицо);
			КонецЕсли;
		КонецЦикла;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Кратко = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		Подробно = Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Ошибка при обновлении вторичных данных: %1: %2'"), Кратко, Подробно);
		СообщенияБЗК.СообщитьОПроблеме(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
