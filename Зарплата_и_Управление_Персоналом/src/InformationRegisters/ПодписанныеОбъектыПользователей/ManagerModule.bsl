#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьЗаписиПоОбъектам(ПодписанныеОбъекты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодписанныеОбъекты", ПодписанныеОбъекты);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ЭлектронныеПодписи.ПодписанныйОбъект, ПодписанныеОбъектыПользователей.ПодписанныйОбъект) КАК ПодписанныйОбъект,
		|	ЕСТЬNULL(ЭлектронныеПодписи.УстановившийПодпись, ПодписанныеОбъектыПользователей.УстановившийПодпись) КАК УстановившийПодпись,
		|	ВЫБОР
		|		КОГДА ЭлектронныеПодписи.ПодписанныйОбъект ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Удалить
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеОбъектыПользователей КАК ПодписанныеОбъектыПользователей
		|		ПО ЭлектронныеПодписи.ПодписанныйОбъект = ПодписанныеОбъектыПользователей.ПодписанныйОбъект
		|			И ЭлектронныеПодписи.УстановившийПодпись = ПодписанныеОбъектыПользователей.УстановившийПодпись
		|ГДЕ
		|	ЕСТЬNULL(ЭлектронныеПодписи.ПодписанныйОбъект, ПодписанныеОбъектыПользователей.ПодписанныйОбъект) В (&ПодписанныеОбъекты)
		|	И ВЫБОР
		|			КОГДА ЭлектронныеПодписи.УстановившийПодпись ЕСТЬ NULL
		|				ТОГДА ИСТИНА
		|			КОГДА ЭлектронныеПодписи.УстановившийПодпись = ЗНАЧЕНИЕ(справочник.Пользователи.ПустаяСсылка)
		|				ТОГДА ЛОЖЬ
		|			КОГДА ПодписанныеОбъектыПользователей.УстановившийПодпись ЕСТЬ NULL
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодписанныйОбъект";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПодписанныйОбъект") Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПодписанныеОбъектыПользователей");
			ЭлементБлокировки.УстановитьЗначение("ПодписанныйОбъект", Выборка.ПодписанныйОбъект);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ПодписанныеОбъектыПользователей.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(Выборка.ПодписанныйОбъект);
			НаборЗаписей.Прочитать();
			
			ТаблицаНабора = НаборЗаписей.Выгрузить();
			Пока Выборка.Следующий() Цикл
				
				Если Выборка.Удалить Тогда
					
					ПараметрыОтбора = Новый Структура("ПодписанныйОбъект,УстановившийПодпись");
					ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Выборка);
					
					НайденныеЗаписи = ТаблицаНабора.НайтиСтроки(ПараметрыОтбора);
					Для Каждого Запись Из НайденныеЗаписи Цикл
						ТаблицаНабора.Удалить(Запись);
					КонецЦикла;
					
				Иначе
					
					Запись = ТаблицаНабора.Добавить();
					Запись.ПодписанныйОбъект = Выборка.ПодписанныйОбъект;
					Запись.УстановившийПодпись = Выборка.УстановившийПодпись;
					
				КонецЕсли;
				
			КонецЦикла;
			
			НаборЗаписей.Загрузить(ТаблицаНабора);
			НаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(
				КадровыйЭДО.ИмяСобытияЖурналаРегистрации(
					НСтр("ru = 'Обновление информационной базы.Ошибка блокировки'", ОбщегоНазначения.КодОсновногоЯзыка())),
				УровеньЖурналаРегистрации.Предупреждение,
				,
				Выборка.ПодписанныйОбъект,
				"РегистрСведений.ПодписанныеОбъектыПользователей");
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодписанныеОбъекты(ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбработанных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбработанных") Тогда
			МассивОбработанных = ПараметрыОбновления.МассивОбработанных;
		Иначе
			МассивОбработанных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбработанных", МассивОбработанных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбработанных", МассивОбработанных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ЭлектронныеПодписи.ПодписанныйОбъект КАК ПодписанныйОбъект
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеОбъектыПользователей КАК ПодписанныеОбъектыПользователей
		|		ПО ЭлектронныеПодписи.ПодписанныйОбъект = ПодписанныеОбъектыПользователей.ПодписанныйОбъект
		|ГДЕ
		|	НЕ ЭлектронныеПодписи.ПодписанныйОбъект В (&МассивОбработанных)
		|	И ПодписанныеОбъектыПользователей.ПодписанныйОбъект ЕСТЬ NULL
		|	И ЭлектронныеПодписи.УстановившийПодпись <> ЗНАЧЕНИЕ(справочник.Пользователи.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ОбъектыДляОбновления = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ПодписанныйОбъект");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОбработанных, ОбъектыДляОбновления);
	ОбновитьЗаписиПоОбъектам(ОбъектыДляОбновления);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
