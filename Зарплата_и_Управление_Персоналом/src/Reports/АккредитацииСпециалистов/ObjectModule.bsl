#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КлючВарианта = НастройкиОтчета.ДополнительныеСвойства.КлючВарианта;
	
	ДополнительноеУсловие = "";
	Запрос = СхемаКомпоновкиДанных.НаборыДанных[0].Запрос;
	Если КлючВарианта="СотрудникиБезАккредитации" Тогда 
		ДополнительноеУсловие = " И (ВТАктуальныеАккредитацииСпециалистов.ДействуетДо <= &КонецПериода ИЛИ ВТАктуальныеАккредитацииСпециалистов.ДействуетДо ЕСТЬ NULL)";
	ИначеЕсли КлючВарианта="ДействующиеАккредитации" Тогда
		ДополнительноеУсловие = " И ВТАктуальныеАккредитацииСпециалистов.ДействуетДо > &КонецПериода И ВТАктуальныеАккредитацииСпециалистов.ДатаПротокола <= &НачалоПериода";
	ИначеЕсли КлючВарианта="ПросроченныеАккредитации" Тогда
		ДополнительноеУсловие = " И ВТАктуальныеАккредитацииСпециалистов.ДействуетДо <= &КонецПериода И ВТАктуальныеАккредитацииСпециалистов.ДействуетДо >= &НачалоПериода";
	ИначеЕсли КлючВарианта="АккредитацииЗаПериод" Тогда
		ДополнительноеУсловие = " И ВТАктуальныеАккредитацииСпециалистов.ДатаПротокола <= &КонецПериода И ВТАктуальныеАккредитацииСпециалистов.ДатаПротокола >= &НачалоПериода";
	КонецЕсли;
	
	Если ДополнительноеУсловие<>"" Тогда 
		СхемаКомпоновкиДанных.НаборыДанных[0].Запрос = Запрос + ДополнительноеУсловие;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);

КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		ИнициализироватьОтчет();
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		КлючСхемы = КлючВарианта;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли