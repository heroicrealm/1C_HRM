&НаКлиенте
// Вызывается после загрузки из файла
Перем ОписаниеОповещенияОЗавершенииЗагрузки Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Данные = Неопределено;
	Параметры.Свойство("Данные", Данные);
	РазделительНомераСтроки = "___";
	ОТЧ = Новый ОписаниеТипов("Число");
	
	Объект.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаКИК;
	УведомлениеОСпецрежимахНалогообложения.НачальныеОперацииПриСозданииНаСервере(ЭтотОбъект);
	ЭтотОбъект.ИмяМакетаАрхиваТабличныхДокументов = "ЭкранныеФормы";
	УведомлениеОСпецрежимахНалогообложения.СформироватьСпискиВыбора(ЭтотОбъект, "СпискиВыбора2021_1");
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		СформироватьДеревоСтраниц();
		УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
		УведомлениеОСпецрежимахНалогообложения.ЗагрузитьДанныеПростогоУведомления(ЭтотОбъект, Данные, ПредставлениеУведомления)
	ИначеЕсли Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Организация = Параметры.Ключ.Организация;
		ЗагрузитьДанные(Параметры.Ключ);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Организация = Параметры.ЗначениеКопирования.Организация;
		ЗагрузитьДанные(Параметры.ЗначениеКопирования);
	ИначеЕсли Параметры.Свойство("ПредставлениеXML") Тогда 
		Параметры.Свойство("РегистрацияВНалоговомОргане", Объект.РегистрацияВИФНС);
		Параметры.Свойство("Организация", Объект.Организация);
		ЗагрузитьИзXMLНаСервере(Новый Структура("Организация, РегистрацияВНалоговомОргане, ПредставлениеXML", 
								Объект.Организация, Объект.РегистрацияВИФНС, Параметры.ПредставлениеXML));
	Иначе
		Параметры.Свойство("Организация", Объект.Организация);
		Объект.РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Объект.Организация);
		СформироватьДеревоСтраниц();
		УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
		ЗаполнитьНачальныеДанные();
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложения.ЗаполнитьТаблицуФорматов(ЭтотОбъект, "Форматы2019_1");
	ИдДляСвор = УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект);
	СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(ИдДляСвор);
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект);
	Заголовок = УведомлениеОСпецрежимахНалогообложения.ДополнитьЗаголовокУведомления(Заголовок, Объект.Организация);
	
	ЭтоЮЛ = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация);
	ЦФБ = Новый Цвет(255,255,255);
	ЭтаФормаИмя = ЭтаФорма.ИмяФормы;
	Если СтрЧислоВхождений(ЭтаФормаИмя, "ВнешнийОтчет.") > 0 Тогда
		ЭтаФормаИмя = СтрЗаменить(ЭтаФормаИмя, "ВнешнийОтчет.", "");
	ИначеЕсли СтрЧислоВхождений(ЭтаФормаИмя, "Отчет.") > 0 Тогда
		ЭтаФормаИмя = СтрЗаменить(ЭтаФормаИмя, "Отчет.", "");
	КонецЕсли;
	ИДОтчета = Лев(ЭтаФормаИмя, СтрНайти(ЭтаФормаИмя, ".Форма.") - 1);
	ИДРедакцииОтчета = Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") + 7);
	ПараметрыОтчета = Новый Структура("Организация, ДатаПодписи, РегистрацияВИФНС",
		Объект.Организация, Объект.ДатаПодписи, Объект.РегистрацияВИФНС);
	ПоказателиОтчета = РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОПоказателяхОтчета(
		ИДОтчета, ИДРедакцииОтчета, ПараметрыОтчета);
		
	Если ТипЗнч(ПоказателиОтчета) = Тип("Структура")
		И ПоказателиОтчета.Количество() > 0 Тогда 
			
		Для Каждого КЗ Из ПоказателиОтчета Цикл 
			ЗаполняемыеРеквизиты.Добавить(КЗ.Ключ);
		КонецЦикла;
	Иначе
		Элементы.ФормаЗаполнить.Видимость = Ложь;
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложения.СпрятатьКнопкиВыгрузкиОтправкиУНеактуальныхФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	РучнойВвод = Ложь;
	Элементы.ФормаРучнойВвод.Пометка = РучнойВвод;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСПодчиненными(Команда)
	СПодчиненными = Не СПодчиненными;
	Элементы.ФормаКопироватьСПодчиненными.Пометка = СПодчиненными;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Очистить(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОчиститьУведомление(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОчисткаОтчета() Экспорт
	Объект.РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Объект.Организация);
	СформироватьДеревоСтраниц();
	УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
	ЗаполнитьНачальныеДанные();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачальныеДанные() Экспорт
	ДанныеУведомленияТитульный = ДанныеУведомления["Титульная"];
	ДанныеУведомленияТитульный.Вставить("КодНО", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РегистрацияВИФНС, "Код"));
	Объект.ДатаПодписи = ТекущаяДатаСеанса();
	ДанныеУведомленияТитульный.Вставить("ДАТА_ПОДПИСИ", Объект.ДатаПодписи);
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда
		СтрокаСведений = "ИННЮЛ,НаимЮЛПол,КППЮЛ,ТелОрганизации";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, СтрокаСведений);
		ДанныеУведомленияТитульный.Вставить("ИНН", СведенияОбОрганизации.ИННЮЛ);
		ДанныеУведомленияТитульный.Вставить("Наименование", СведенияОбОрганизации.НаимЮЛПол);
		ДанныеУведомленияТитульный.Вставить("КПП", СведенияОбОрганизации.КППЮЛ);
		ДанныеУведомленияТитульный.Вставить("Тлф", СведенияОбОрганизации.ТелОрганизации);
	Иначе
		СтрокаСведений = "ИННФЛ,ФИО,ТелДом";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, СтрокаСведений);
		ДанныеУведомленияТитульный.Вставить("ИНН", СведенияОбОрганизации.ИННФЛ);
		ДанныеУведомленияТитульный.Вставить("Наименование", СведенияОбОрганизации.ФИО);
		ДанныеУведомленияТитульный.Вставить("Тлф", СведенияОбОрганизации.ТелДом);
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.РегистрацияВИФНС, "Код,Представитель,КПП,ДокументПредставителя");
	ДанныеУведомленияТитульный.Вставить("КодНО", Реквизиты.Код);
	ДанныеУведомленияТитульный.Вставить("КПП", Реквизиты.КПП);
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
			УстановитьПредставителяПоФизЛицу(Реквизиты.Представитель);
			ДанныеУведомленияТитульный.Вставить("ПРИЗНАК_НП_ПОДВАЛ", "4");
			ДанныеУведомленияТитульный.Вставить("НаимДок", Реквизиты.ДокументПредставителя);
		Иначе
			УстановитьПредставителяПоОрганизации();
			ДанныеУведомленияТитульный.Вставить("ПРИЗНАК_НП_ПОДВАЛ", "3");
			ДанныеУведомленияТитульный.Вставить("НаимДок", "");
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
			УстановитьПредставителяПоФизЛицу(Реквизиты.Представитель);
			ДанныеУведомленияТитульный.Вставить("ПРИЗНАК_НП_ПОДВАЛ", "2");
			ДанныеУведомленияТитульный.Вставить("НаимДок", Реквизиты.ДокументПредставителя);
		Иначе
			УстановитьПредставителяПоОрганизации();
			ДанныеУведомленияТитульный.Вставить("ПРИЗНАК_НП_ПОДВАЛ", "1");
			ДанныеУведомленияТитульный.Вставить("НаимДок", "");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоСтраниц() Экспорт
	ДеревоСтраниц.ПолучитьЭлементы().Очистить();
	КорневойУровень = ДеревоСтраниц.ПолучитьЭлементы();
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Титульная страница";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "Титульная_2021";
	Стр001.Многостраничность = Ложь;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "Титульная";
	Стр001.МакетыПФ = "Печать_Форма2021_1_Титульная";
	
	Если Не РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		Стр001 = КорневойУровень.Добавить();
		Стр001.Наименование = "Сведения о физлице";
		Стр001.ИндексКартинки = 1;
		Стр001.ИмяМакета = "ЛистСведФЛ_2019";
		Стр001.Многостраничность = Ложь;
		Стр001.Многострочность = Ложь;
		Стр001.УИД = Новый УникальныйИдентификатор;
		Стр001.ИДНаименования = "ЛистСведФЛ";
		Стр001.МакетыПФ = "Печать_Форма2021_1_ЛистСведФЛ";
	КонецЕсли;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Сведения об иностранных"+символы.ПС+"организациях (Лист А)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистА_2019";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистА";
	Стр001.МакетыПФ = "Печать_Форма2021_1_ЛистА;Печать_Форма2021_1_ЛистА1";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Сведения об иностранных"+символы.ПС+"структурах (Лист Б)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистБ_2019";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистБ";
	Стр001.МакетыПФ = "Печать_Форма2021_1_ЛистБ;Печать_Форма2021_1_ЛистБ1";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Сведения о финансовой"+символы.ПС+"отчетности (Лист В)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистВ_2019";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистВ";
	Стр001.МакетыПФ = "Печать_Форма2021_1_ЛистВ";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Раскрытие порядка"+символы.ПС+"участия (Лист Г)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Истина;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистГ_2019";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Истина;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистГ";
	Стр001.МногострочныеЧасти.Добавить("МнгСтр");
	Стр001.МакетыПФ = "Печать_Форма2021_1_ЛистГ";
	
	СтрГ1 = Стр001.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Сведения о российской"+символы.ПС+"организации (Лист Г1)";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	
	СтрГ1 = СтрГ1.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Стр. 1";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.ИмяМакета = "ЛистГ1_2019";
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	СтрГ1.УИД = Новый УникальныйИдентификатор;
	СтрГ1.ИДНаименования = "ЛистГ1";
	СтрГ1.МакетыПФ = "Печать_Форма2021_1_ЛистГ1";
	
	СтрГ1 = Стр001.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Сведения об иностранной"+символы.ПС+"организации (Лист Г2)";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	
	СтрГ1 = СтрГ1.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Стр. 1";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.ИмяМакета = "ЛистГ2_2021";
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	СтрГ1.УИД = Новый УникальныйИдентификатор;
	СтрГ1.ИДНаименования = "ЛистГ2";
	СтрГ1.МакетыПФ = "Печать_Форма2021_1_ЛистГ2";
КонецПроцедуры

&НаКлиенте
Функция ПолучитьМногострочныеЧасти(ТекущиеДанные)
	Если ТекущиеДанные.МногострочныеЧасти.Количество() > 0 Тогда 
		Возврат ТекущиеДанные.МногострочныеЧасти;
	ИначеЕсли ТекущиеДанные.ПолучитьЭлементы().Количество() > 0 Тогда 
		Возврат ТекущиеДанные.ПолучитьЭлементы()[0].МногострочныеЧасти;
	Иначе
		Возврат ТекущиеДанные.МногострочныеЧасти;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура СобратьДанныеМногострочныхЧастейТекущейСтраницы(МногострочныеЧасти, ПредУИД)
	Для Каждого МнгЧ Из ТекущиеМногострочныеЧасти Цикл 
		СЗ = ДанныеДопСтрокСтраницы[МнгЧ.Значение];
		Для Инд = 1 По СЗ.Количество() Цикл 
			ИндСтр = РазделительНомераСтроки + Формат(Инд, "ЧГ=0");
			ДанныеСтроки = СЗ[Инд-1].Значение;
			Для Каждого КЗ Из ДанныеСтроки Цикл 
				ДанныеСтроки[КЗ.Ключ] = ПредставлениеУведомления.Области[КЗ.Ключ + ИндСтр].Значение;
			КонецЦикла;
		КонецЦикла;
		
		ВсеДопСтроки = ПолучитьИзВременногоХранилища(ДанныеДопСтрок[МнгЧ.Значение]);
		СтрокиТекущейСтраницы = ВсеДопСтроки.НайтиСтроки(Новый Структура("УИД", ПредУИД));
		Для Инд = 0 По СтрокиТекущейСтраницы.Количество() - СЗ.Количество() - 1 Цикл 
			ВсеДопСтроки.Удалить(СтрокиТекущейСтраницы[Инд]);
		КонецЦикла;
		
		Для Инд = СтрокиТекущейСтраницы.Количество() + 1 По СЗ.Количество() Цикл 
			НовСтр = ВсеДопСтроки.Добавить();
			НовСтр.УИД = ПредУИД;
		КонецЦикла;
		
		СтрокиТекущейСтраницы = ВсеДопСтроки.НайтиСтроки(Новый Структура("УИД", ПредУИД));
		Для Инд = 0 По СтрокиТекущейСтраницы.ВГраница() Цикл 
			ЗаполнитьЗначенияСвойств(СтрокиТекущейСтраницы[Инд], СЗ[Инд].Значение);
		КонецЦикла;
		
		ДанныеДопСтрок[МнгЧ.Значение] = ПоместитьВоВременноеХранилище(ВсеДопСтроки, ДанныеДопСтрок[МнгЧ.Значение]);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтроки(Элемент)
	Если УИДТекущаяСтраница <> Элемент.ТекущиеДанные.УИД Тогда 
		ПредУИД = УИДТекущаяСтраница;
		
		УИДТекущаяСтраница = Элемент.ТекущиеДанные.УИД;
		ТекущееИДНаименования = Элемент.ТекущиеДанные.ИДНаименования;
		Если Не ЗначениеЗаполнено(ТекущееИДНаименования) Тогда 
			ТекущееИДНаименования = Элемент.ТекущиеДанные.ПолучитьЭлементы()[0].ИДНаименования;
			УИДТекущаяСтраница = Элемент.ТекущиеДанные.ПолучитьЭлементы()[0].УИД;
		КонецЕсли;
		
		Если Элемент.ТекущиеДанные.Многостраничность Тогда 
			ИмяМакета = УведомлениеОСпецрежимахНалогообложенияКлиент.ПолучитьИмяВыводимогоМакета(Элемент.ТекущиеДанные);
			ПоказатьТекущуюМногостраничнуюСтраницу(ИмяМакета, ПолучитьМногострочныеЧасти(Элемент.ТекущиеДанные), ПредУИД);
		Иначе 
			ПоказатьТекущуюСтраницу(Элемент.ТекущиеДанные.ИмяМакета, Элемент.ТекущиеДанные.МногострочныеЧасти, ПредУИД);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПоказатьМногострочныеЧасти(Макет, МногострочныеЧасти)
	Если ТипЗнч(МногострочныеЧасти) = Тип("СписокЗначений") 
		И МногострочныеЧасти.Количество() > 0 Тогда 
		
		Для Каждого МнгСтр Из МногострочныеЧасти Цикл 
			ТЗ = ПолучитьИзВременногоХранилища(ДанныеДопСтрок[МнгСтр.Значение]);
			ИсхСтр = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеДопСтрокСтраницы[МнгСтр.Значение][0].Значение);
			Для Каждого КЗ Из ИсхСтр Цикл 
				ИсхСтр[Кз.Ключ] = Неопределено;
			КонецЦикла;
			
			Строки = ТЗ.НайтиСтроки(Новый Структура("УИД", УИДТекущаяСтраница));
			ДанныеДопСтрокСтраницы[МнгСтр.Значение].Очистить();
			Инд = 0;
			Пока ДанныеДопСтрокСтраницы[МнгСтр.Значение].Количество() < Строки.Количество() Цикл 
				ТекСтр = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ИсхСтр);
				ЗаполнитьЗначенияСвойств(ТекСтр, Строки[Инд]);
				ДанныеДопСтрокСтраницы[МнгСтр.Значение].Добавить(ТекСтр);
				Инд = Инд + 1;
			КонецЦикла;
			
			Если ДанныеДопСтрокСтраницы[МнгСтр.Значение].Количество() = 0 Тогда 
				ДанныеДопСтрокСтраницы[МнгСтр.Значение].Добавить(ИсхСтр);
			КонецЕсли;
			
			ПредставлениеУведомления.Вывести(Макет.ПолучитьОбласть("Header_" + МнгСтр.Значение));
			Инд = 0;
			ВыводитьЗначокУдаления = ДанныеДопСтрокСтраницы[МнгСтр.Значение].Количество() > 1;
			Для Каждого Стр Из ДанныеДопСтрокСтраницы[МнгСтр.Значение] Цикл
				Инд = Инд + 1;
				ИндСтр = РазделительНомераСтроки + Формат(Инд, "ЧГ=");
				Обл = Макет.ПолучитьОбласть("Str_" + МнгСтр.Значение);
				УстановитьФорматыВМногострочныхПолях(Обл);
				Для Каждого ОблПодч Из Обл.Области Цикл 
					Если ОблПодч.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
						Если ОблПодч.СодержитЗначение Тогда 
							Стр.Значение.Свойство(ОблПодч.Имя, ОблПодч.Значение);
						КонецЕсли;
						ОблПодч.Имя = ОблПодч.Имя + ИндСтр;
					КонецЕсли;
					
					Если ВыводитьЗначокУдаления И ОблПодч.Имя = "Del_" + МнгСтр.Значение + ИндСтр Тогда 
						ОблПодч.Текст = "х";
						ОблПодч.Гиперссылка = Истина;
					КонецЕсли;
				КонецЦикла;
				ПредставлениеУведомления.Вывести(Обл);
			КонецЦикла;
			ПредставлениеУведомления.Области["Str_" + МнгСтр.Значение].Имя = "";
			
			ОблДобавленияСтроки = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьМакет("ОбщиеЭлементы").ПолучитьОбласть("AddStrArea");
			ОблДобавленияСтроки.Области["AddStrLabel"].Имя = "AddStrLabel_" + МнгСтр.Значение;
			ОблДобавленияСтроки.Области["AddStr"].Имя = "AddStr_" + МнгСтр.Значение;
			ПредставлениеУведомления.Вывести(ОблДобавленияСтроки);
			ПредставлениеУведомления.Вывести(Макет.ПолучитьОбласть("Footer_" + МнгСтр.Значение));
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматыВМногострочныхПолях(ТД) Экспорт
	Для Каждого Обл Из ТД.Области Цикл 
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И Обл.СодержитЗначение Тогда 
			ОблИмя = Лев(Обл.Имя, СтрНайти(Обл.Имя, РазделительНомераСтроки) - 1);
			ОблИмя = ?(ЗначениеЗаполнено(ОблИмя), ОблИмя, Обл.Имя);
			ПоляСФорматами = ЧисловыеФорматы.НайтиСтроки(Новый Структура("ИДСтраницы, Поле", ТекущееИДНаименования, ОблИмя));
			Если ПоляСФорматами.Количество() > 0 Тогда 
				Знак = ?(ПоляСФорматами[0].Неотрицательный, ДопустимыйЗнак.Неотрицательный, ДопустимыйЗнак.Любой);
				ОТ = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(ПоляСФорматами[0].Всего, ПоляСФорматами[0].ПослеЗапятой, Знак));
				Обл.ТипЗначения = ОТ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПоказатьТекущуюСтраницу(ИмяМакета, МногострочныеЧасти, ПредУИД)
	Если Не УдалениеСтраницы И ТекущиеМногострочныеЧасти.Количество() > 0 Тогда 
		СобратьДанныеМногострочныхЧастейТекущейСтраницы(МногострочныеЧасти, ПредУИД);
	КонецЕсли;
	
	ТекущиеМногострочныеЧасти = ОбщегоНазначенияКлиентСервер.СкопироватьСписокЗначений(МногострочныеЧасти);
	ТекущийМакет = ИмяМакета;
	Макет = УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, ИмяМакета);
	УведомлениеОСпецрежимахНалогообложения.ПоказатьТекущуюСтраницу(ЭтотОбъект, ИмяМакета, ПредУИД);
	ПоказатьМногострочныеЧасти(Макет, МногострочныеЧасти);
	
	Если ТекущееИДНаименования = "Титульная" И Не ЭтоЮЛ Тогда 
		ОблНаименование = ПредставлениеУведомления.Области.Найти("Наименование");
		ОблНаименование.Защита = Истина;
		ОблНаименование.ЦветФона = ЦФБ
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПоказатьТекущуюМногостраничнуюСтраницу(ИмяМакета, МногострочныеЧасти, ПредУИД)
	Если Не УдалениеСтраницы И ТекущиеМногострочныеЧасти.Количество() > 0 Тогда 
		СобратьДанныеМногострочныхЧастейТекущейСтраницы(МногострочныеЧасти, ПредУИД);
	КонецЕсли;
	
	ТекущиеМногострочныеЧасти = ОбщегоНазначенияКлиентСервер.СкопироватьСписокЗначений(МногострочныеЧасти);
	
	ПредставлениеУведомления.Очистить();
	ТекущийМакет = ИмяМакета;
	Макет = УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, ИмяМакета);
	ПредставлениеУведомления.Вывести(Макет.ПолучитьОбласть("УправлениеСтраницами"));
	ПредставлениеУведомления.Вывести(Макет.ПолучитьОбласть("ОсновнаяЧасть"));
	УведомлениеОСпецрежимахНалогообложения.УстановитьФорматыВПолях(ЭтотОбъект);
	СтрДанных = Неопределено;
	Для Каждого Элт Из ДанныеМногостраничныхРазделов[ТекущееИДНаименования] Цикл 
		Если Элт.Значение.УИД = УИДТекущаяСтраница Тогда 
			СтрДанных = Элт.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл 
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Обл.СодержитЗначение Тогда 
			
			СтрДанных.Свойство(Обл.Имя, Обл.Значение);
		КонецЕсли;
	КонецЦикла;
	
	НайденнаяСтрока = УведомлениеОСпецрежимахНалогообложения.НайтиСтрокуВДеревеПоУИД(ДеревоСтраниц.ПолучитьЭлементы(), УИДТекущаяСтраница);
	Если НайденнаяСтрока <> Неопределено
		И НайденнаяСтрока.ПолучитьРодителя().ПолучитьЭлементы().Количество() = 1 Тогда 
		
		ПредставлениеУведомления.Области.УдалитьСтраницуЗначок.Текст = "";
		ПредставлениеУведомления.Области.УдалитьСтраницу.Текст = "";
		ПредставлениеУведомления.Области.УдалитьСтраницуЗначок.Гиперссылка = Ложь;
		ПредставлениеУведомления.Области.УдалитьСтраницу.Гиперссылка = Ложь;
		Элементы.ДеревоСтраницКонтекстноеМенюУдалитьСтраницу.Доступность = Ложь;
	Иначе
		Элементы.ДеревоСтраницКонтекстноеМенюУдалитьСтраницу.Доступность = Истина;
	КонецЕсли;
	
	Элементы.ДеревоСтраницКонтекстноеМенюДобавитьСтраницу.Доступность = Истина;
	ПоказатьМногострочныеЧасти(Макет, МногострочныеЧасти);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элемент, Область)
	Если ЭтоОбластьДоля(Область) Тогда 
		Если Область.Значение > 100 Тогда 
			Область.Значение = 100;
		КонецЕсли;
	КонецЕсли;

	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, Область, Истина);
	
	Если Область.Имя = "ДАТА_ПОДПИСИ" Тогда
		Объект.ДатаПодписи = Область.Значение;
		УстановитьДанныеПоРегистрацииВИФНС();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьДанныеПоРегистрацииВИФНС()
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.РегистрацияВИФНС, "Код,Представитель,КПП,ДокументПредставителя");
	ПредставлениеУведомления.Области["КодНО"].Значение = Реквизиты.Код;
	ПредставлениеУведомления.Области["КПП"].Значение = Реквизиты.КПП;
	
	ДанныеУведомленияТитульный = ДанныеУведомления["Титульная"];
	ДанныеУведомленияТитульный.Вставить("КодНО", ПредставлениеУведомления.Области["КодНО"].Значение);
	ДанныеУведомленияТитульный.Вставить("КПП", ПредставлениеУведомления.Области["КПП"].Значение);
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
			УстановитьПредставителяПоФизЛицу(Реквизиты.Представитель);
			ПредставлениеУведомления.Области["ПРИЗНАК_НП_ПОДВАЛ"].Значение = "4";
			ПредставлениеУведомления.Области["НаимДок"].Значение = Реквизиты.ДокументПредставителя;
		Иначе
			УстановитьПредставителяПоОрганизации();
			ПредставлениеУведомления.Области["ПРИЗНАК_НП_ПОДВАЛ"].Значение = "3";
			ПредставлениеУведомления.Области["НаимДок"].Значение = "";
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
			УстановитьПредставителяПоФизЛицу(Реквизиты.Представитель);
			ПредставлениеУведомления.Области["ПРИЗНАК_НП_ПОДВАЛ"].Значение = "2";
			ПредставлениеУведомления.Области["НаимДок"].Значение = Реквизиты.ДокументПредставителя;
		Иначе
			УстановитьПредставителяПоОрганизации();
			ПредставлениеУведомления.Области["ПРИЗНАК_НП_ПОДВАЛ"].Значение = "1";
			ПредставлениеУведомления.Области["НаимДок"].Значение = "";
		КонецЕсли;
	КонецЕсли;
	
	ДанныеУведомленияТитульный.Вставить("ПРИЗНАК_НП_ПОДВАЛ", ПредставлениеУведомления.Области["ПРИЗНАК_НП_ПОДВАЛ"].Значение);
	ДанныеУведомленияТитульный.Вставить("НаимДок", ПредставлениеУведомления.Области["НаимДок"].Значение);
	ДанныеУведомленияТитульный.Вставить("ДАТА_ПОДПИСИ", ПредставлениеУведомления.Области["ДАТА_ПОДПИСИ"].Значение);
	ДанныеУведомленияТитульный.Вставить("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ", ПредставлениеУведомления.Области["ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"].Значение);
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставителяПоФизЛицу(Физлицо)
	ЕстьОбласть = (Неопределено <> ПредставлениеУведомления.Области.Найти("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"));
	ДанныеУведомленияТитульный = ДанныеУведомления["Титульная"];
	Если ЗначениеЗаполнено(Физлицо) Тогда 
		СведенияОПредставителе = РегламентированнаяОтчетностьВызовСервера.ПолучитьПоКодамСведенияОПредставителе(
			Объект.Организация, 
			ДанныеУведомленияТитульный["КодНО"], 
			ДанныеУведомленияТитульный["КПП"]);
		
		Если ЗначениеЗаполнено(СведенияОПредставителе.НаименованиеОрганизацииПредставителя) Тогда 
			ПодписантСтр = СведенияОПредставителе.ФИОПредставителя;
			ФИО = РегламентированнаяОтчетность.РазложитьФИО(СведенияОПредставителе.ФИОПредставителя);
			Объект.ПодписантФамилия = СокрЛП(ФИО.Фамилия);
			Объект.ПодписантИмя = СокрЛП(ФИО.Имя);
			Объект.ПодписантОтчество = СокрЛП(ФИО.Отчество);
		Иначе
			ДанныеПредставителя = РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОФизЛице(Физлицо, , Объект.ДатаПодписи);
			Объект.ПодписантФамилия = СокрЛП(ДанныеПредставителя.Фамилия);
			Объект.ПодписантИмя = СокрЛП(ДанныеПредставителя.Имя);
			Объект.ПодписантОтчество = СокрЛП(ДанныеПредставителя.Отчество);
			ПодписантСтр = СокрЛП(Объект.ПодписантФамилия + " " + Объект.ПодписантИмя + " " + Объект.ПодписантОтчество);
		КонецЕсли;
		ДанныеУведомленияТитульный.Вставить("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ", ПодписантСтр);
		Если ЕстьОбласть Тогда 
			ПредставлениеУведомления.Области["ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"].Значение = ПодписантСтр;
		КонецЕсли;
	Иначе
		Объект.ПодписантФамилия = "";
		Объект.ПодписантИмя = "";
		Объект.ПодписантОтчество = "";
		ДанныеУведомленияТитульный.Вставить("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ", "");
		Если ЕстьОбласть Тогда 
			ПредставлениеУведомления.Области["ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"].Значение = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставителяПоОрганизации()
	Документы.УведомлениеОСпецрежимахНалогообложения.УстановитьДанныеРуководителя(Объект);
	ДанныеУведомленияТитульный = ДанныеУведомления["Титульная"];
	ЕстьОбласть = (Неопределено <> ПредставлениеУведомления.Области.Найти("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"));
	ПодписантСтр = СокрЛП(Объект.ПодписантФамилия + " " + Объект.ПодписантИмя + " " + Объект.ПодписантОтчество);
	ДанныеУведомленияТитульный.Вставить("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ", ПодписантСтр);
	Если ЕстьОбласть Тогда 
		ПредставлениеУведомления.Области["ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"].Значение = ПодписантСтр;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранитьДанные() Экспорт
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Модифицированность Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДатаСеанса() 
	КонецЕсли;
	
	Если ТекущиеМногострочныеЧасти.Количество() > 0 Тогда 
		СобратьДанныеМногострочныхЧастейТекущейСтраницы(ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	КонецЕсли;
	
	ДанныеДопСтрокБД = Новый Структура;
	Для Каждого КЗ Из ДанныеДопСтрок Цикл 
		ДанныеДопСтрокБД.Вставить(КЗ.Ключ, ПолучитьИзВременногоХранилища(КЗ.Значение));
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура;
			
	СтруктураПараметров.Вставить("ИдентификаторыОбычныхСтраниц", ИдентификаторыОбычныхСтраниц);
	СтруктураПараметров.Вставить("ДанныеДопСтрокБД", ДанныеДопСтрокБД);
	СтруктураПараметров.Вставить("ДеревоСтраниц", РеквизитФормыВЗначение("ДеревоСтраниц"));
	СтруктураПараметров.Вставить("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов);
	СтруктураПараметров.Вставить("ДанныеУведомления", ДанныеУведомления);
	СтруктураПараметров.Вставить("РазрешитьВыгружатьСОшибками", РазрешитьВыгружатьСОшибками);
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ДанныеУведомления = Новый ХранилищеЗначения(СтруктураПараметров);
	Документ.Записать();
	ЗначениеВДанныеФормы(Документ, Объект);
	
	РегламентированнаяОтчетность.СохранитьСтатусОтправкиУведомления(ЭтаФорма);
	
	Модифицированность = Ложь;
	ЭтотОбъект.Заголовок = СтрЗаменить(ЭтотОбъект.Заголовок, " (создание)", "");
	
	УведомлениеОСпецрежимахНалогообложения.СохранитьНастройкиРучногоВвода(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанные(СсылкаНаДанные)
	СтруктураПараметров = СсылкаНаДанные.Ссылка.ДанныеУведомления.Получить();
	ДанныеУведомления = СтруктураПараметров.ДанныеУведомления;
	ДанныеМногостраничныхРазделов = СтруктураПараметров.ДанныеМногостраничныхРазделов;
	ЗначениеВРеквизитФормы(СтруктураПараметров.ДеревоСтраниц, "ДеревоСтраниц");
	СтруктураПараметров.Свойство("ИдентификаторыОбычныхСтраниц", ИдентификаторыОбычныхСтраниц);
	
	ДанныеДопСтрокБД = СтруктураПараметров.ДанныеДопСтрокБД;
	ДанныеДопСтрок = Новый Структура;
	ДанныеДопСтрокСтраницы = Новый Структура;
	Для Каждого КЗ Из ДанныеДопСтрокБД Цикл 
		ДанныеДопСтрок.Вставить(КЗ.Ключ, ПоместитьВоВременноеХранилище(КЗ.Значение, Новый УникальныйИдентификатор));
		Стр = Новый Структура;
		Для Каждого Кол Из КЗ.Значение.Колонки Цикл 
			Если Кол.Имя <> "УИД" Тогда 
				Стр.Вставить(Кол.Имя);
			КонецЕсли;
		КонецЦикла;
		СЗ = Новый СписокЗначений;
		СЗ.Добавить(Стр);
		ДанныеДопСтрокСтраницы.Вставить(КЗ.Ключ, СЗ);
	КонецЦикла;
	
	СтруктураПараметров.Свойство("РазрешитьВыгружатьСОшибками", РазрешитьВыгружатьСОшибками);
КонецПроцедуры

&НаСервере
Процедура СкопироватьСтраницуНаСервере()
	СобратьДанныеМногострочныхЧастейТекущейСтраницы(ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	НовИд = УведомлениеОСпецрежимахНалогообложения.КопироватьСтраницуУведомления(ЭтотОбъект, СПодчиненными);
	Если НовИд <> Неопределено Тогда 
		Элементы.ДеревоСтраниц.ТекущаяСтрока = НовИд;
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор(Элемент, Область, СтандартнаяОбработка)
	Если Область.Имя = "ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ" Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораПодписантаЗавершение", ЭтотОбъект, Неопределено);
		УведомлениеОСпецрежимахНалогообложенияКлиент.ОткрытьФормуВыбораФИО(ЭтотОбъект, СтандартнаяОбработка, "ПредставлениеУведомления",
																	"ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ", ОписаниеОповещения);
		Возврат;
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "СкопироватьСтраницу") > 0 Тогда
		СкопироватьСтраницуНаСервере();
		СтандартнаяОбработка = Ложь;
		Возврат;
	ИначеЕсли СтрНачинаетсяС(Область.Имя, "AddStr_") Или СтрНачинаетсяС(Область.Имя, "AddStrLabel_") Тогда
		ДобавитьСтроку(Область.Имя);
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли СтрНачинаетсяС(Область.Имя, "Del_") И Область.Текст = "х" Тогда
		УдалитьСтроку(Область.Имя);
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "ДобавитьСтраницу") > 0 Тогда
		ДобавитьСтраницу(Неопределено);
		СтандартнаяОбработка = Ложь;
		Возврат;
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "УдалитьСтраницу") > 0 Тогда
		УведомлениеОСпецрежимахНалогообложенияКлиент.УдалитьСтраницу(ЭтотОбъект);
		СтандартнаяОбработка = Ложь;
		Возврат;
	ИначеЕсли Область.Имя = "ПересчитатьДанныеЦепочек" Тогда 
		ПересчитатьДанныеЦепочек();
		СтандартнаяОбработка = Ложь;
		Возврат;
	ИначеЕсли (Область.Имя = "НомерКИК" И ТекущееИДНаименования = "ЛистВ")
		Или (Область.Имя = "НомерКИК" И ТекущееИДНаименования = "ЛистГ")
		Или (Область.Имя = "НомерКИК" И ТекущееИДНаименования = "ЛистГ2") Тогда 
		
		СтандартнаяОбработка = Ложь;
		ВыборНомерУчастника = Новый ОписаниеОповещения("ВыборНомерУчастникаЗавершение", ЭтотОбъект);
		ПараметрыВыборНомерУчастника = Новый Структура("ЗначениеОбласти, ИмяОбласти", Область.Значение, Область.Имя);
		ОткрытьФорму("Отчет.РегламентированноеУведомлениеКИК.Форма.ФормаВыборНомерУчастника", ПараметрыВыборНомерУчастника, ЭтотОбъект, , , , ВыборНомерУчастника, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Возврат;
	ИначеЕсли СтрНачинаетсяС(Область.Имя, "НомерУч" + РазделительНомераСтроки) Тогда 
		СтандартнаяОбработка = Ложь;
		ВыборНомерУчастника = Новый ОписаниеОповещения("ВыборНомерУчастникаЗавершение", ЭтотОбъект);
		ПараметрыВыборНомерУчастника = Новый Структура("ЗначениеОбласти, ИмяОбласти, ДоступенВыборРО", Область.Значение, Область.Имя);
		ОткрытьФорму("Отчет.РегламентированноеУведомлениеКИК.Форма.ФормаВыборНомерУчастника", ПараметрыВыборНомерУчастника, ЭтотОбъект, , , , ВыборНомерУчастника, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Возврат;
	Иначе
	КонецЕсли;
	
	Если РучнойВвод Тогда 
		Возврат;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда 
		УведомлениеОСпецрежимахНалогообложенияКлиент.ПредставлениеУведомленияВыбор(ЭтотОбъект, Область, СтандартнаяОбработка, Истина, Истина);
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда 
		УведомлениеОСпецрежимахНалогообложенияКлиент.ПредставлениеУведомленияВыборКодЗнач(ЭтотОбъект, Область, СтандартнаяОбработка, Истина);
	КонецЕсли;
	
	Если Область.Имя = "КодНО" Тогда 
		СтандартнаяОбработка = Ложь;
		ОбработкаКодаНО(Область.Имя);
	КонецЕсли;
	
	Если СтандартнаяОбработка И ЭтоОбластьОКСМ(Область) Тогда
		СтандартнаяОбработка = Ложь;
		ДополнительныеПараметры = Новый Структура("Область, СтандартнаяОбработка, Элемент", Область, СтандартнаяОбработка, Элемент);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраныЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Справочник.СтраныМира.ФормаВыбора", Новый Структура("РежимВыбора", Истина), ЭтотОбъект,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДанныеЦепочек()
	НомерУчастника =НРег(ПредставлениеУведомления.Области["НомерКИК"].Значение);
	Если Не ЗначениеЗаполнено(НомерУчастника) Или 
		(Не СтрНачинаетсяС(НомерУчастника, "ио-") И Не СтрНачинаетсяС(НомерУчастника, "ис-")) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Некорректно указан номер участника'"));
		Возврат;
	КонецЕсли;
	
	ОТЧ = Новый ОписаниеТипов("Число");
	ИО_Номер = ОТЧ.ПривестиЗначение(Сред(НомерУчастника, 4));
	Если Не ЗначениеЗаполнено(ИО_Номер) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Некорректно указан номер участника'"));
		Возврат;
	КонецЕсли;
	
	ЛистыИО = Новый СписокЗначений;
	ЛистыДляОтбора = ?(СтрНачинаетсяС(НомерУчастника, "ио-"), ДанныеМногостраничныхРазделов.ЛистА, ДанныеМногостраничныхРазделов.ЛистБ);
	Для Каждого Лист Из ЛистыДляОтбора Цикл 
		Если Лист.Значение.НомерКИКЧисло = ИО_Номер Тогда
			ЛистыИО.Добавить(Лист.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ЛистыИО.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не найден соответствующий лист А/Б'"));
		Возврат;
	ИначеЕсли ЛистыИО.Количество() > 1 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Найдено более одного листа А/Б с указанным номером'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПересчитатьДанныеЦепочекЗавершение", ЭтотОбъект, Новый Структура("НомерУчастника", НомерУчастника));
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Произвести пересчет цепочек (операция не может быть отменена)?'"), РежимДиалогаВопрос.ДаНет, 0);
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДанныеЦепочекЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОбщаяДоляУчастия = 0;
		ПересчитатьДанныеЦепочекНаСервере(ДополнительныеПараметры.НомерУчастника, ОбщаяДоляУчастия);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьДанныеЦепочекНаСервере(НомерУчастника, ОбщаяДоляУчастия)
	СобратьДанныеМногострочныхЧастейТекущейСтраницы(ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	ОбщаяДоляУчастия = 0;
	ДанныеДопСтрокМнгСтр = ПолучитьИзВременногоХранилища(ДанныеДопСтрок.МнгСтр);
	
	Для Каждого Стр Из ДанныеМногостраничныхРазделов.ЛистГ Цикл 
		Если НРег(Стр.Значение.НомерКИК) <> НомерУчастника Тогда
			Продолжить;
		КонецЕсли;
		
		МнгСтр = ДанныеДопСтрокМнгСтр.НайтиСтроки(Новый Структура("УИД", Стр.Значение.УИД));
		ДУчИзн = Число(МнгСтр[0].ДоляУчПрям);
		Для Инд = 1 По МнгСтр.ВГраница() Цикл 
			ДУчИзн = ДУчИзн * Число(МнгСтр[Инд].ДоляУчПрям) / 100;
			МнгСтр[Инд].ДоляУчКосв = ДУчИзн;
		КонецЦикла;
		
		Стр.Значение.ДоляКосвПослИт = ДУчИзн;
		ОбщаяДоляУчастия = ОбщаяДоляУчастия + ДУчИзн;
	КонецЦикла;
	
	УдалениеСтраницы = Истина;
	ПоказатьТекущуюМногостраничнуюСтраницу(ТекущийМакет, ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	УдалениеСтраницы = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВыборНомерУчастникаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда 
		ТипЧисло = Новый ОписаниеТипов("Число");
		МассивДопОбл = Неопределено;
		Результат = Неопределено;
		ИмяОбласти = Неопределено;
		
		Ответ.Свойство("ИмяОбласти", ИмяОбласти);
		Ответ.Свойство("Результат", Результат);
		Если Ответ.Свойство("Очистить") Тогда 
			Результат = "";
		КонецЕсли;
		Область = ПредставлениеУведомления.Области[ИмяОбласти];
		Область.Значение = Результат;
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(ПредставлениеУведомления, Область);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКодаНО(Инфо)
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуВыбораРегистрацииВИФНС(ЭтотОбъект, Инфо);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКодаНОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Инфо = ДополнительныеПараметры.Инфо;
	
	Если Результат <> Неопределено Тогда 
		Объект.РегистрацияВИФНС = Результат;
		УстановитьДанныеПоРегистрацииВИФНС();
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПодписантаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Свойство("Фамилия", Объект.ПодписантФамилия);
		Результат.Свойство("Имя", Объект.ПодписантИмя);
		Результат.Свойство("Отчество", Объект.ПодписантОтчество);
		Представление = СокрЛП(Объект.ПодписантФамилия + " " + Объект.ПодписантИмя + " " + Объект.ПодписантОтчество);
		Область = ПредставлениеУведомления.Области.Найти("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ");
		Область.Значение = Представление;
		УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, Область, Истина);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ЭтоОбластьОКСМ(Область)
	Если (Область.Имя = "СтрРег" И (ТекущееИДНаименования = "ЛистА" Или ТекущееИДНаименования = "ЛистБ"
				Или ТекущееИДНаименования = "ЛистСведФЛ" Или ТекущееИДНаименования = "ЛистГ2"))
		Или (Область.Имя = "ОКСМ" И ТекущееИДНаименования = "ЛистСведФЛ") Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Функция ЭтоОбластьДоля(Область)
	Если ((ТекущееИДНаименования = "ЛистА") И (Область.Имя = "ДоляПрямУч" Или Область.Имя = "ДоляКосУч" Или Область.Имя = "ДоляСмешУч" Или Область.Имя = "ДоляУчСупДет" Или Область.Имя = "ДоляУчНалРезРФ"))
		Или ((ТекущееИДНаименования = "ЛистГ") И (Область.Имя = "ДоляУчКосвИт" Или Область.Имя = "ДоляКосвПослИт") 
													Или СтрНачинаетсяС(Область.Имя, "ДоляУчКосв" + РазделительНомераСтроки)
													Или СтрНачинаетсяС(Область.Имя, "ДоляУчПрям" + РазделительНомераСтроки))Тогда 
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ДобавитьСтраницу(Команда)
	ДобавитьСтраницуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтраницуНаСервере()
	НовИд = УведомлениеОСпецрежимахНалогообложения.ДобавитьСтраницуУведомления(ЭтотОбъект);
	Если НовИд <> Неопределено Тогда 
		Элементы.ДеревоСтраниц.ТекущаяСтрока = НовИд;
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницу() Экспорт
	УдалениеСтраницы = Истина;
	УдалитьСтраницуНаСервере();
	УдалениеСтраницы = Ложь;
КонецПроцедуры

&НаСервере
Процедура УдалитьСтраницуНаСервере()
	Для Каждого Стр Из ТекущиеМногострочныеЧасти Цикл 
		ТЗ = ПолучитьИзВременногоХранилища(ДанныеДопСтрок[Стр.Значение]);
		Строки = ТЗ.НайтиСтроки(Новый Структура("УИД", УИДТекущаяСтраница));
		Для Каждого СтрМнг Из Строки Цикл 
			ТЗ.Удалить(СтрМнг);
		КонецЦикла;
		ДанныеДопСтрок[Стр.Значение] = ПоместитьВоВременноеХранилище(ТЗ, ДанныеДопСтрок[Стр.Значение]);
	КонецЦикла;
	
	НовИд = УведомлениеОСпецрежимахНалогообложения.УдалитьСтраницуНаСервере(ЭтотОбъект);
	Если НовИд <> Неопределено Тогда 
		Элементы.ДеревоСтраниц.ТекущаяСтрока = НовИд;
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	Если Модифицированность Тогда 
		СохранитьДанные();
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УведомлениеОбъект = Объект.Ссылка.ПолучитьОбъект();
		Если УведомлениеОбъект.Заблокирован() Тогда 
			УведомлениеОбъект.Разблокировать();
		КонецЕсли;
		РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция КодЭлементаСправочника(Результат)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Результат, "Код");
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраныЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		КодЭлементаСправочника = КодЭлементаСправочника(Результат);
		Область = ДополнительныеПараметры.Область;
		Если Область.Значение <> КодЭлементаСправочника Тогда
			Область.Значение = КодЭлементаСправочника;
			Модифицированность = Истина;
		КонецЕсли;
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элементы.ПредставлениеУведомления, Область);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтроку(ИмяОбласти)
	ИмяОбласти = СтрЗаменить(ИмяОбласти, "AddStr_", "");
	ИмяОбласти = СтрЗаменить(ИмяОбласти, "AddStrLabel_", "");
	
	ДопСтрокиТекущейСтраницы = ДанныеДопСтрокСтраницы[ИмяОбласти];
	НомерДобавляемойСтроки = ДопСтрокиТекущейСтраницы.Количество() + 1;
	НоваяСтрока = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеДопСтрокСтраницы[ИмяОбласти][0].Значение);
	Для Каждого КЗ Из НоваяСтрока Цикл 
		НоваяСтрока[КЗ.Ключ] = Неопределено;
	КонецЦикла;
	ДопСтрокиТекущейСтраницы.Добавить(НоваяСтрока);
	Верх = ПредставлениеУведомления.Области[КЗ.Ключ + РазделительНомераСтроки + Формат(НомерДобавляемойСтроки - 1, "ЧГ=0")].Верх + 1;
	Обл = УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, ТекущийМакет).ПолучитьОбласть("Str_" + ИмяОбласти);
	ИндСтр = РазделительНомераСтроки + Формат(НомерДобавляемойСтроки, "ЧГ=0");
	Для Каждого ОблПодч Из Обл.Области Цикл 
		Если ОблПодч.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			ОблПодч.Имя = ОблПодч.Имя + ИндСтр;
		КонецЕсли;
	КонецЦикла;
	ПредставлениеУведомления.ВставитьОбласть(Обл.Область(), ПредставлениеУведомления.Область(Верх,,Верх,), ТипСмещенияТабличногоДокумента.ПоВертикали);
	ПредставлениеУведомления.Области["Str_" + ИмяОбласти].Имя = "";
	Для Инд = 1 По НомерДобавляемойСтроки Цикл 
		ОблПодч = ПредставлениеУведомления.Области["Del_" + ИмяОбласти + РазделительНомераСтроки + Формат(Инд, "ЧГ=0")];
		ОблПодч.Текст = "х";
		ОблПодч.Гиперссылка = Истина;
	КонецЦикла;
	
	УстановитьФорматыВМногострочныхПолях(ПредставлениеУведомления);
КонецПроцедуры

&НаСервере
Процедура УдалитьСтроку(ИмяОбласти)
	ОТЧ = Новый ОписаниеТипов("Число");
	Разложение = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(ИмяОбласти, "Del_", ""), РазделительНомераСтроки);
	ДопСтроки = Разложение[0];
	Номер = ОТЧ.ПривестиЗначение(Разложение[1]);
	ДанныеДопСтрокСтраницы[ДопСтроки].Удалить(Номер-1);
	Верх = ПредставлениеУведомления.Области[ИмяОбласти].Верх;
	ПредставлениеУведомления.УдалитьОбласть(ПредставлениеУведомления.Область(Верх,,Верх), ТипСмещенияТабличногоДокумента.ПоВертикали);
	Низ = Верх + ДанныеДопСтрокСтраницы[ДопСтроки].Количество() - Номер;
	Если Низ >= Верх Тогда 
		Области = ПредставлениеУведомления.ПолучитьОбласть(Верх,,Низ).Области;
		СоответствиеИмен = Новый Соответствие;
		МаксИнд = 1;
		МинИнд = 10000;
		Для Каждого Обл Из Области Цикл 
			Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				П = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Обл.Имя, РазделительНомераСтроки);
				Если П.Количество() = 2 Тогда 
					СоответствиеИмен[Обл.Имя] = П[0] + РазделительНомераСтроки + Формат(ОТЧ.ПривестиЗначение(П[1]) - 1, "ЧГ=0");
					Если (ОТЧ.ПривестиЗначение(П[1])) > МаксИнд Тогда 
						МаксИнд = (ОТЧ.ПривестиЗначение(П[1]));
					КонецЕсли;
					Если (ОТЧ.ПривестиЗначение(П[1])) < МинИнд Тогда 
						МинИнд = (ОТЧ.ПривестиЗначение(П[1]));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Пока МинИнд <= МаксИнд Цикл 
			Суффикс = РазделительНомераСтроки + Формат(МинИнд, "ЧГ=");
			СуффиксДл = СтрДлина(Суффикс);
			
			Для Каждого КЗ Из СоответствиеИмен Цикл
				Если Прав(КЗ.Ключ, СуффиксДл) = Суффикс Тогда 
					ПредставлениеУведомления.Области[КЗ.Ключ].Имя = КЗ.Значение;
				КонецЕсли;
			КонецЦикла;
			
			МинИнд = МинИнд + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеДопСтрокСтраницы[ДопСтроки].Количество() = 1 Тогда
		ОблПодч = ПредставлениеУведомления.Области["Del_" + Разложение[0] + РазделительНомераСтроки + "1"];
		ОблПодч.Текст = "";
		ОблПодч.Гиперссылка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ОбластьИмя) Экспорт 
КонецФункции

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстВопроса = "Перед печатью необходимо сохранить изменения. Сохранить изменения?";
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредварительныйПросмотрЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);
		Возврат;
	ИначеЕсли Модифицированность Тогда 
		СохранитьДанные();
	КонецЕсли;
	
	МассивПечати = Новый Массив;
	МассивПечати.Добавить(Объект.Ссылка);
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УведомлениеОСпецрежимахНалогообложения",
		"Уведомление", МассивПечати, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьДанные();
		МассивПечати = Новый Массив;
		МассивПечати.Добавить(Объект.Ссылка);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.УведомлениеОСпецрежимахНалогообложения",
			"Уведомление", МассивПечати, Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СформироватьXMLНаСервере(УникальныйИдентификатор)
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ВыгрузитьДокумент(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура СформироватьXML(Команда)
	
	ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если ВыгружаемыеДанные <> Неопределено Тогда 
		РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСДвухмернымШтрихкодомPDF417(Команда)
	РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимах(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Функция СформироватьВыгрузкуИПолучитьДанные() Экспорт 
	Выгрузка = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если Выгрузка = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	Выгрузка = Выгрузка[0];
	СтруктураВыгрузки = Новый Структура("ТестВыгрузки,КодировкаВыгрузки", 
			Выгрузка.ТестВыгрузки, Выгрузка.КодировкаВыгрузки);
	СтруктураВыгрузки.Вставить("Данные", УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетДвоичныхДанных(Объект.ИмяОтчета, "TIFF_2021_1"));
	СтруктураВыгрузки.Вставить("ИмяФайла", "1120416_5.03000_04.tif");
	Возврат СтруктураВыгрузки;
КонецФункции

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь,ВыполняемоеОповещение = Неопределено) Экспорт 
	
	СохранитьДанные();
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
	Закрыть(Неопределено);
КонецПроцедуры

#Область ОтправкаВФНС
////////////////////////////////////////////////////////////////////////////////
// Отправка в ФНС
&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru='Вы уверены, что уведомление уже сдано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ПроверитьВыгрузкуНаСервере()
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ПроверитьДокументСВыводомВТаблицу(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	ТаблицаОшибок = ПроверитьВыгрузкуНаСервере();
	Если ТаблицаОшибок.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибок не обнаружено");
	Иначе
		ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.НавигацияПоОшибкам", Новый Структура("ТаблицаОшибок", ТаблицаОшибок), ЭтотОбъект, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьУведомлениеИОткрытьФормуПрисоединенныеФайлы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьБРО(Команда)
	ПечатьБРОНаСервере();
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтотОбъект, "Открыть", Ложь, СтруктураРеквизитовУведомления.СписокПечатаемыхЛистов);
КонецПроцедуры

&НаСервере
Процедура ПечатьБРОНаСервере()
	УведомлениеОСпецрежимахНалогообложения.ПечатьУведомленияБРО(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РучнойВвод(Команда)
	РучнойВвод = Не РучнойВвод;
	Элементы.ФормаРучнойВвод.Пометка = РучнойВвод;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "УведомлениеОСпецрежимахНалогообложения_НавигацияПоОшибкам" Тогда 
		УведомлениеОСпецрежимахНалогообложенияКлиент.ОбработкаОповещенияНавигацииПоОшибкам(ЭтотОбъект, Параметр, Источник);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОКСМ(Команда)
	ПредставлениеУведомления.ТекущаяОбласть.Значение = Неопределено;
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, ПредставлениеУведомления.ТекущаяОбласть, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриАктивизацииОбласти(Элемент)
	Если ПредставлениеУведомления.ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда 
		Элементы.ПредставлениеУведомленияКонтекстноеМенюОчиститьОКСМ.Доступность = ЭтоОбластьОКСМ(ПредставлениеУведомления.ТекущаяОбласть);
	Иначе
		Элементы.ПредставлениеУведомленияКонтекстноеМенюОчиститьОКСМ.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьВыгружатьСОшибками(Команда)
	РазрешитьВыгружатьСОшибками = Не РазрешитьВыгружатьСОшибками;
	Элементы.ФормаРазрешитьВыгружатьСОшибками.Пометка = РазрешитьВыгружатьСОшибками;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#Область ПереносДанныхФормы2017
&НаКлиенте
Процедура ПеренестиДанныеИзПрошлойРедакции(Команда)
	ДополнительныеПараметры = Новый Структура("ИмяФормы", "Форма2017_1");
	ПоказатьВопрос(Новый ОписаниеОповещения("ПеренестиДанныеИзПрошлойРедакцииПродолжение", ЭтотОбъект, ДополнительныеПараметры),
		НСтр("ru='Текущий отчет будет очищен. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиДанныеИзПрошлойРедакцииПродолжение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ОО = Новый ОписаниеОповещения("ПеренестиДанныеИзПрошлойРедакцииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОтборПрошлойРедакции = Новый Структура("ВидУведомления, Организация, ИмяФормы", 
			Объект.ВидУведомления, Объект.Организация, ДополнительныеПараметры.ИмяФормы);
		ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбора",
			Новый Структура("Отбор", ОтборПрошлойРедакции), ЭтотОбъект,,,, ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиДанныеИзПрошлойРедакцииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда 
		Если ДополнительныеПараметры.ИмяФормы = "Форма2017_1" Тогда 
			ЗаполнитьНаОснованииФормы2017(Результат);
			УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Неопределено);
		ИначеЕсли ДополнительныеПараметры.ИмяФормы = "Форма2019_1" Тогда
			Если ЗагрузитьФормат502(Результат) Тогда 
				УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаОснованииФормы2017(Уведомление)
	ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Уведомление, "РегистрацияВИФНС");
	Объект.РегистрацияВИФНС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Уведомление, "РегистрацияВИФНС");
	ПреобразованныеДанные = ПолучитьПреобразованныеДанные2017(Уведомление);
	ЗагрузитьДанные(Новый Структура("Ссылка", Новый Структура("ДанныеУведомления", ПреобразованныеДанные)));
	ИдДляСвор = УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект);
	СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(ИдДляСвор);
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Функция ПолучитьПреобразованныеДанные2017(Уведомление2017)
	Если Не ЗначениеЗаполнено(Уведомление2017) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	НовДокОбъект = Документы.УведомлениеОСпецрежимахНалогообложения.СоздатьДокумент();
	СтарДокОбъект = Уведомление2017.ПолучитьОбъект();
	Данные = СтарДокОбъект.ДанныеУведомления.Получить();
	
	ЗаполнитьЗначенияСвойств(НовДокОбъект, СтарДокОбъект);
	НовДокОбъект.Дата = ТекущаяДатаСеанса();
	НовДокОбъект.ДатаПодписи = ТекущаяДатаСеанса();
	НовДокОбъект.Номер = Неопределено;
	НовДокОбъект.ИмяФормы = "Форма2021_1";
	НовДокОбъект.Комментарий = "Сформировано на основании формы в редакции от 13.12.2016 № ММВ-7-13/679@";
	
	Данные.ДанныеУведомления.Титульная.Вставить("ФормРеорг", "");
	Данные.ДанныеУведомления.Титульная.Вставить("ИННРеорг", "");
	Данные.ДанныеУведомления.Титульная.Вставить("КППРеорг", "");
	
	Если Данные.ДанныеУведомления.Свойство("ЛистСведФЛ") Тогда
		Данные.ДанныеУведомления.Титульная.Удалить("Пол");
		Данные.ДанныеУведомления.Титульная.Удалить("ПрАдр");
		Данные.ДанныеУведомления.Титульная.Удалить("Индекс");
		Данные.ДанныеУведомления.Титульная.Удалить("КодРегион");
		Данные.ДанныеУведомления.Титульная.Удалить("Район");
		Данные.ДанныеУведомления.Титульная.Удалить("Город");
		Данные.ДанныеУведомления.Титульная.Удалить("НаселПункт");
		Данные.ДанныеУведомления.Титульная.Удалить("Улица");
		Данные.ДанныеУведомления.Титульная.Удалить("Дом");
		Данные.ДанныеУведомления.Титульная.Удалить("Корпус");
		Данные.ДанныеУведомления.Титульная.Удалить("Кварт");
	КонецЕсли;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистА Цикл 
		ДЛ = Лист.Значение;
		ДЛ.Удалить("НаимОрг");
		ДЛ.Вставить("ДатаРегИО", "");
		ДЛ.Вставить("КодНПРезид", "");
	КонецЦикла;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистБ Цикл 
		ДЛ = Лист.Значение;
		ДЛ.НаимДокУчр = ДЛ.НаимДокУчрЛат;
		ДЛ.Удалить("НаимОрг");
		ДЛ.Удалить("НаимДокУчрЛат");
	КонецЦикла;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистВ Цикл 
		ДЛ = Лист.Значение;
		ДЛ.Вставить("Код1009", "");
	КонецЦикла;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистГ1 Цикл 
		ДЛ = Лист.Значение;
		ДЛ.Вставить("ПрПредДокОсв", "");
	КонецЦикла;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистГ2 Цикл 
		ДЛ = Лист.Значение;
		ДЛ.Удалить("НаимОрг");
	КонецЦикла;
	
	КонвертироватьДерево2017(Данные.ДеревоСтраниц.Строки);
	Возврат Новый ХранилищеЗначения(Данные);
КонецФункции

&НаСервере
Процедура КонвертироватьДерево2017(Строки)
	Для Каждого Стр Из Строки Цикл 
		Стр.МакетыПФ = СтрЗаменить(Стр.МакетыПФ, "Форма2017_1", "Форма2021_1");
		Если ЗначениеЗаполнено(Стр.ИмяМакета) Тогда
			Если Стр.ИмяМакета = "Титульная" Тогда 
				Стр.ИмяМакета = Стр.ИмяМакета + "_2021";
			Иначе
				Стр.ИмяМакета = Стр.ИмяМакета + "_2019";
			КонецЕсли;
		КонецЕсли;
		КонвертироватьДерево2017(Стр.Строки);
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

&НаКлиенте
Процедура ЗагрузитьИзXML(ПараметрыЗагрузкиXML) Экспорт
	ЗагрузитьИзXMLНаСервере(ПараметрыЗагрузкиXML);
	Элементы.ДеревоСтраниц.ТекущаяСтрока = ДеревоСтраниц.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуОсобыхПолей()
	ТаблицаОсобыхПолейВВыгрузке = УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуПутейВВыгрузке();
	
	НовСтр = ТаблицаОсобыхПолейВВыгрузке.Добавить();
	НовСтр.ПутьXML = "Файл/Документ/УведУчКИК/СведКИКЯвлИО/НомерКИК";
	
	НовСтр = ТаблицаОсобыхПолейВВыгрузке.Добавить();
	НовСтр.ПутьXML = "Файл/Документ/УведУчКИК/СведКИКЯвлИС/НомерКИК";
	
	Возврат ТаблицаОсобыхПолейВВыгрузке;
КонецФункции

&НаСервере
Процедура ЗагрузитьЛистыГ(ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры)
	МассивКолонок = Новый Соответствие;
	ДанныеДопСтрокМнгСтр = ПолучитьИзВременногоХранилища(ДанныеДопСтрок.МнгСтр);
	Для Каждого Кол Из ДанныеДопСтрокМнгСтр.Колонки Цикл 
		МассивКолонок.Вставить(Кол.Имя, Истина);
	КонецЦикла;
	
	СтрокаЛистГВДереве = ДеревоСтраниц.ПолучитьЭлементы()[ДеревоСтраниц.ПолучитьЭлементы().Количество() - 1].ПолучитьЭлементы()[0];
	ЛистыГ0 = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПути(ДеревоЗагрузки, "Файл/Документ/УведУчКИК/РаскрУчКИККосв", "ИмяЭлемента");
	Если ЛистыГ0 = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ЛистыГ0 = ЛистыГ0.Родитель.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "РаскрУчКИККосв"), Ложь);
	
	ВсегоСтрок = ЛистыГ0.Количество();
	Если ВсегоСтрок = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого ЛистГ0 Из ЛистыГ0 Цикл
		ПодчиненныеУзлыГ1Количество = ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "РаскрУчКИК"), Ложь).Количество();
		ВсегоСтрок = ВсегоСтрок + ?(ПодчиненныеУзлыГ1Количество > 0, ПодчиненныеУзлыГ1Количество - 1, 0);
	КонецЦикла;
	
	Для Инд = 2 По ВсегоСтрок Цикл 
		НоваяСтрокаЛистГВДереве = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, СтрокаЛистГВДереве);
		НоваяСтрокаЛистГВДереве.Наименование = "Стр. " + Формат(Инд, "ЧГ=0");
	КонецЦикла;
	
	ЛистГЭталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистГ[0].Значение);
	ЛистГ1Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистГ1[0].Значение);
	ЛистГ2Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистГ2[0].Значение);
	СтрокиВДереве = ДеревоСтраниц.ПолучитьЭлементы()[ДеревоСтраниц.ПолучитьЭлементы().Количество() - 1].ПолучитьЭлементы();
	
	ИндексЛистГ0 = 0;
	Для Каждого ЛистГ0 Из ЛистыГ0 Цикл
		ИндексЛистГ0Нач = ИндексЛистГ0;
		ЛистГЗнач = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГЭталон);
		ЛистГЗнач.УИД = Новый УникальныйИдентификатор;
		ДанныеМногостраничныхРазделов.ЛистГ.Добавить(ЛистГЗнач);
		СтрокиВДереве[ИндексЛистГ0].УИД = ЛистГЗнач.УИД;
		ИндексЛистГ0 = ИндексЛистГ0 + 1;
		
		Для Каждого Стр Из ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвУчКосвКИК"), Ложь) Цикл 
			Для Каждого Стр Из Стр.Строки Цикл 
				Если ЛистГЗнач.Свойство(Стр.ИмяЭлемента) Тогда
					ЛистГЗнач[Стр.ИмяЭлемента] = Стр.ЗначениеЭлемента;
				ИначеЕсли Стр.ИмяЭлемента = "НаимОргЛат" Тогда
					ЛистГЗнач["НаимОрг"] = Стр.ЗначениеЭлемента;
				КонецЕсли;
			КонецЦикла;
			Прервать;
		КонецЦикла;
		
		Инд = 0;
		Для Каждого ЛистГ1 Из ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "РаскрУчКИК"), Ложь) Цикл 
			Если Инд <> 0 Или ИндексЛистГ0Нач <> 0 Тогда
				ИндексЛистГ0 = ИндексЛистГ0 + 1;
				ЛистГЗнач = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГЭталон);
				ЛистГЗнач.УИД = Новый УникальныйИдентификатор;
				СтрокиВДереве[ИндексЛистГ0Нач + Инд].УИД = ЛистГЗнач.УИД;
				ДанныеМногостраничныхРазделов.ЛистГ.Добавить(ЛистГЗнач);
				Для Каждого Стр Из ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвУчКосвКИК"), Ложь) Цикл 
					Для Каждого Стр Из Стр.Строки Цикл 
						Если ЛистГЗнач.Свойство(Стр.ИмяЭлемента) Тогда
							ЛистГЗнач[Стр.ИмяЭлемента] = Стр.ЗначениеЭлемента;
						ИначеЕсли Стр.ИмяЭлемента = "НаимОргЛат" Тогда
							ЛистГЗнач["НаимОрг"] = Стр.ЗначениеЭлемента;
						КонецЕсли;
					КонецЦикла;
					Прервать;
				КонецЦикла;
			КонецЕсли;
			Для Каждого Стр Из ЛистГ1.Строки Цикл 
				Если ЛистГЗнач.Свойство(Стр.ИмяЭлемента) Тогда
					ЛистГЗнач[Стр.ИмяЭлемента] = Стр.ЗначениеЭлемента;
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого ЛистГ2 Из ЛистГ1.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СведУчПосл"), Ложь) Цикл
				НовСтр = ДанныеДопСтрокМнгСтр.Добавить();
				НовСтр.УИД = ЛистГЗнач.УИД;
				Для Каждого Стр Из ЛистГ2.Строки Цикл
					Если МассивКолонок[Стр.ИмяЭлемента] = Истина Тогда 
						НовСтр[Стр.ИмяЭлемента] = Стр.ЗначениеЭлемента;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			Инд = Инд + 1;
		КонецЦикла;
		
		СтрокаВДеревеПодч = СтрокиВДереве[ИндексЛистГ0Нач].ПолучитьЭлементы()[0].ПолучитьЭлементы()[0];
		Для Инд = 2 По ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвРосОрг"), Ложь).Количество() Цикл 
			СтрокаВДеревеПодч = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, СтрокаВДеревеПодч);
			СтрокаВДеревеПодч.Наименование = "Стр. " + Формат(Инд, "ЧГ=0");
		КонецЦикла;
		
		Инд = 0;
		Для Каждого ЛистГ1 Из ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвРосОрг"), Ложь) Цикл
			ЛистГ1Знач = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГ1Эталон);
			ЛистГ1Знач.УИД = Новый УникальныйИдентификатор;
			ЛистГ1Знач.УИДРодителя = ЛистГЗнач.УИД;
			ДанныеМногостраничныхРазделов.ЛистГ1.Добавить(ЛистГ1Знач);
			СтрокаВДеревеПодч.ПолучитьРодителя().ПолучитьЭлементы()[Инд].УИД = ЛистГ1Знач.УИД;
			
			Для Каждого Стр Из ЛистГ1.Строки Цикл 
				Если ЛистГ1Знач.Свойство(Стр.ИмяЭлемента) Тогда
					ЛистГ1Знач[Стр.ИмяЭлемента] = Стр.ЗначениеЭлемента;
				КонецЕсли;
			КонецЦикла;
			ЛистГ1Знач["НомерРосОрг"] = ОТЧ.ПривестиЗначение(Прав(Строка(ЛистГ1Знач["НомерРосОрг"]), 5));
			Инд = Инд + 1;
		КонецЦикла;
		
		СтрокаВДеревеПодч = СтрокиВДереве[ИндексЛистГ0Нач].ПолучитьЭлементы()[1].ПолучитьЭлементы()[0];
		Для Инд = 2 По ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвИО"), Ложь).Количество() Цикл 
			СтрокаВДеревеПодч = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, СтрокаВДеревеПодч);
			СтрокаВДеревеПодч.Наименование = "Стр. " + Формат(Инд, "ЧГ=0");
		КонецЦикла;
		
		Инд = 0;
		Для Каждого ЛистГ2 Из ЛистГ0.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвИО"), Ложь) Цикл
			ЛистГ2Знач = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГ2Эталон);
			ЛистГ2Знач.УИД = Новый УникальныйИдентификатор;
			ЛистГ2Знач.УИДРодителя = ЛистГЗнач.УИД;
			ДанныеМногостраничныхРазделов.ЛистГ2.Добавить(ЛистГ2Знач);
			СтрокаВДеревеПодч.ПолучитьРодителя().ПолучитьЭлементы()[Инд].УИД = ЛистГ2Знач.УИД;
			
			Для Каждого Стр Из ЛистГ2.Строки Цикл 
				Если ЛистГ2Знач.Свойство(Стр.ИмяЭлемента) Тогда
					ЛистГ2Знач[Стр.ИмяЭлемента] = Стр.ЗначениеЭлемента;
				ИначеЕсли Стр.ИмяЭлемента = "НомерИО" Тогда 
					ЛистГ2Знач["НомерКИК"] = Стр.ЗначениеЭлемента;
				КонецЕсли;
			КонецЦикла;
			Инд = Инд + 1;
		КонецЦикла;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ДанныеДопСтрокМнгСтр, ДанныеДопСтрок.МнгСтр);
	УдалениеЛишнихЛистов();
КонецПроцедуры

&НаСервере
Процедура УдалениеЛишнихЛистов()
	КУдалению = Новый Соответствие;
	Для Каждого КЗ Из ДанныеМногостраничныхРазделов Цикл 
		Для Каждого Элт Из ДанныеМногостраничныхРазделов[КЗ.Ключ] Цикл 
			Стр = УведомлениеОСпецрежимахНалогообложения.НайтиСтрокуВДеревеПоУИД(ДеревоСтраниц.ПолучитьЭлементы(), Элт.Значение.УИД);
			Если Стр = Неопределено Тогда 
				КУдалению.Вставить(Элт.Значение.УИД, Новый Структура("Элт, СЗ", Элт, ДанныеМногостраничныхРазделов[КЗ.Ключ]));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если КУдалению.Количество() > 0 Тогда 
		Для Каждого КЗ Из КУдалению Цикл 
			КЗ.Значение.СЗ.Удалить(КЗ.Значение.Элт);
		КонецЦикла;
		Модифицированность = Истина;
		СохранитьДанные();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзXMLНаСервере(ПараметрыЗагрузкиXML)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТаблицаОсобыхПолейВВыгрузке", ПолучитьТаблицуОсобыхПолей());
	УведомлениеОСпецрежимахНалогообложения.УстановитьОрганизациюПоПараметрамЗагрузки(ЭтотОбъект, ПараметрыЗагрузкиXML);
	
	ДеревоЗагрузки = УведомлениеОСпецрежимахНалогообложения.СформироватьДеревоЗагрузки(ПараметрыЗагрузкиXML.ПредставлениеXML);
	СхемаВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2021_1");
	ДеревоСтраниц.ПолучитьЭлементы().Очистить();
	СформироватьДеревоСтраниц();
	УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложения.ЗагрузитьОбычныеСтраницы(ЭтотОбъект, ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры);
	УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМногостраничныеСтраницы(ЭтотОбъект, ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры);
	ЗагрузитьЛистыГ(ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры);
	Если Не РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		ДанныеУведомления.Титульная.Наименование = УведомлениеОСпецрежимахНалогообложения.ПолучитьНаименованиеИПИзВыгрузки(ДеревоЗагрузки);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОсобаяОбработкаЭлементов(Путь, СтрЗагружаемоеЗначение, Страница) Экспорт 
	Если Путь = "Файл/Документ/УведУчКИК/СведКИКЯвлИО/НомерКИК" Тогда 
		Страница.НомерКИКЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/УведУчКИК/СведКИКЯвлИС/НомерКИК" Тогда
		Страница.НомерКИКЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьКартуМногостраничныхРазделов() Экспорт 
	КартаРазделов = УведомлениеОСпецрежимахНалогообложения.СформироватьПустуюКартуМногостраничныхРазделов();
	НовСтр = КартаРазделов.Строки.Добавить();
	НовСтр.ИДНаименования = "ЛистА";
	НовСтр = КартаРазделов.Строки.Добавить();
	НовСтр.ИДНаименования = "ЛистБ";
	НовСтр = КартаРазделов.Строки.Добавить();
	НовСтр.ИДНаименования = "ЛистВ";
	Возврат КартаРазделов;
КонецФункции

&НаКлиенте
Процедура ЗагрузитьИзФайлаВФормуУведомление(Команда)
	ОписаниеОповещенияОЗавершенииЗагрузки = Новый ОписаниеОповещения("ЗагрузитьИзФайлаВФормуУведомлениеЗавершение", ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложенияКлиент.ЗагрузитьИзФайлаУведомление(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПолучитьСворачиваемыеЭлементы()
	СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВФормуУведомлениеЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	ПолучитьСворачиваемыеЭлементы();
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Доступность = Ложь;
	РезультатВыполнения = ЗаполнитьАвтоНаСервере();
	Если НЕ РезультатВыполнения.ЗаданиеВыполнено Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершениеПослеЗаполнения", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется заполнение по данным информационной базы'");
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоЗавершениеПослеЗаполнения(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	Доступность = Истина;
	
	Если РезультатЗагрузки = Неопределено
		ИЛИ РезультатЗагрузки.Статус <> "Выполнено" Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось выполнить автозаполнение...'"));
		Возврат;
	КонецЕсли;

	ЗагрузитьПодготовленныеДанные();
КонецПроцедуры

&НаСервере
Функция ЗаполнитьАвтоНаСервере()
	ПредУИД = УИДТекущаяСтраница;
	УИДТекущаяСтраница = ДеревоСтраниц.ПолучитьЭлементы()[0].УИД;
	ТекущееИДНаименования = "Титульная";
	СобратьДанныеМногострочныхЧастейТекущейСтраницы(ТекущиеМногострочныеЧасти, ПредУИД);
	ПоказатьТекущуюСтраницу("Титульная", ДеревоСтраниц.ПолучитьЭлементы()[0].МногострочныеЧасти, Неопределено);
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация", Объект.Организация);
	ПараметрыОтчета.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыОтчета.Вставить("ВидУведомления", Объект.ВидУведомления);
	ПараметрыОтчета.Вставить("РегистрацияВИФНС", Объект.РегистрацияВИФНС);
	ПараметрыОтчета.Вставить("ДатаПодписи", Объект.ДатаПодписи);
	Контейнер = Новый Структура();
	
	ОТУИД = Новый ОписаниеТипов("УникальныйИдентификатор");
	Для Каждого КЗ Из ДанныеМногостраничныхРазделов Цикл 
		ДанныеСтраницы = ОбщегоНазначения.СкопироватьРекурсивно(КЗ.Значение[0].Значение);
		ТЗСтр = Новый ТаблицаЗначений;
		Для Каждого Кол Из ДанныеСтраницы Цикл 
			ТЗСтр.Колонки.Добавить(Кол.Ключ, ?(ТипЗнч(Кол.Значение) = Тип("УникальныйИдентификатор"), ОТУИД, Неопределено))
		КонецЦикла;
		Контейнер.Вставить(КЗ.Ключ, ТЗСтр);
	КонецЦикла;
	
	Контейнер.Вставить("РегистрацияВИФНС", Объект.РегистрацияВИФНС);
	Контейнер.Вставить("ДатаПодписи", Объект.ДатаПодписи);
	Контейнер.Вставить("ДанныеДопСтрок", ПолучитьИзВременногоХранилища(ДанныеДопСтрок.МнгСтр));
	
	ЭтаФормаИмя = ЭтаФорма.ИмяФормы;
	Если СтрЧислоВхождений(ЭтаФормаИмя, "ВнешнийОтчет.") > 0 Тогда
		ЭтаФормаИмя = СтрЗаменить(ЭтаФормаИмя, "ВнешнийОтчет.", "");
	ИначеЕсли СтрЧислоВхождений(ЭтаФормаИмя, "Отчет.") > 0 Тогда
		ЭтаФормаИмя = СтрЗаменить(ЭтаФормаИмя, "Отчет.", "");
	КонецЕсли;
	ИДОтчета = Лев(ЭтаФормаИмя, СтрНайти(ЭтаФормаИмя, ".Форма.") - 1);
	ИДРедакцииОтчета = Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") + 7);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ЗапуститьВФоне = Истина;
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне("РегламентированнаяОтчетность.ЗаполнитьОтчетВФоне_Уведомления",
		Новый Структура("ИДОтчета, ИДРедакцииОтчета, ПараметрыОтчета, Контейнер", ИДОтчета, ИДРедакцииОтчета, ПараметрыОтчета, Контейнер),
		ПараметрыВыполненияВФоне);
	
	РезультатВыполнения.Вставить("ЗаданиеВыполнено", НРег(РезультатВыполнения.Статус) = "выполнено");
	АдресХранилища       = РезультатВыполнения.АдресРезультата;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанные();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
КонецФункции

&НаСервере
Функция НайтиКорневойЭлементПоИД(Элементы, Имя)
	Для Каждого ЭлтДерева Из Элементы Цикл 
		Если ЭлтДерева.ИДНаименования = Имя Тогда 
			Возврат ЭлтДерева.ПолучитьРодителя();
		КонецЕсли;
		РезультатВнутреннегоПоиска = НайтиКорневойЭлементПоИД(ЭлтДерева.ПолучитьЭлементы(), Имя);
		Если РезультатВнутреннегоПоиска <> Неопределено Тогда 
			Возврат РезультатВнутреннегоПоиска;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные()
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	СтруктураДанных.Контейнер.Свойство("РегистрацияВИФНС", Объект.РегистрацияВИФНС);
	СтруктураДанных.Контейнер.Свойство("ДатаПодписи", Объект.ДатаПодписи);
	ДанныеУведомления.Титульная["КодНО"] = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РегистрацияВИФНС, "Код");
	ДанныеУведомления.Титульная["ДАТА_ПОДПИСИ"] = Объект.ДатаПодписи;
	
	Для Каждого Ключ Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("ЛистА,ЛистБ,ЛистВ", ",") Цикл 
		СтрДанных = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтруктураДанных.Контейнер[Ключ].СкопироватьКолонки().Добавить());
		ДанныеМногостраничныхРазделов[Ключ].Очистить();
		
		Корень = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), Ключ);
		
		Для Каждого Стр Из СтруктураДанных.Контейнер[Ключ] Цикл 
			Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтрДанных);
			ЗаполнитьЗначенияСвойств(Данные, Стр);
			ДанныеМногостраничныхРазделов[Ключ].Добавить(Данные);
		КонецЦикла;
		
		Если ДанныеМногостраничныхРазделов[Ключ].Количество() = 0 Тогда 
			СтрДанных.УИД = Новый УникальныйИдентификатор;
			ДанныеМногостраничныхРазделов[Ключ].Добавить(СтрДанных);
		КонецЕсли;
		
		Пока Корень.ПолучитьЭлементы().Количество() < ДанныеМногостраничныхРазделов[Ключ].Количество() Цикл 
			ЗаполнитьЗначенияСвойств(Корень.ПолучитьЭлементы().Добавить(), Корень.ПолучитьЭлементы()[0]);
		КонецЦикла;
		Пока Корень.ПолучитьЭлементы().Количество() > ДанныеМногостраничныхРазделов[Ключ].Количество() Цикл 
			Корень.ПолучитьЭлементы().Удалить(0);
		КонецЦикла;
		Для Инд = 1 По ДанныеМногостраничныхРазделов[Ключ].Количество() Цикл 
			Элт = Корень.ПолучитьЭлементы()[Инд - 1];
			Элт.УИД = ДанныеМногостраничныхРазделов[Ключ][Инд - 1].Значение.УИД;
			Элт.Наименование = "Стр. " + Формат(Инд, "ЧГ=");
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого Ключ Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("ЛистГ,ЛистГ1,ЛистГ2", ",") Цикл
		Корень = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), Ключ);
		Пока Корень.ПолучитьЭлементы().Количество() > 1 Цикл 
			Корень.ПолучитьЭлементы().Удалить(0);
		КонецЦикла;
	КонецЦикла;
	
	КореньГ = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), "ЛистГ");
	КореньГ1 = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), "ЛистГ1");
	КореньГ2 = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), "ЛистГ2");
	ПрототипСтрокиГ = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), "ЛистГ").ПолучитьЭлементы()[0];
	ПрототипСтрокиГ1 = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), "ЛистГ1").ПолучитьЭлементы()[0];
	ПрототипСтрокиГ2 = НайтиКорневойЭлементПоИД(ДеревоСтраниц.ПолучитьЭлементы(), "ЛистГ2").ПолучитьЭлементы()[0];
	ПрототипДанныхГ = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтруктураДанных.Контейнер["ЛистГ"].СкопироватьКолонки().Добавить());
	ПрототипДанныхГ1 = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтруктураДанных.Контейнер["ЛистГ1"].СкопироватьКолонки().Добавить());
	ПрототипДанныхГ2 = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтруктураДанных.Контейнер["ЛистГ2"].СкопироватьКолонки().Добавить());
	
	ДанныеМногостраничныхРазделов["ЛистГ"].Очистить();
	ДанныеМногостраничныхРазделов["ЛистГ1"].Очистить();
	ДанныеМногостраничныхРазделов["ЛистГ2"].Очистить();
	
	СтруктураОтбора = Новый Структура("УИДРодителя");
	Для Каждого Стр Из СтруктураДанных.Контейнер["ЛистГ"] Цикл 
		НовСтрГ = КореньГ.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрГ, ПрототипСтрокиГ);
		НовСтрГ.УИД = Стр.УИД;
		Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ);
		ЗаполнитьЗначенияСвойств(Данные, Стр);
		Данные.УИД = Стр.УИД;
		ДанныеМногостраничныхРазделов["ЛистГ"].Добавить(Данные);
		
		СтруктураОтбора.УИДРодителя = Стр.УИД;
		ОтбСтрокиГ1 = СтруктураДанных.Контейнер["ЛистГ1"].НайтиСтроки(СтруктураОтбора);
		НовыйГ1Корень = НовСтрГ.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НовыйГ1Корень, КореньГ1);
		Если ОтбСтрокиГ1.Количество() > 0 Тогда 
			Для Каждого СтрПодч Из ОтбСтрокиГ1 Цикл 
				НовСтр = НовыйГ1Корень.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ПрототипСтрокиГ1);
				НовСтр.УИД = Новый УникальныйИдентификатор;
				
				Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ1);
				ЗаполнитьЗначенияСвойств(Данные, СтрПодч);
				Данные.УИД = НовСтр.УИД;
				Данные.УИДРодителя = Стр.УИД;
				ДанныеМногостраничныхРазделов["ЛистГ1"].Добавить(Данные);
			КонецЦикла;
		Иначе
			НовСтр = НовыйГ1Корень.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ПрототипСтрокиГ1);
			НовСтр.УИД = Новый УникальныйИдентификатор;
			
			Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ1);
			Данные.УИД = НовСтр.УИД;
			Данные.УИДРодителя = Стр.УИД;
			ДанныеМногостраничныхРазделов["ЛистГ1"].Добавить(Данные);
		КонецЕсли;
		
		ОтбСтрокиГ2 = СтруктураДанных.Контейнер["ЛистГ2"].НайтиСтроки(СтруктураОтбора);
		НовыйГ2Корень = НовСтрГ.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НовыйГ2Корень, КореньГ2);
		Если ОтбСтрокиГ2.Количество() > 0 Тогда 
			Для Каждого СтрПодч Из ОтбСтрокиГ2 Цикл 
				НовСтр = НовыйГ2Корень.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ПрототипСтрокиГ2);
				НовСтр.УИД = Новый УникальныйИдентификатор;
				
				Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ2);
				ЗаполнитьЗначенияСвойств(Данные, СтрПодч);
				Данные.УИД = НовСтр.УИД;
				Данные.УИДРодителя = Стр.УИД;
				ДанныеМногостраничныхРазделов["ЛистГ2"].Добавить(Данные);
			КонецЦикла;
		Иначе
			НовСтр = НовыйГ2Корень.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ПрототипСтрокиГ2);
			НовСтр.УИД = Новый УникальныйИдентификатор;
			
			Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ2);
			Данные.УИД = НовСтр.УИД;
			Данные.УИДРодителя = Стр.УИД;
			ДанныеМногостраничныхРазделов["ЛистГ2"].Добавить(Данные);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеМногостраничныхРазделов["ЛистГ"].Количество() = 0 Тогда 
		Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ);
		Данные.УИД = ПрототипСтрокиГ.УИД;
		ДанныеМногостраничныхРазделов["ЛистГ"].Добавить(Данные);
		
		Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ1);
		Данные.УИД = ПрототипСтрокиГ1.УИД;
		Данные.УИДРодителя = ПрототипСтрокиГ.УИД;
		ДанныеМногостраничныхРазделов["ЛистГ1"].Добавить(Данные);
		Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПрототипДанныхГ2);
		Данные.УИД = ПрототипСтрокиГ2.УИД;
		Данные.УИДРодителя = ПрототипСтрокиГ.УИД;
		ДанныеМногостраничныхРазделов["ЛистГ2"].Добавить(Данные);
	Иначе
		КореньГ.ПолучитьЭлементы().Удалить(ПрототипСтрокиГ);
	КонецЕсли;
	
	Инд0 = 0;
	Для Каждого Стр0 Из КореньГ.ПолучитьЭлементы() Цикл 
		Инд0 = Инд0 + 1;
		Стр0.Наименование = "Стр. " + Формат(Инд0, "ЧГ=");
		
		Инд1 = 0;
		Для Каждого Стр1 Из Стр0.ПолучитьЭлементы()[0].ПолучитьЭлементы() Цикл 
			Инд1 = Инд1 + 1;
			Стр1.Наименование = "Стр. " + Формат(Инд1, "ЧГ=");
		КонецЦикла;
		Инд1 = 0;
		Для Каждого Стр1 Из Стр0.ПолучитьЭлементы()[1].ПолучитьЭлементы() Цикл 
			Инд1 = Инд1 + 1;
			Стр1.Наименование = "Стр. " + Формат(Инд1, "ЧГ=");
		КонецЦикла;
	КонецЦикла;
	
	ДанныеДопСтрок["МнгСтр"] = ПоместитьВоВременноеХранилище(СтруктураДанных.Контейнер.ДанныеДопСтрок, ДанныеДопСтрок["МнгСтр"]);
	
	Модифицированность = Истина;
	Доступность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНаОсновании502(Команда)
	ДополнительныеПараметры = Новый Структура("ИмяФормы", "Форма2019_1");
	ПоказатьВопрос(Новый ОписаниеОповещения("ПеренестиДанныеИзПрошлойРедакцииПродолжение", ЭтотОбъект, ДополнительныеПараметры),
		НСтр("ru='Текущий отчет будет очищен. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаСервере
Функция ЗагрузитьФормат502(Ссылка)
	Попытка
		ПредставлениеXML = Ссылка.ПолучитьОбъект().ВыгрузитьДокумент()[0].ТестВыгрузки; 
		ЗагрузитьИзXMLНаСервере(Новый Структура("Организация, РегистрацияВНалоговомОргане, ПредставлениеXML", 
								Объект.Организация, Объект.РегистрацияВИФНС, ПредставлениеXML));
								
		ИдДляСвор = УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект);
		СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(ИдДляСвор);
		ТекущееИДНаименования = "Титульная";
		ПоказатьТекущуюСтраницу("Титульная_2021", Новый СписокЗначений, Неопределено);
		Модифицированность = Истина;
		Возврат Истина;
	Исключение
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не удалось получить текст выгрузки отчета редакции от 26.08.2019'"));
	КонецПопытки;
	Возврат Ложь;
КонецФункции