#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет объектный реквизит (приемник) объекту XDTO (источник)
//
// Параметры:
//   Приемник - Произвольный -  объект, содержащий реквизиты или свойства,
//     которые должны быть заполнены по свойству объекта XDTO.
//   Источник - ОбъектXDTO, СвойствоXDTO - объект XDTO или его свойство, по которому будет заполнен объектный реквизит.
//   ИмяРеквизита - Строка - имя объектного реквизита.
//   ЗаполнятьПредставление - Булево - признак необходимости заполнения реквизита представления
//     по свойству objectID.presentation.
//
Процедура ЗаполнитьОбъектныйРеквизит(Приемник, Источник, ИмяРеквизита, ЗаполнятьПредставление = Ложь) Экспорт
	
	Если Источник <> Неопределено Тогда
		
		Приемник[ИмяРеквизита] = Источник.name;
		Приемник[ИмяРеквизита + "ID"] = Источник.objectID.ID;
		Приемник[ИмяРеквизита + "Тип"] = Источник.objectID.type;
		
		// Старые версии сервиса не заполняют представление.
		Если ЗаполнятьПредставление Тогда
			Если Источник.objectID.Свойства().Получить("presentation") <> Неопределено
				И Источник.objectID.Установлено("presentation") Тогда
				Приемник[ИмяРеквизита + "Представление"] = Источник.objectID.presentation;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает значение навигационной ссылки формы на объект Документооборота
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма объекта 1С:Документооборота.
//   ОбъектXDTO - ОбъектXDTO, СвойствоXDTO - объект XDTO или его свойство,
//     по которому будет заполнена навигационная ссылка.
//
Процедура УстановитьНавигационнуюСсылку(Форма, ОбъектXDTO) Экспорт
	
	// навигационная ссылка в objectID
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3") Тогда
		Возврат;
	КонецЕсли;
	
	АдресСервера = Константы.АдресВебСервиса1СДокументооборот.Получить();
	Форма.АвтоНавигационнаяСсылка = Ложь;
	Форма.НавигационнаяСсылка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1#%2",
			АдресСервера, ОбъектXDTO.objectID.navigationRef);
			
КонецПроцедуры

// Устанавливает значения реквизитов состояний в форме документа Документооборота
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма документа 1С:Документооборота
//   ОбъектXDTO - ОбъектXDTO - документ, по которому будут заполнены состояния
//
Процедура УстановитьСостоянияДокумента(Форма, ОбъектXDTO) Экспорт
	
	// независимые состояния документа
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		Возврат
	КонецЕсли;
	
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.statusRegistration, "СостояниеРегистрация");
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.statusConsideration, "СостояниеРассмотрение");
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.statusApproval, "СостояниеСогласование");
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.statusConfirmation, "СостояниеУтверждение");
	Если Форма.Тип = "DMInternalDocument"
			И ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.statusSigning, "СостояниеПодписание");
	КонецЕсли;
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.statusPerformance, "СостояниеИсполнение");
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("СостояниеСогласование");
	Реквизиты.Вставить("СостояниеУтверждение");
	Если Форма.Тип = "DMInternalDocument" Тогда
		Реквизиты.Вставить("СостояниеПодписание");
	КонецЕсли;
	Реквизиты.Вставить("СостояниеРегистрация");
	Реквизиты.Вставить("СостояниеРассмотрение");
	Реквизиты.Вставить("СостояниеИсполнение");
	Форма.СостояниеСписок.Очистить();
	Для Каждого Реквизит Из Реквизиты Цикл
		ИмяРеквизита = Реквизит.Ключ;
		Если ЗначениеЗаполнено(Форма[ИмяРеквизита + "ID"]) Тогда
			Форма.СостояниеСписок.Добавить(Форма[ИмяРеквизита + "ID"], Форма[ИмяРеквизита]);
		КонецЕсли;
	КонецЦикла;
	Если Форма.СостояниеСписок.Количество() > 1 Тогда
		Для Каждого Элемент Из Форма.СостояниеСписок Цикл
			Если Элемент.Значение = "Проект" Тогда
				Форма.СостояниеСписок.Удалить(Элемент);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает заголовок документа в зависимости от типа интегрированного объекта.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма документа 1С:Документооборота:
//     * Наименование - Строка
//
Процедура УстановитьЗаголовокДокумента(Форма) Экспорт
	
	ПредставлениеТипа = "";
	Если Форма.Тип = "DMInternalDocument" Тогда
		ПредставлениеТипа = НСтр("ru='Внутренний документ'");
	ИначеЕсли Форма.Тип = "DMIncomingDocument" Тогда
		ПредставлениеТипа = НСтр("ru='Входящий документ'");
	ИначеЕсли Форма.Тип = "DMOutgoingDocument" Тогда
		ПредставлениеТипа = НСтр("ru='Исходящий документ'");
	КонецЕсли;
	
	ПредставлениеДокумента = "";
	Если ЗначениеЗаполнено(Форма.ВнешнийОбъект) Тогда
		Если ОбщегоНазначения.ЭтоДокумент(Форма.ВнешнийОбъект.Метаданные()) Тогда
			ПредставлениеДокумента = Форма.Наименование;
		КонецЕсли;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ПредставлениеДокумента) И ЗначениеЗаполнено(Форма.Представление) Тогда
		ПредставлениеДокумента = Форма.Представление;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Форма.ID) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (создание)'"), ПредставлениеТипа);
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)'"), ПредставлениеДокумента, ПредставлениеТипа);
	КонецЕсли;
		
	Форма.Заголовок = Заголовок;
	
КонецПроцедуры

// Устанавливает свойства элементов хронометража формы
//
Процедура УстановитьСвойстваЭлементовХронометража(
		ВключенХронометраж,
		КомандаПереключитьХронометраж,
		ЭлементПереключитьХронометраж) Экспорт
	
	Если ВключенХронометраж Тогда // хронометраж включен
		КомандаПереключитьХронометраж.Подсказка = НСтр("ru = 'Закончить хронометраж'");
		ЭлементПереключитьХронометраж.Пометка = Истина;
	Иначе
		КомандаПереключитьХронометраж.Подсказка = НСтр("ru = 'Включить хронометраж'");
		ЭлементПереключитьХронометраж.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметры хронометража объекта Документооборота
//
Процедура ЗаполнитьПараметрыХронометража(Форма, ОбъектXDTO) Экспорт
	
	// хронометраж
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMGetChronometrationSettingsResponse")
				Или ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMSetChronometrationSettingsResponse") Тогда
			Параметры = ОбъектXDTO.settings[0];
		Иначе
			Параметры = ОбъектXDTO.chronometrationSettings;
		КонецЕсли;
		Форма.ВключенХронометраж = Параметры.chronometrationOn;
		Форма.ДатаНачалаХронометража = Параметры.beginDate;
		Форма.ДатаКонцаХронометража = Параметры.endDate;
		УстановитьСвойстваЭлементовХронометража(Форма.ВключенХронометраж,
			Форма.Команды.ПереключитьХронометраж, Форма.Элементы.ФормаПереключитьХронометраж);
	КонецЕсли;
	
КонецПроцедуры

// Инвертирует значение хронометража
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//
Процедура ПереключитьХронометраж(Форма) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSetChronometrationSettingsRequest");
	ОбъектID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, Форма.ID, Форма.Тип);
	СписокОбъектов = Запрос.objects; // СписокXDTO
	СписокОбъектов.Добавить(ОбъектID);
	
	Результат = Прокси.execute(Запрос);
	
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	ЗаполнитьПараметрыХронометража(Форма, Результат);
	
КонецПроцедуры

// Помещает набор дополнительных реквизитов объекта на форму объекта
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - управляемая форма документа 1С:Документооборота
//   ОбъектXDTO - ОбъектXDTO - объект, из которого заполняются реквизиты формы
//
Процедура ПоместитьДополнительныеРеквизитыНаФорму(Форма, ОбъектXDTO) Экспорт
	
	ЕстьСвойства =
		(ОбъектXDTO.Свойства().Получить("additionalProperties") <> Неопределено)
		И (ОбъектXDTO.additionalProperties.Количество() > 0);
	
	// На некоторых формах для свойств выделена отдельная страница.
	Если Форма.Элементы.Найти("СтраницаСвойства") = Неопределено Тогда
		Форма.Элементы.ГруппаСвойства.Видимость = ЕстьСвойства;
	Иначе
		Форма.Элементы.СтраницаСвойства.Видимость = ЕстьСвойства;
	КонецЕсли;
	
	СтарыеЗначения = Форма.Свойства.Выгрузить();
	Форма.Свойства.Очистить();
	
	Если Не ЕстьСвойства Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ДопСвойство Из ОбъектXDTO.additionalProperties Цикл
		
		Если Не ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ДопСвойство, "propertyValueTypes") Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Форма.Свойства.Добавить();
		СписокДоступныхТипов = НоваяСтрока.СписокДоступныхТипов; // СписокЗначений
		
		НоваяСтрока.Свойство = ДопСвойство.name;
		НоваяСтрока.СвойствоТип = ДопСвойство.objectID.type;
		НоваяСтрока.СвойствоID = ДопСвойство.objectID.ID;
		
		Если ДопСвойство.Свойства().Получить("mandatory") <> Неопределено Тогда
			НоваяСтрока.ЗаполнятьОбязательно = ДопСвойство.mandatory;
		КонецЕсли;
		
		Если ДопСвойство.Установлено("propertySimpleValue") Тогда
			НоваяСтрока.Значение = ДопСвойство.propertySimpleValue;
		ИначеЕсли ДопСвойство.Установлено("propertyObjectValue") Тогда
			НоваяСтрока.Значение = ДопСвойство.propertyObjectValue.name;
			НоваяСтрока.ЗначениеТип = ДопСвойство.propertyObjectValue.objectID.type;
			НоваяСтрока.ЗначениеID = ДопСвойство.propertyObjectValue.objectID.ID;
		Иначе
			ПараметрыОтбора = Новый Структура("СвойствоТип, СвойствоID",
				НоваяСтрока.СвойствоТип,
				НоваяСтрока.СвойствоID);
			МассивСтрокСтарыеЗначения = СтарыеЗначения.НайтиСтроки(ПараметрыОтбора);
			Если МассивСтрокСтарыеЗначения.Количество() = 1 Тогда
				ЗаполнитьЗначенияСвойств(
					НоваяСтрока,
					МассивСтрокСтарыеЗначения[0],
					"Значение, ЗначениеТип, ЗначениеID");
			Иначе
				НоваяСтрока.Значение = "";
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого ОписаниеТипа Из ДопСвойство.PropertyValueTypes Цикл
			ДанныеОТипе = Новый Структура("xdtoClassName, presentation");
			ДанныеОТипе.xdtoClassName = ОписаниеТипа.xdtoClassName;
			ДанныеОТипе.presentation = ОписаниеТипа.presentation;
			СписокДоступныхТипов.Добавить(ДанныеОТипе);
		КонецЦикла;
		
	КонецЦикла;
	
	Форма.Элементы.Свойства.ВысотаВСтрокахТаблицы = ОбъектXDTO.additionalProperties.Количество();
	
КонецПроцедуры

// Получает набор дополнительных реквизитов объекта и помещает его на форму объекта.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Форма - ФормаКлиентскогоПриложения - форма объекта 1С:Документооборота:
//     * Наименование - Строка
//
Процедура ПолучитьДополнительныеРеквизитыИПоместитьНаФорму(Прокси, Форма) Экспорт
	
	Объект = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, Форма.Тип);
	Объект.name = Форма.Наименование;
	Объект.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
	Объект.objectID.ID = Форма.ID;
	Объект.objectID.type = Форма.Тип;
	
	Если Найти(Форма.Тип, "Document") <> 0 Тогда
		Если ЗначениеЗаполнено(Форма.ВидДокумента) Тогда
			Объект.documentType = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, Форма.Тип + "Type");
			Объект.documentType.name = Форма.ВидДокумента;
			Объект.documentType.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
			Объект.documentType.objectID.ID = Форма.ВидДокументаID;
			Объект.documentType.objectID.type = Форма.ВидДокументаТип;
		КонецЕсли;
		
	ИначеЕсли Форма.Тип = "DMCorrespondent" Тогда
		Если ЗначениеЗаполнено(Форма.ЮрФизЛицо) Тогда
			Объект.legalPrivatePerson = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMLegalPrivatePerson");
			Объект.legalPrivatePerson.name = Форма.ЮрФизЛицо;
			Объект.legalPrivatePerson.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
			Объект.legalPrivatePerson.objectID.ID = Форма.ЮрФизЛицоID;
			Объект.legalPrivatePerson.objectID.type = Форма.ЮрФизЛицоТип;
		КонецЕсли;
		
	КонецЕсли;
		
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectAdditionalPropertiesRequest");
	Запрос.object = Объект;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ПоместитьДополнительныеРеквизитыНаФорму(Форма, Результат);
	
КонецПроцедуры

// Заполняет дерево связей в форме по объекту XDTO и начинает асинхронное получение файлов.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - заполняемая форма обработки ИнтеграцияС1СДокументооборот.
//   ОбъектXDTO - ОбъектXDTO - объект, из которого заполняются реквизиты формы.
//   ЭлементДляОбновленияЗаголовка - ПолеФормы, ГруппаФормы - элемент, чей заголовок обновляется.
//   ДобавленныйДокумент - Строка - идентификатор документа, файлы которого следует обновить, или
//                       - Неопределено - признак необходимости обновить все файлы.
//
// Возвращаемое значение:
//   Строка - адрес в хранилище, куда будут помещены сведения о файлах.
//
Функция ЗаполнитьДеревоСвязейИНачатьПолучениеФайлов(Форма, ОбъектXDTO,
		ЭлементДляОбновленияЗаголовка = Неопределено, ДобавленныйДокумент = Неопределено) Экспорт
	
	Если ОбъектXDTO.Свойства().Получить("relations") = Неопределено
			Или Не ОбъектXDTO.Установлено("relations") Тогда
		
		ЭлементыДерева = Форма.ДеревоСвязей.ПолучитьЭлементы();
		ЭлементыДерева.Очистить();
		
		Если ЭлементДляОбновленияЗаголовка <> Неопределено Тогда
			ЭлементДляОбновленияЗаголовка.Заголовок = НСтр("ru = 'Связи'");
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Дерево = Форма.РеквизитФормыВЗначение("ДеревоСвязей"); // ДеревоЗначений
	
	// Сохраним файлы текущих документов.
	КэшФайлов = Новый Соответствие;
	Для Каждого СтрокаТип Из Дерево.Строки Цикл
		Для Каждого СтрокаДокумент Из СтрокаТип.Строки Цикл
			ФайлыДокумента = Новый Массив;
			Для Каждого СтрокаФайл Из СтрокаДокумент.Строки Цикл
				Файл = Новый Структура;
				Файл.Вставить("ID", СтрокаФайл.ID);
				Файл.Вставить("Тип", СтрокаФайл.Тип);
				Файл.Вставить("Заголовок", СтрокаФайл.Заголовок);
				Файл.Вставить("Расширение", СтрокаФайл.Расширение);
				Файл.Вставить("Картинка", СтрокаФайл.Картинка);
				ФайлыДокумента.Добавить(Файл);
			КонецЦикла;
			Если ФайлыДокумента.Количество() > 0 Тогда
				КэшФайлов.Вставить(СтрокаДокумент.ID, ФайлыДокумента);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Дерево.Строки.Очистить();
	
	ЗаполнятьАвансовыйОтчет =
		(ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.28.12.CORP")
		И Форма.ИмяФормы = "Обработка.ИнтеграцияС1СДокументооборот.Форма.ВнутреннийДокумент"
		И Форма.ЯвляетсяЗаявкойНаОплату);
	
	СвязанныеДокументы = Новый Массив;
	
	Для Каждого Связь Из ОбъектXDTO.relations Цикл
		
		// Найдем строку связи по типу или создадим ее.
		НайденнаяСтрока = Дерево.Строки.Найти(Связь.relationType.name, "Заголовок", Ложь);
		Если НайденнаяСтрока <> Неопределено Тогда 
			СтрокаТипСвязи = НайденнаяСтрока;
		Иначе
			СтрокаТипСвязи = Дерево.Строки.Добавить();
			СтрокаТипСвязи.Заголовок = Связь.relationType.name;
			СтрокаТипСвязи.ID = Связь.relationType.objectID.ID;
			СтрокаТипСвязи.Тип = Связь.relationType.objectID.type;
		КонецЕсли;
		
		// Добавим строку для документа.
		СтрокаДокумент = СтрокаТипСвязи.Строки.Добавить();
		Если ЗначениеЗаполнено(Связь.relatedDocument.regNumber)
			И ЗначениеЗаполнено(Связь.relatedDocument.regDate) Тогда
			СтрокаДокумент.Заголовок = СтроковыеФункцииКлиентСервер.
				ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (№ %2 от %3)'"),
					Связь.relatedDocument.title,
					Связь.relatedDocument.regNumber,
					Связь.relatedDocument.regDate);
		Иначе
			СтрокаДокумент.Заголовок = Связь.relatedDocument.title;
		КонецЕсли;
		СтрокаДокумент.Тип = Связь.relatedDocument.objectID.type;
		СтрокаДокумент.ID = Связь.relatedDocument.objectID.ID;
		СтрокаДокумент.РегистрационныйНомер = Связь.relatedDocument.regNumber;
		СтрокаДокумент.ДатаРегистрации = Связь.relatedDocument.regDate;
		
		ФайлыДокумента = КэшФайлов.Получить(СтрокаДокумент.ID);
		Если ФайлыДокумента <> Неопределено Тогда
			Для Каждого Файл Из ФайлыДокумента Цикл
				СтрокаФайл = СтрокаДокумент.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаФайл, Файл);
			КонецЦикла;
		КонецЕсли;
		
		// Подготовим параметры для асинхронного получения файлов.
		Если ДобавленныйДокумент = Неопределено
			Или ДобавленныйДокумент = Связь.relatedDocument.objectID.ID Тогда
			СвязанныйДокумент = Новый Структура;
			СвязанныйДокумент.Вставить("ID", Связь.relatedDocument.objectID.ID);
			СвязанныйДокумент.Вставить("Тип", Связь.relatedDocument.objectID.type);
			СвязанныеДокументы.Добавить(СвязанныйДокумент);
		КонецЕсли;
		
		Если ЗаполнятьАвансовыйОтчет И Связь.relationType.predefinedName = "АвансовыйОтчет" Тогда //@NON-NLS-1
			Форма.АвансовыйОтчет = Связь.relatedDocument.name;
			Форма.АвансовыйОтчетID = Связь.relatedDocument.objectID.ID;
			Форма.АвансовыйОтчетТип = Связь.relatedDocument.objectID.type;
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.ЗначениеВРеквизитФормы(Дерево, "ДеревоСвязей");
	
	Если ЭлементДляОбновленияЗаголовка <> Неопределено Тогда
		Если ОбъектXDTO.relations.Количество() = 0 Тогда 
			ТекстЗаголовка = НСтр("ru = 'Связи'");
		Иначе
			ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Связи (%1)'"),
				ОбъектXDTO.relations.Количество());
		КонецЕсли;
		ЭлементДляОбновленияЗаголовка.Заголовок = ТекстЗаголовка;
	КонецЕсли;
	
	Если СвязанныеДокументы.Количество() > 0 Тогда
		Возврат ИнтеграцияС1СДокументооборот.ПолучитьФайлыСвязанныхДокументов(
			СвязанныеДокументы);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Получает описания файлов по списку связанных документов-владельцев.
//
// Параметры:
//   СвязанныеДокументы - Массив из Структура:
//     * ID - Строка - идентификатор связанного документа.
//     * Тип - Строка - тип связанного документа.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * ID - Строка - идентификатор связанного документа.
//     * Тип - Строка - тип связанного документа.
//     * Файлы - Массив из Структура:
//         ** ID - Строка - идентификатор файла.
//         ** Тип - Строка - тип файла (DMFile).
//         ** Наименование - Строка - имя без расширения.
//         ** Расширение - Строка - расширение файла.
//
Функция ПолучитьФайлыСвязанныхДокументов(СвязанныеДокументы) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборот.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	ПолучаемыеСвойства = Запрос.columnSet; // СписокXDTO
	СписокОбъектов = Запрос.objectIDs; // СписокXDTO
	ПолучаемыеСвойства.Добавить("files");
	
	Для Каждого СвязанныйДокумент Из СвязанныеДокументы Цикл
		ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
			СвязанныйДокумент.ID, СвязанныйДокумент.Тип);
		СписокОбъектов.Добавить(ОбъектИд);
	КонецЦикла;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Результат = Новый Массив;
	
	Для Каждого ВладелецXDTO Из Ответ.objects Цикл
		
		Владелец = Новый Структура;
		Владелец.Вставить("ID", ВладелецXDTO.objectID.ID);
		Владелец.Вставить("Тип", ВладелецXDTO.objectID.type);
		Владелец.Вставить("Файлы", Новый Массив);
		
		Для Каждого ФайлXDTO Из ВладелецXDTO.files Цикл
			
			Файл = Новый Структура;
			Файл.Вставить("ID", ФайлXDTO.objectID.ID);
			Файл.Вставить("Тип", ФайлXDTO.objectID.type);
			Файл.Вставить("Наименование", ФайлXDTO.name);
			Файл.Вставить("Расширение", ФайлXDTO.extension);
			
			Владелец.Файлы.Добавить(Файл);
			
		КонецЦикла;
		
		Результат.Добавить(Владелец);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Заполняет дерево связей автоматически по ссылкам в объекте интегрируемой конфигурации.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - заполняемая форма обработки ИнтеграцияС1СДокументооборот.
//   ОбъектИС - ЛюбаяСсылка - объект, на основании которого заполняется объект ДО.
//   ЭлементДляОбновленияЗаголовка - ПолеФормы, ГруппаФормы - элемент, чей заголовок обновляется.
//
Процедура ЗаполнитьСвязиНовогоДокумента(Форма, ОбъектИС, ЭлементДляОбновленияЗаголовка) Экспорт
	
	ПодходящиеОбъекты = ИнтеграцияС1СДокументооборот.ПолучитьПодходящиеОбъектыДляДобавленияСвязей(ОбъектИС);
	
	Если ПодходящиеОбъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВидДокумента = Новый Структура("ID, Тип",
		Форма.ВидДокументаID,
		Форма.ВидДокументаТип);
	
	Дерево = Форма.РеквизитФормыВЗначение("ДеревоСвязей");
	КоличествоСвязей = 0;
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Для Каждого ПодходящийОбъект Из ПодходящиеОбъекты Цикл
		
		СвязываемыйДокумент = Новый Структура("Ссылка, Заголовок",
			ПодходящийОбъект,
			Строка(ПодходящийОбъект));
			
		Попытка
			ДанныеОбъектаДО = ИнтеграцияС1СДокументооборотВызовСервера.
				ДанныеОбъектаДОПоВнешнемуОбъекту(ПодходящийОбъект);
		Исключение
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось получить связанный объект Документооборота при автоматическом создании связей: %1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			Продолжить;
		КонецПопытки;
			
		Если ДанныеОбъектаДО = Неопределено Тогда
			
			Правила = ИнтеграцияС1СДокументооборотВызовСервера.ПодходящиеПравила(ПодходящийОбъект);
			Если Правила.Количество() <> 1 Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(Правила[0].ТипОбъектаДО) Тогда
				Продолжить;
			КонецЕсли;
			
			ВидСвязываемогоДокумента = Новый Структура("ID, Тип",
				Правила[0].ИдентификаторВидаДокумента,
				Правила[0].ТипВидаДокумента);
			
			СвязываемыйДокумент.Вставить("Правило", Правила[0].Ссылка);
			
		Иначе // получим связи по виду существующего документа
			
			Если Не ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(ДанныеОбъектаДО.type) Тогда
				Продолжить;
			КонецЕсли;
			
			ВидСвязываемогоДокумента = Новый Структура("ID, Тип",
				ДанныеОбъектаДО.documentType.ID,
				ДанныеОбъектаДО.documentType.type);
			
			СвязываемыйДокумент.Вставить("ID", ДанныеОбъектаДО.ID);
			СвязываемыйДокумент.Вставить("Тип", ДанныеОбъектаДО.type);
			
		КонецЕсли;
		
		Условия = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
		СписокУсловийОтбора = Условия.conditions; // СписокXDTO
		
		УсловиеИз = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		УсловиеИз.property = "DocumentFrom";
		УсловиеИз.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(
			Прокси, ВидДокумента.ID, ВидДокумента.Тип);
		СписокУсловийОтбора.Добавить(УсловиеИз);
		
		УсловиеВ = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		УсловиеВ.property = "DocumentTo";
		УсловиеВ.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(
			Прокси, ВидСвязываемогоДокумента.ID, ВидСвязываемогоДокумента.Тип);
		СписокУсловийОтбора.Добавить(УсловиеВ);
		
		УсловиеПредопределенная = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		УсловиеПредопределенная.property = "Predefined";
		УсловиеПредопределенная.value = Ложь;
		СписокУсловийОтбора.Добавить(УсловиеПредопределенная);
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
		Запрос.type = "DMRelationType";
		Запрос.query = Условия;
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		Если Результат.items.Количество() > 1 Тогда
			Продолжить;
		ИначеЕсли Результат.items.Количество() = 1 Тогда
			ТипСвязиXDTO = Результат.items[0].object;
			ТипСвязи = Новый Структура("ID, Тип, Заголовок",
				ТипСвязиXDTO.objectID.ID,
				ТипСвязиXDTO.objectID.type,
				ТипСвязиXDTO.name);
		Иначе // предопределенный
			ТипСвязи = Новый Структура("ID, Тип, Заголовок",
				"",
				"DMRelationType",
				НСтр("ru = 'Содержит ссылку на'"));
		КонецЕсли;
			
		ПодходящиеСтрокиДерева = Дерево.Строки.НайтиСтроки(ТипСвязи);
		Если ПодходящиеСтрокиДерева.Количество() = 0 Тогда
			СтрокаТипа = Дерево.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТипа, ТипСвязи);
		Иначе
			СтрокаТипа = ПодходящиеСтрокиДерева[0];
		КонецЕсли;
		
		ПодходящиеСтрокиДерева = СтрокаТипа.Строки.НайтиСтроки(СвязываемыйДокумент);
		Если ПодходящиеСтрокиДерева.Количество() = 0 Тогда
			СтрокаДокумента = СтрокаТипа.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДокумента, СвязываемыйДокумент);
			СтрокаДокумента.СвязьНовогоДокумента = Истина;
			КоличествоСвязей = КоличествоСвязей + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.ЗначениеВРеквизитФормы(Дерево, "ДеревоСвязей");
	
	Если КоличествоСвязей = 0 Тогда 
		ТекстЗаголовка = НСтр("ru = 'Связи'");
	Иначе
		ТекстЗаголовка = СтрШаблон(
			НСтр("ru = 'Связи (%1)'"),
			КоличествоСвязей);
	КонецЕсли;
	ЭлементДляОбновленияЗаголовка.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

// Создает в ДО связи нового документа, добавленные автоматически при его создании на основании
// объекта ИС, но еще не записанные.
//
// Параметры:
//   ДеревоСвязей - ДанныеФормыДерево - дерево связей в форме документа.
//   ID - Строка - идентификатор документа-владельца.
//   Тип - Строка - тип документа-владельца.
//   Представление - Строка - представление документа-владельца.
//
Процедура ЗаписатьСвязиНовогоДокумента(ДеревоСвязей, ID, Тип, Представление) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ИсходныйДокумент = Новый Структура("ID, Тип, Представление",
		ID,
		Тип,
		Представление);
	
	СтрокиТиповСвязей = ДеревоСвязей.ПолучитьЭлементы();
	Для Каждого СтрокаТипаСвязи Из СтрокиТиповСвязей Цикл
		
		Если ЗначениеЗаполнено(СтрокаТипаСвязи.ID) Тогда
			ТипСвязи = Новый Структура("ID, Тип, Представление",
				СтрокаТипаСвязи.ID,
				СтрокаТипаСвязи.Тип,
				СтрокаТипаСвязи.Заголовок);
		Иначе
			ТипСвязи = Неопределено;
		КонецЕсли;
		
		СтрокиДокументов = СтрокаТипаСвязи.ПолучитьЭлементы();
		Для Каждого СтрокаДокумента Из СтрокиДокументов Цикл
			
			Если Не СтрокаДокумента.СвязьНовогоДокумента Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ID) Тогда
				
				СвязываемыйДокумент = Новый Структура("ID, Тип, Представление",
					СтрокаДокумента.ID,
					СтрокаДокумента.Тип,
					СтрокаДокумента.Заголовок);
				
			Иначе
				
				НовыйОбъектДО = ИнтеграцияС1СДокументооборот.СоздатьОбъектДОПоПравилу(Прокси,
					СтрокаДокумента.Ссылка,
					СтрокаДокумента.Правило);
				
				Если ТипЗнч(НовыйОбъектДО) = Тип("ОбъектXDTO") Тогда
					
					СвязываемыйДокумент = Новый Структура("ID, Тип, Представление",
						НовыйОбъектДО.objectID.ID,
						НовыйОбъектДО.objectID.type,
						НовыйОбъектДО.name);
					
				Иначе // сообщение об ошибке
					
					ИмяСобытия = НСтр("ru = 'Автоматическое создание связанного объекта'");
					ЗаписьЖурналаРегистрации(
						ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(ИмяСобытия), 
						УровеньЖурналаРегистрации.Ошибка,,,
						НовыйОбъектДО);
					
					ТекстИсключения = СтрШаблон(НСтр("ru = 'Не удалось создать связанный документ для ""%1"":
						|%2'"),
						СтрокаДокумента.Заголовок,
						НовыйОбъектДО);
					ВызватьИсключение ТекстИсключения;
				
				КонецЕсли;
				
			КонецЕсли;
			
			ИнтеграцияС1СДокументооборотВызовСервера.ДобавитьСвязьДокументов(
				ИсходныйДокумент,
				СвязываемыйДокумент,
				ТипСвязи);
			
			СтрокаДокумента.СвязьНовогоДокумента = Ложь;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Передает в ДО файлы нового документа.
//
// Параметры:
//   Файлы - ДанныеФормыКоллекция - список файлов в форме документа.
//   ID - Строка - идентификатор документа-владельца.
//   Тип - Строка - тип документа-владельца.
//   Представление - Строка - представление документа-владельца.
//   Владелец - ЛюбаяСсылка - документ-владельца.
//
Процедура ЗаписатьФайлыНовогоДокумента(Файлы, ID, Тип, Представление, Владелец) Экспорт
	
	// Передадим в ДО печатные формы, которые могли быть добавлены на стороне ИС до записи документа.
	Для Каждого Файл Из Файлы Цикл
		
		Если ЗначениеЗаполнено(Файл.ID)
			Или Не ЗначениеЗаполнено(Файл.АдресВременногоХранилищаФайла) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыСоздания = ИнтеграцияС1СДокументооборотВызовСервера.НовыеПараметрыСозданияФайла();
		ПараметрыСоздания.Имя = Файл.Наименование;
		ПараметрыСоздания.Расширение = Файл.Расширение;
		ПараметрыСоздания.Размер = Файл.Размер;
		ПараметрыСоздания.АдресВременногоХранилищаФайла = Файл.АдресВременногоХранилищаФайла;
		ПараметрыСоздания.ВремяИзменения = Файл.ДатаСоздания;
		ПараметрыСоздания.ВремяИзмененияУниверсальное = Файл.ДатаМодификацииУниверсальная;
		ПараметрыСоздания.Текст = "";
		ПараметрыСоздания.ЯвляетсяСканКопией = Ложь;
		ПараметрыСоздания.Владелец = Владелец;
		
		Файл.ID = ИнтеграцияС1СДокументооборотВызовСервера.СоздатьИзФайлаНаДискеСервер(
			ПараметрыСоздания,
			ID,
			Тип,
			Представление,
			Ложь); // не пытаться обновить файл в ДО.
		
	КонецЦикла;
	
	// Создадим в ДО файлы по их шаблонам, скопированным из шаблона документа и хранящимся в ДО.
	Для Каждого Файл Из Файлы Цикл
		
		Если ЗначениеЗаполнено(Файл.ID)
			Или Не ЗначениеЗаполнено(Файл.ШаблонID) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыСоздания = Новый Структура;
		ПараметрыСоздания.Вставить("Имя", Файл.Наименование);
		ПараметрыСоздания.Вставить("Расширение", Файл.Расширение);
		ПараметрыСоздания.Вставить("ШаблонID", Файл.ШаблонID);
		
		Файл.ID = ИнтеграцияС1СДокументооборотВызовСервера.СоздатьФайлИзШаблона(
			ПараметрыСоздания,
			ID,
			Тип,
			Представление);
		
	КонецЦикла;
		
КонецПроцедуры

// Выполняет действия при изменении вида документа
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма документа 1С:Документооборота
//   ДанныеОбъектовДО - ОбъектXDTO - XDTO Объект типа DMRetrieveResponse,
//     содержащий данные вида документа и, возможно, организации.
//
Процедура ПриИзмененииВидаНаФормеДокумента(Форма, ДанныеОбъектовДО = Неопределено) Экспорт
	
	organization = Неопределено;
	
	Если ДанныеОбъектовДО = Неопределено Тогда
		documentType = ВидДокументаXDTOПоФормеДокумента(Форма);
	Иначе
		documentType = ДанныеОбъектовДО.objects[0];
		Если ДанныеОбъектовДО.objects.Количество() > 1
				И ДанныеОбъектовДО.objects[1].objectID.type = "DMOrganization" Тогда
			organization = ДанныеОбъектовДО.objects[1];
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПроверитьВозможностьСозданияДокументаНеПоШаблону(Форма, documentType) Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуДокументаСогласноВидуДокументаXDTO(Форма, documentType, organization);
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ПолучитьДополнительныеРеквизитыИПоместитьНаФорму(Прокси, Форма);
	
	Если ЗначениеЗаполнено(Форма.Тип) И ЗначениеЗаполнено(Форма.ID) Тогда
		Колонки = Новый Массив;
		Колонки.Добавить("registrationAvailable");
		Ответ = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси,
			Форма.Тип,
			Форма.ID,
			Колонки);
		ОбъектXDTO = Ответ.objects[0];
		УстановитьДоступностьРегистрации(Форма, ОбъектXDTO);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает видимость кнопки Зарегистрировать на форме документа.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - управляемая форма документа 1С:Документооборота
//   ОбъектXDTO - ОбъектXDTO - объект с данными документа
//
Процедура УстановитьДоступностьРегистрации(Форма, ОбъектXDTO) Экспорт
	
	Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "registrationAvailable") Тогда
		Форма.Элементы.Зарегистрировать.Видимость = ОбъектXDTO.registrationAvailable;
	Иначе
		Форма.Элементы.Зарегистрировать.Видимость = Истина;
	КонецЕсли;
	
	Если Форма.Элементы.Зарегистрировать.Видимость Тогда
		Форма.Элементы.ГруппаРегНомерДата.Подсказка =
			НСтр("ru = 'Для регистрации документа используйте кнопку ""Зарегистрировать"" в командной панели.'");
	Иначе
		Форма.Элементы.ГруппаРегНомерДата.Подсказка = "";
	КонецЕсли;
	
КонецПроцедуры

// Возвращает объект XDTO, соответствующий виду документа, по данным его формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма внутреннего, входящего или исходящего документа.
//
// Возвращаемое значение:
//   ОбъектXDTO
//
Функция ВидДокументаXDTOПоФормеДокумента(Форма) Экспорт
		
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ВидДокументаID = Форма.ВидДокументаID;
	ВидДокументаТип = Форма.ВидДокументаТип;
	
	Если ЗначениеЗаполнено(ВидДокументаID) И ЗначениеЗаполнено(ВидДокументаТип) Тогда
		ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ВидДокументаID, ВидДокументаТип);
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
		СписокОбъектов = Запрос.objectIDs; // СписокXDTO
		СписокОбъектов.Добавить(ОбъектИд);
		
		Результат = Прокси.execute(Запрос);
		Попытка
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		Исключение
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Вид документа ""%1"" не найден в 1С:Документообороте'"),
				Форма.ВидДокумента);
		КонецПопытки;
		
		documentType = Результат.objects[0];
	Иначе
		documentType = Неопределено;
	КонецЕсли;
	
	Возврат documentType;
	
КонецФункции

// Проверяет возможность создания документа указанного вида не по шаблону, возвращая Истина,
// если таковая возможность есть, и формируя сообщение пользователю со сбросом выбранного им 
// вида документа.
//
// Возвращаемое значение:
//   Булево
//
Функция ПроверитьВозможностьСозданияДокументаНеПоШаблону(Форма, documentType) Экспорт
	
	Если documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "templateRequired")
			И documentType.templateRequired
			И Форма.ШаблонID = "" Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = СтрШаблон(
			НСтр("ru = 'В настройках вида документа ""%1"" установлен запрет на создание документов не по шаблону.'"),
			Форма.ВидДокумента);
		Сообщение.Поле = "ВидДокумента";
		Сообщение.Сообщить();
		
		Форма.ВидДокументаID = "";
		Форма.ВидДокументаТип = "";
		Форма.ВидДокумента = "";
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Настраивает форму документа согласно его виду.
//
Процедура НастроитьФормуДокументаСогласноВидуДокументаXDTO(Форма, documentType, organization = Неопределено) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	Элементы = Форма.Элементы;
	
	Если ИмяФормы = "Обработка.ИнтеграцияС1СДокументооборот.Форма.ВходящийДокумент" Тогда
		
		Элементы.СрокИсполнения.Видимость =
			(documentType <> Неопределено И documentType.performanceDateEnabled = Истина);
		Элементы.Сумма.Видимость =
			(documentType <> Неопределено И documentType.sumEnabled = Истина);
		Элементы.Валюта.Видимость =
			(documentType <> Неопределено И documentType.sumEnabled = Истина);
		
	ИначеЕсли ИмяФормы = "Обработка.ИнтеграцияС1СДокументооборот.Форма.ИсходящийДокумент" Тогда
		
		Элементы.СрокИсполнения.Видимость =
			(documentType <> Неопределено И documentType.PerformanceDateEnabled = Истина);
		Элементы.ГруппаНомерДатаПолучателя.Видимость =
			(documentType <> Неопределено И documentType.ExternalNumberEnabled = Истина);
		Элементы.Сумма.Видимость =
			(documentType <> Неопределено И documentType.SumEnabled = Истина);
		Элементы.Валюта.Видимость =
			(documentType <> Неопределено И documentType.SumEnabled = Истина);
		
	ИначеЕсли ИмяФормы = "Обработка.ИнтеграцияС1СДокументооборот.Форма.ВнутреннийДокумент" Тогда
		
		Элементы.СрокИсполнения.Видимость =
			(documentType <> Неопределено И documentType.performanceDateEnabled = Истина);
		Элементы.Сумма.Видимость =
			(documentType <> Неопределено И documentType.sumEnabled = Истина);
		Элементы.Валюта.Видимость =
			(documentType <> Неопределено И documentType.sumEnabled = Истина);
		Элементы.СрокДействияПредставление.Видимость =
			(documentType <> Неопределено И documentType.durationEnabled = Истина);
		Элементы.Адресат.Видимость =
			(documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "addresseeEnabled")
			И documentType.addresseeEnabled);
		
		Форма.ВестиУчетПоКонтрагентам =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "correspondentEnabled")
			И documentType.correspondentEnabled;
		Форма.ВестиУчетСторон =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "partiesEnabled")
			И documentType.partiesEnabled;
		Форма.ЯвляетсяЗаявкойНаОплату =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "isAPaymentRequest")
			И documentType.isAPaymentRequest;
		Форма.ВестиУчетПоСтатьямДДС =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "cashFlowDetailsEnabled")
			И documentType.cashFlowDetailsEnabled;
		Форма.ВестиУчетТоваровИУслуг =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "productRowsEnabled")
			И documentType.productRowsEnabled;
		Форма.ИспользоватьПодписание =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "useSigningByManager")
			И documentType.useSigningByManager;
		Форма.ИспользоватьУтверждение =
			documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "useConfirmation")
			И documentType.useConfirmation;
		
		Если documentType <> Неопределено
				И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "signatureOption") Тогда
			ВариантПодписания = documentType.signatureOption.objectID.ID;
		Иначе
			ВариантПодписания = "";
		КонецЕсли;
		
		Элементы.ГруппаСтороны.Видимость = Форма.ВестиУчетСторон
			И Не Форма.ЯвляетсяЗаявкойНаОплату;
		Элементы.СтороныПодписан.Видимость = Форма.ВестиУчетСторон
			И Не Форма.ЯвляетсяЗаявкойНаОплату
			И ВариантПодписания <> "НеПодписывается";
		Элементы.СтороныДатаПодписи.Видимость = Форма.ВестиУчетСторон
			И Не Форма.ЯвляетсяЗаявкойНаОплату
			И ВариантПодписания <> "НеПодписывается";
		
		Если Не Форма.ВестиУчетСторон Или Форма.ЯвляетсяЗаявкойНаОплату Тогда
			Элементы.ГруппаНашаОрганизация.Видимость =
				(documentType <> Неопределено И documentType.organizationEnabled = Истина);
		Иначе
			Элементы.ГруппаНашаОрганизация.Видимость = Ложь;
		КонецЕсли;
		
		// Заявка на оплату.
		Если Форма.ЯвляетсяЗаявкойНаОплату Тогда
			НаименованиеПолучательXDTO = ИнтеграцияС1СДокументооборот.ПредопределенноеЗначениеДО(
				"DMPartyName", "Получатель"); //@NON-NLS-2
			Форма.Получатель = "";
			Форма.ПолучательID = "";
			Форма.ПолучательТип = "";
			Для Каждого СтрокаСторона Из Форма.Стороны Цикл
				Если СтрокаСторона.НаименованиеID = НаименованиеПолучательXDTO.objectID.ID Тогда
					Форма.Получатель = СтрокаСторона.Сторона;
					Форма.ПолучательID = СтрокаСторона.СторонаID;
					Форма.ПолучательТип = СтрокаСторона.СторонаТип;
				КонецЕсли;
			КонецЦикла;
			Элементы.ГруппаПолучатель.Видимость = Истина;
			Элементы.Организация.Заголовок = НСтр("ru = 'Плательщик'");
			Элементы.ГруппаНашаОрганизация.ОтображатьЗаголовок = Ложь;
			Элементы.АвансовыйОтчет.Видимость = (Форма.ПолучательТип = "DMUser"
				Или ИнтеграцияС1СДокументооборотВызовСервера.ИмяТипаXDTO(Форма.ПолучательТип) = "DMUser");
		Иначе
			Элементы.ГруппаПолучатель.Видимость = Ложь;
			Элементы.Организация.Заголовок = НСтр("ru = 'Организация'");
			Элементы.ГруппаНашаОрганизация.ОтображатьЗаголовок = Истина;
		КонецЕсли;
		
		Элементы.ГруппаКонтрагент.ОтображатьЗаголовок = Форма.ВестиУчетПоКонтрагентам;
		Элементы.Контрагент.Видимость = Форма.ВестиУчетПоКонтрагентам;
		Элементы.КонтактноеЛицо.Видимость = Форма.ВестиУчетПоКонтрагентам;
		Элементы.ГруппаСтатьиДДС.Видимость = Форма.ВестиУчетПоСтатьямДДС;
		Элементы.СтраницаТовары.Видимость = Форма.ВестиУчетТоваровИУслуг;
		
		Если organization <> Неопределено
				И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(organization, "VATpayer") Тогда
			Форма.УчитыватьНДС = organization.VATpayer;
		Иначе
			Форма.УчитыватьНДС = Истина;
		КонецЕсли;
		Элементы.СуммаНДС.Видимость =
			Форма.УчитыватьНДС
			И documentType <> Неопределено
			И documentType.sumEnabled = Истина;
		Элементы.ТоварыСтавкаНДС.Видимость =
			Форма.УчитыватьНДС
			И documentType <> Неопределено
			И documentType.sumEnabled = Истина;
		Элементы.ТоварыСуммаНДС.Видимость =
			Форма.УчитыватьНДС
			И documentType <> Неопределено
			И documentType.sumEnabled = Истина;
		
		Элементы.Подписал.Видимость = Истина;
		Если Форма.ИспользоватьПодписание Тогда
			Элементы.Подписал.Заголовок = НСтр("ru = 'Подписан'");
		ИначеЕсли Форма.ИспользоватьУтверждение Тогда
			Элементы.Подписал.Заголовок = НСтр("ru = 'Утвержден'");
		Иначе
			Элементы.Подписал.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ОтображатьПредупреждения = ОтображениеПредупрежденияПриРедактировании.Отображать;
	НеОтображатьПредупреждения = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	
	Форма.АвтоматическаяНумерация = (documentType <> Неопределено И documentType.automaticNumeration = Истина);
	Если Форма.АвтоматическаяНумерация Тогда
		Элементы.РегистрационныйНомер.ОтображениеПредупрежденияПриРедактировании = ОтображатьПредупреждения;
		Элементы.ДатаРегистрации.ОтображениеПредупрежденияПриРедактировании = ОтображатьПредупреждения;
		Элементы.Зарегистрировать.Доступность = Истина;
	Иначе
		Элементы.РегистрационныйНомер.ОтображениеПредупрежденияПриРедактировании = НеОтображатьПредупреждения;
		Элементы.ДатаРегистрации.ОтображениеПредупрежденияПриРедактировании = НеОтображатьПредупреждения;
		Элементы.Зарегистрировать.Доступность = Ложь;
	КонецЕсли;
	
	Если documentType <> Неопределено
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType,
				"accountingForCaseFilesEnabled")
			И documentType.accountingForCaseFilesEnabled Тогда
		Элементы.НоменклатураДел.Видимость = Истина;
	Иначе
		Элементы.НоменклатураДел.Видимость = Ложь;
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
		
		Если Форма.Элементы.Дело.Видимость Тогда
			Если documentType <> Неопределено
					И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(documentType, "accountingForCaseFilesEnabled")
					И documentType.accountingForCaseFilesEnabled Тогда
				Форма.Элементы.НоменклатураДел.Видимость = Истина;
			Иначе
				Форма.Элементы.НоменклатураДел.Видимость = Ложь;
			КонецЕсли;
		Иначе
			Форма.Элементы.НоменклатураДел.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает видимость группы информации о недоступности функционала версии веб-сервиса.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма объекта или списка 1С:Документооборота
//
Процедура ОбработатьФормуПриНедоступностиФункционалаВерсииСервиса(Форма) Экспорт
	
	Форма.Элементы.ГруппаФункционалНеПоддерживается.Видимость = Истина;
	Форма.Элементы.ДекорацияФункционалНеПоддерживается.Заголовок = 
		НСтр("ru='Функционал не поддерживается в данной версии 1С:Документооборота.'");
		
	Форма.ТолькоПросмотр = Истина;

КонецПроцедуры

// Заполняет стандартные реквизиты бизнес-процесса в соответствующей форме обработки.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма обработки, соответствующая бизнес-процессу:
//     * ДатаНачала - Дата
//     * Наименование - Строка
//     * Описание - Строка
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO типа, наследующего типу DMBusinessProcess, с данными процесса.
//
Процедура ЗаполнитьСтандартнуюШапкуБизнесПроцесса(Форма, ОбъектXDTO) Экспорт
	
	Форма.Стартован = ОбъектXDTO.started;
	Форма.Завершен = ОбъектXDTO.completed;
	Форма.ДатаНачала = ОбъектXDTO.beginDate;
	Форма.ДатаЗавершения = ОбъектXDTO.endDate;
	Форма.Наименование = ОбъектXDTO.name;
	Если ОбъектXDTO.objectID.Свойства().Получить("presentation") <> Неопределено Тогда
		Форма.Заголовок = ОбъектXDTO.objectID.presentation;
	Иначе
		Форма.Заголовок = ОбъектXDTO.name;
	КонецЕсли;
	Если Форма.Элементы.Найти("СрокДата") <> Неопределено Тогда
		Форма.Срок = ОбъектXDTO.dueDate;
	КонецЕсли;
	Если Форма.Элементы.Найти("Описание") <> Неопределено Тогда
		Форма.Описание = ОбъектXDTO.description;
	КонецЕсли;
	Форма.Тип = ОбъектXDTO.objectID.type;
	Если ОбъектXDTO.objectID.Свойства().Получить("presentation") <> неопределено Тогда
		Форма.Представление = ОбъектXDTO.objectID.presentation;
	КонецЕсли;
	
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.author, "Автор");
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.parentTask, "ГлавнаяЗадача");
	ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.state, "Состояние");
	
	Если Форма.Параметры.Свойство("ГлавнаяЗадача") И Не ЗначениеЗаполнено(Форма.ГлавнаяЗадачаID) Тогда
		Форма.ГлавнаяЗадача = Форма.Параметры.ГлавнаяЗадача.name;
		Форма.ГлавнаяЗадачаID = Форма.Параметры.ГлавнаяЗадача.ID;
		Форма.ГлавнаяЗадачаТип = Форма.Параметры.ГлавнаяЗадача.type;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.ГлавнаяЗадачаID) Тогда
		Если Форма.Элементы.Найти("ГлавнаяЗадачаПредставление") <> Неопределено Тогда
			Форма.Элементы.ГлавнаяЗадачаПредставление.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьПредметыВФорме(Форма, ОбъектXDTO);
	
	Если Форма.Элементы.Найти("ГруппаСостояние") <> Неопределено Тогда
		Если Форма.СостояниеID = "Остановлен" Тогда //@NON-NLS-1
			Форма.Элементы.ГруппаСостояние.Видимость = Истина;
			Форма.Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Остановлен'");
			Форма.КартинкаСостояние = 1;
		ИначеЕсли Форма.СостояниеID = "Прерван" Тогда //@NON-NLS-1
			Форма.Элементы.ГруппаСостояние.Видимость = Истина;
			Форма.Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Прерван'");
			Форма.КартинкаСостояние = 2;
		ИначеЕсли ОбъектXDTO.completionMark = "ReadyToStart" Тогда
			Форма.Элементы.ГруппаСостояние.Видимость = Истина;
			Форма.Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Старт отложен'");
			Форма.КартинкаСостояние = 3;
		ИначеЕсли ОбъектXDTO.completionMark = "StartCanceled" Тогда
			Форма.Элементы.ГруппаСостояние.Видимость = Истина;
			Форма.Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Старт отменен'");
			Форма.КартинкаСостояние = 4;
		ИначеЕсли ОбъектXDTO.completionMark = "ReadyToExecute" Тогда
			Форма.Элементы.ГруппаСостояние.Видимость = Истина;
			Форма.Элементы.ТекстСостояние.Заголовок = НСтр(
				"ru = 'Процесс находится в очереди для запуска. Его старт произойдет автоматически в ближайшее время.'");
			Форма.КартинкаСостояние = 0;
		ИначеЕсли ОбъектXDTO.completionMark = "ExecutionCanceled" Тогда
			Форма.Элементы.ГруппаСостояние.Видимость = Истина;
			Форма.Элементы.ТекстСостояние.Заголовок = НСтр("ru = 'Ошибка при старте процесса'");
			Форма.КартинкаСостояние = 0;
		Иначе
			Форма.Элементы.ГруппаСостояние.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ВидПроцессаПредставление") <> Неопределено Тогда
		Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "processType") Тогда
			ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.processType, "ВидПроцесса");
			Форма.Элементы.ВидПроцессаПредставление.Видимость = Истина;
		Иначе
			Форма.ВидПроцесса = "Утверждение";
			Форма.ВидПроцессаID = "Утверждение"; //@NON-NLS-1
			Форма.ВидПроцессаТип = "DMProcessConfirmationType";
			Форма.Элементы.ВидПроцессаПредставление.Видимость = Ложь;
		КонецЕсли;
		Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "target")
				И ОбъектXDTO.target.objectID.type = "DMInternalDocument" Тогда
			Форма.Элементы.ВидПроцессаПредставление.Видимость = Ложь;
		КонецЕсли;
		Если Не Форма.Элементы.ВидПроцессаПредставление.Видимость Тогда
			Форма.Заголовок = Форма.ВидПроцесса;
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ВажностьПредставление") <> Неопределено
			Или Форма.Элементы.Найти("Важность") <> Неопределено Тогда
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.importance, "Важность");
	КонецЕсли;
	
	Если ОбъектXDTO.started Или ОбъектXDTO.completed Тогда
		Форма.ТолькоПросмотр = Истина;
		Если Форма.Элементы.Найти("ФормаСтартоватьИЗакрыть") <> Неопределено Тогда
			Форма.Элементы.ФормаСтартоватьИЗакрыть.Доступность = Ложь;
		КонецЕсли;
		Если Форма.Элементы.Найти("ФормаЗаполнитьПоШаблону") <> Неопределено Тогда
			Форма.Элементы.ФормаЗаполнитьПоШаблону.Доступность = Ложь;
		КонецЕсли;
		Если Форма.Элементы.Найти("ИсполнителиГруппаПеремещение") <> Неопределено Тогда
			Форма.Элементы.ИсполнителиГруппаПеремещение.Доступность = Ложь;
		КонецЕсли;
		Если Форма.Элементы.Найти("СрокИсполненияДней") <> Неопределено Тогда
			Форма.Элементы.СрокИсполненияДней.ТолькоПросмотр = Истина;
		КонецЕсли;
		Если Форма.Элементы.Найти("СрокИсполненияЧасов") <> Неопределено Тогда
			Форма.Элементы.СрокИсполненияЧасов.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		Если ОбъектXDTO.completed Тогда
			Форма.Элементы.ФормаЗаписать.Доступность = Ложь;
			Если Форма.Элементы.Найти("Предметы") <> Неопределено Тогда
				Форма.Элементы.Предметы.Доступность = Ложь;
			КонецЕсли;
			Если Форма.Элементы.Найти("ФормаДобавитьПредмет") <> Неопределено Тогда
				Форма.Элементы.ФормаДобавитьПредмет.Доступность = Ложь;
			КонецЕсли;
			Если Форма.Элементы.Найти("ФормаУдалитьПредмет") <> Неопределено Тогда
				Форма.Элементы.ФормаУдалитьПредмет.Доступность = Ложь;
			КонецЕсли;
			Если Форма.Элементы.Найти("ДобавитьПредмет") <> Неопределено Тогда
				Форма.Элементы.ДобавитьПредмет.Доступность = Ложь;
			КонецЕсли;
			Если Форма.Элементы.Найти("УдалитьПредмет") <> Неопределено Тогда
				Форма.Элементы.УдалитьПредмет.Доступность = Ложь;
			КонецЕсли;
			Если Форма.Элементы.Найти("ДобавитьПредметИзДекорации") <> Неопределено Тогда
				Форма.Элементы.ДобавитьПредметИзДекорации.Доступность = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбъектXDTO.Установлено("state") И ОбъектXDTO.state.objectID.ID = "Прерван" Тогда //@NON-NLS-2
		Форма.Элементы.ФормаЗаписать.Доступность = Ложь;
		Если Форма.Элементы.Найти("Предметы") <> Неопределено Тогда
			Форма.Элементы.Предметы.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Изменение формы в зависимости от функциональных опций.
	Если Форма.Элементы.Найти("ГлавнаяЗадачаПредставление") <> Неопределено Тогда
		Форма.Элементы.ГлавнаяЗадачаПредставление.Видимость = ОбъектXDTO.parentTaskEnabled;
	КонецЕсли;
	Если Форма.Элементы.Найти("СрокВремя") <> Неопределено Тогда
		Форма.Элементы.СрокВремя.Видимость = ОбъектXDTO.dueTimeEnabled;
	ИначеЕсли Форма.Элементы.Найти("СрокИсполненияЧасов") <> Неопределено Тогда
		Форма.Элементы.СрокИсполненияЧасов.Видимость = ОбъектXDTO.dueTimeEnabled;
		Форма.Элементы.Часов.Видимость = ОбъектXDTO.dueTimeEnabled;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.ID) Тогда
		Форма.Заголовок = СтрШаблон(НСтр("ru = '%1 (Создание)'"), Форма.Заголовок);
		Если Форма.Элементы.Найти("ГруппаИнфо") <> Неопределено Тогда
			Форма.Элементы.ГруппаИнфо.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет предметы бизнес-процесса в соответствующей форме обработки.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма обработки, соответствующая бизнес-процессу:
//     * Предметы - ТаблицаЗначений:
//         ** Ссылка - ЛюбаяСсылка
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO типа, наследующего типу DMBusinessProcess, с данными процесса.
//
Процедура ЗаполнитьПредметыВФорме(Форма, ОбъектXDTO) Экспорт
	
	Если ЗначениеЗаполнено(Форма.ГлавнаяЗадачаID) И Не ЗначениеЗаполнено(ОбъектXDTO.objectID.ID) Тогда
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		Ответ = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси,
			Форма.ГлавнаяЗадачаТип,
			Форма.ГлавнаяЗадачаID);
		ГлавнаяЗадачаXDTO = Ответ.objects[0];
		Если Форма.ДоступнаМультипредметность Тогда
			ПредметыXDTO = ГлавнаяЗадачаXDTO.targets.items;
		Иначе
			ПредметXDTO = ГлавнаяЗадачаXDTO.target;
		КонецЕсли;
	Иначе // предметы самой задачи
		Если Форма.ДоступнаМультипредметность Тогда
			ПредметыXDTO = ОбъектXDTO.targets.items;
		Иначе
			ПредметXDTO = ОбъектXDTO.target;
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.ДоступнаМультипредметность Тогда
		
		Форма.Предметы.Очистить();
		
		Для Каждого ПредметXDTO Из ПредметыXDTO Цикл
			
			СтрокаПредмета = Форма.Предметы.Добавить();
			СтрокаПредмета.ИмяПредмета = ПредметXDTO.name;
			СтрокаПредмета.Предмет = ПредметXDTO.target.name;
			СтрокаПредмета.ПредметID = ПредметXDTO.target.objectID.ID;
			СтрокаПредмета.ПредметТип = ПредметXDTO.target.objectID.type;
			СтрокаПредмета.РольПредмета = ПредметXDTO.role.objectID.ID;
			СтрокаПредмета.Картинка = ИнтеграцияС1СДокументооборотКлиентСервер.
				НомерКартинкиПоРолиПредмета(СтрокаПредмета.РольПредмета);
			МассивСсылокПоВнешнимОбъектам = ИнтеграцияС1СДокументооборот.СсылкиПоВнешнимОбъектам(ПредметXDTO.target);
			СтрокаПредмета.Ссылка = ?(МассивСсылокПоВнешнимОбъектам.Количество() > 0,
				МассивСсылокПоВнешнимОбъектам[0],
				Неопределено);
			
			Если ЗначениеЗаполнено(СтрокаПредмета.Ссылка) Тогда
				СтрокаПредмета.Представление = Строка(СтрокаПредмета.Ссылка);
			Иначе
				Если ЗначениеЗаполнено(СтрокаПредмета.ИмяПредмета) Тогда
					СтрокаПредмета.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"%1 (%2)",
						СтрокаПредмета.Предмет,
						СтрокаПредмета.ИмяПредмета);
				Иначе
					СтрокаПредмета.Представление = СтрокаПредмета.Предмет;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла; // по предметам
		
	Иначе // мультипредметность не поддерживается
		
		ЗаполнитьОбъектныйРеквизит(Форма, ПредметXDTO, "Предмет");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет контролера бизнес-процесса в соответствующей форме обработки.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма обработки, соответствующая бизнес-процессу.
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO типа, наследующего типу DMBusinessProcess, с данными процесса.
//
Процедура ЗаполнитьКонтролераВФорме(Форма, ОбъектXDTO) Экспорт
	
	Если ОбъектXDTO.controller.Установлено("user") Тогда
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.controller.user, "Контролер");
	ИначеЕсли ОбъектXDTO.controller.Установлено("role") Тогда
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.controller.role, "Контролер");
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.controller.mainAddressingObject, "ОсновнойОбъектАдресацииКонтролера");
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.controller.secondaryAddressingObject, "ДополнительныйОбъектАдресацииКонтролера");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет проверяющего бизнес-процесса в соответствующей форме обработки.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма обработки, соответствующая бизнес-процессу.
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO типа, наследующего типу DMBusinessProcess, с данными процесса.
//
Процедура ЗаполнитьПроверяющегоВФорме(Форма, ОбъектXDTO) Экспорт
	
	Если ОбъектXDTO.verifier.Установлено("user") Тогда
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.verifier.user, "Проверяющий");
	ИначеЕсли ОбъектXDTO.verifier.Установлено("role") Тогда
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.verifier.role, "Проверяющий");
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.verifier.mainAddressingObject, "ОсновнойОбъектАдресацииПроверяющего");
		ЗаполнитьОбъектныйРеквизит(Форма, ОбъектXDTO.verifier.secondaryAddressingObject, "ДополнительныйОбъектАдресацииПроверяющего");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет исполнителя бизнес-процесса в соответствующей форме обработки.
//
// Параметры:
//   Приемник - СтрокаТаблицыЗначений - строка таблицы значений Исполнители.
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO типа, наследующего типу DMBusinessProcess, с данными процесса.
//
Процедура ЗаполнитьИсполнителяВФорме(Приемник, ОбъектXDTO) Экспорт
	
	Если ОбъектXDTO.Свойства().Получить("performer") = Неопределено Тогда
		ИсполнительXDTO = ОбъектXDTO;
	Иначе
		ИсполнительXDTO = ОбъектXDTO.performer;
	КонецЕсли;
		
	Если ИсполнительXDTO.Установлено("user") Тогда
		ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.user, "Исполнитель");
	ИначеЕсли ИсполнительXDTO.Установлено("role") Тогда
		ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.role, "Исполнитель");
		ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.mainAddressingObject, "ОсновнойОбъектАдресации");
		ЗаполнитьОбъектныйРеквизит(Приемник, ИсполнительXDTO.secondaryAddressingObject, "ДополнительныйОбъектАдресации");
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает доступность команд, меняющих состояние процесса, в его форме.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма обработки, соответствующая бизнес-процессу.
//   ОбъектXDTO - ОбъектXDTO - Объект XDTO типа, наследующего типу DMBusinessProcess, с данными процесса.
//
Процедура УстановитьВидимостьКомандИзмененияСостоянияПроцесса(Форма, ОбъектXDTO) Экспорт
	
	Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "leadingTask") Тогда
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Ложь;
		
	ИначеЕсли Не ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "state") Тогда
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Ложь;
		
	ИначеЕсли ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "completed") И ОбъектXDTO.completed Тогда
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Ложь;
		
	ИначеЕсли Не ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "started") Или Не ОбъектXDTO.started Тогда
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Ложь;
		
	ИначеЕсли ОбъектXDTO.state.objectID.ID = "Активен" Тогда //@NON-NLS-1
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Истина;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Истина;
		
	ИначеЕсли ОбъектXDTO.state.objectID.ID = "Остановлен" Тогда //@NON-NLS-1
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Истина;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Истина;
		
	ИначеЕсли ОбъектXDTO.state.objectID.ID = "Прерван" Тогда //@NON-NLS-1
		Форма.Элементы.ФормаОстановитьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПрерватьПроцесс.Видимость = Ложь;
		Форма.Элементы.ФормаПродолжитьПроцесс.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет объект XDTO (приемник) по реквизиту (источник)
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Источник - ФормаКлиентскогоПриложения, Структура - содержит реквизиты или свойства,
//     по которым заполняется объект XDTO.
//   ИмяРеквизита - Строка - имя объектного реквизита источника.
//   Приемник - ОбъектXDTO - содержит заполняемое свойство.
//   ТипОбъектаXDTO - Строка - тип XDTO заполняемого свойства.
//   ЗаполнятьВсегда - Булево - признак заполнения, даже если объектный реквизит не заполнен.
//
Процедура ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси, Источник, ИмяРеквизита, Приемник, ТипОбъектаXDTO,
		ЗаполнятьВсегда = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(Источник[ИмяРеквизита])
			Или ЗначениеЗаполнено(Источник[ИмяРеквизита + "ID"])
			Или ЗаполнятьВсегда Тогда
		Приемник = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, ТипОбъектаXDTO);
		Приемник.name = Источник[ИмяРеквизита];
		Приемник.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		Приемник.objectID.ID = Источник[ИмяРеквизита + "ID"];
		Приемник.objectID.type = Источник[ИмяРеквизита + "Тип"];
	КонецЕсли;
	
КонецПроцедуры

// Заполняет свойство XDTO (приемник) типа DMBusinessProcessTaskExecutor по данным формы (источник).
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMBusinessProcessTaskExecutor, свойство которого нужно заполнить.
//   Форма - ФормаКлиентскогоПриложения, Структура - содержит реквизиты - источники заполнения.
//   ИмяРеквизита - Строка - имя реквизита исполнителя или роли на форме.
//   ИмяРеквизитаАдресации - Строка - имя реквизита данных адресации на форме.
//
Процедура СоздатьУчастникаБизнесПроцесса(Прокси, ОбъектXDTO, Форма, ИмяРеквизита, ИмяРеквизитаАдресации) Экспорт
	
	Если ЗначениеЗаполнено(Форма[ИмяРеквизита]) Тогда 
		ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMBusinessProcessTaskExecutor");
		Если Форма[ИмяРеквизита + "Тип"] = "DMUser" Тогда
			ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси, Форма, ИмяРеквизита, ОбъектXDTO.user, "DMUser")
		Иначе
			ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси, Форма, ИмяРеквизита, ОбъектXDTO.role, "DMBusinessProcessExecutorRole");
			
			ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси, Форма, "Основной" + ИмяРеквизитаАдресации,
				ОбъектXDTO.mainAddressingObject, "DMMainAddressingObject");
			
			ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси, Форма, "Дополнительный" + ИмяРеквизитаАдресации,
				ОбъектXDTO.secondaryAddressingObject, "DMSecondaryAddressingObject")
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Создает объект XDTO типа DMBusinessProcess[ИмяПроцесса], и заполняет общие свойства по данным формы.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Тип - Строка - тип объекта XDTO бизнес-процесса.
//   Форма - ФормаКлиентскогоПриложения - содержит реквизиты - источники заполнения:
//     * ДатаНачала - Дата
//     * Наименование - Строка
//     * Описание - Строка
//   Исключения - Строка - имена исключаемых из заполнения реквизитов формы разделенных запятой.
//
// Возвращаемое значение:
//   ОбъектXDTO
//
Функция ПодготовитьШапкуБизнесПроцесса(Прокси, Тип, Форма, Исключения = "") Экспорт
	
	Объект = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, Тип);
	
	Объект.name = Форма.Наименование;
	Объект.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
	Объект.objectID.ID = Форма.ID;
	Объект.objectID.type = Тип;
	
	// Общая шапка бизнес-процессов
	ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(
		Прокси,
		Форма,
		"Автор",
		Объект.author,
		"DMUser");
	ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(
		Прокси,
		Форма,
		"Состояние",
		Объект.state,
		"DMBusinessProcessState");
	
	Если Форма.ДоступнаМультипредметность Тогда
		ЗаполнитьПроцессПредметамиИзФормы(Прокси, Форма.Предметы, Объект);
	Иначе
		ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(
			Прокси,
			Форма,
			"Предмет",
			Объект.target,
			"DMObject");
	КонецЕсли;
	
	Если Найти(Исключения, "ВидПроцесса") = 0
			И ИнтеграцияС1СДокументооборот.СвойствоСуществует(Объект, "processType") Тогда
		ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(
			Прокси,
			Форма,
			"ВидПроцесса",
			Объект.processType,
			"DMProcessConfirmationType");
	КонецЕсли;
	
	Если Найти(Исключения, "Важность") = 0 Тогда
		ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(
			Прокси,
			Форма,
			"Важность",
			Объект.importance,
			"DMBusinessProcessImportance");
	КонецЕсли;
	
	Если Найти(Исключения, "Шаблон") = 0 Тогда
		ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(
			Прокси,
			Форма,
			"Шаблон",
			Объект.businessProcessTemplate,
			Тип + "Template");
	КонецЕсли;
	
	Объект.beginDate = Форма.ДатаНачала;
	
	Если Найти(Исключения,"Стартован") = 0 Тогда
		Объект.started = Форма.Стартован;
	КонецЕсли;
	Если Найти(Исключения,"Завершен") = 0 Тогда
		Объект.completed = Форма.Завершен;
	КонецЕсли;
	Если Найти(Исключения,"Описание") = 0 Тогда
		Объект.description = Форма.Описание;
	КонецЕсли;
	Если Найти(Исключения,"Срок") = 0 Тогда
		Объект.dueDate = Форма.Срок;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.ГлавнаяЗадачаID) Тогда
		Ответ = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси, Форма.ГлавнаяЗадачаТип, Форма.ГлавнаяЗадачаID);
		ГлавнаяЗадачаПриемник = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMBusinessProcessTask");
		ИнтеграцияС1СДокументооборот.ЗаполнитьЗначенияСвойствXDTO(Прокси,ГлавнаяЗадачаПриемник,Ответ.Objects[0]);
		Объект.parentTask = ГлавнаяЗадачаПриемник;
	КонецЕсли;
	
	Возврат Объект;
	
КонецФункции

// Заполняет объект XDTO бизнес-процесса предметами из таблицы формы.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Предметы - ДанныеФормыКоллекция - таблица предметов.
//   Процесс - ОбъектXDTO - Объект XDTO типа DMBusinessProcess<...> - заполняемый процесс.
//
Процедура ЗаполнитьПроцессПредметамиИзФормы(Прокси, Предметы, Процесс)
	
	targetCollection = ИнтеграцияС1СДокументооборот.СоздатьОбъект(
		Прокси, "DMBusinessProcessTargetCollection");
	ОписаниеПредмета = targetCollection.items; // СписокXDTO
		
	Для Каждого СтрокаПредмета Из Предметы Цикл
			
		Если Не Процесс.Установлено("target") Тогда
			Процесс.target = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, СтрокаПредмета.ПредметТип);
			Процесс.target.name = СтрокаПредмета.Предмет;
			Процесс.target.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
			Процесс.target.objectID.ID = СтрокаПредмета.ПредметID;
			Процесс.target.objectID.type = СтрокаПредмета.ПредметТип;
		КонецЕсли;
		
		target = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMBusinessProcessTarget");
		target.name = СтрокаПредмета.ИмяПредмета;
		
		target.role = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMBusinessProcessTargetRole");
		target.role.name = СтрокаПредмета.РольПредмета;
		target.role.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		target.role.objectID.ID = СтрокаПредмета.РольПредмета;
		target.role.objectID.type = "DMBusinessProcessTargetRole";
		
		target.target = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, СтрокаПредмета.ПредметТип);
		target.target.name = СтрокаПредмета.Предмет;
		target.target.objectID = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		target.target.objectID.ID = СтрокаПредмета.ПредметID;
		target.target.objectID.type = СтрокаПредмета.ПредметТип;
		
		ОписаниеПредмета.Добавить(target);
		
	КонецЦикла;
	
	Процесс.targets = targetCollection;
	
КонецПроцедуры

// Помещает значения дополнительных свойств в объект XDTO из формы объекта.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   ОбъектXDTO - ОбъектXDTO - заполняемый объект XDTO.
//   Форма - ФормаКлиентскогоПриложения, Структура - управляемая форма или структура реквизитов
//     документа 1С:Документооборота.
//
Процедура СформироватьДополнительныеСвойства(Прокси, ОбъектXDTO, Форма) Экспорт
	
	Для Каждого ДопСвойство Из Форма.Свойства Цикл
		
		ДополнительноеСвойство = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMAdditionalProperty");
		ДополнительноеСвойство.name = ДопСвойство.Свойство;
		
		ДополнительноеСвойство.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(
			Прокси, ДопСвойство.СвойствоID, ДопСвойство.СвойствоТип);
		
		Если ЗначениеЗаполнено(ДопСвойство.ЗначениеID) Тогда //значение объектного типа
			
			ДоступныеТипы = ДопСвойство.СписокДоступныхТипов;
			Если ДоступныеТипы.Количество() <> 1 Тогда
				ДополнительноеСвойство.propertyObjectValue = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObject");
				ДополнительноеСвойство.propertyObjectValue.name = ДопСвойство.Значение;
				ДополнительноеСвойство.propertyObjectValue.objectID =
					ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ДопСвойство.ЗначениеID, ДопСвойство.ЗначениеТип);
				
			Иначе
				ТипСвойства = ДоступныеТипы[0].Значение.xdtoClassName;
				ЗначениеСвойства = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, ТипСвойства);
				
				// заполнить из потребителя
				РеквизитТип = ДопСвойство.ЗначениеТип;
				РеквизитID = ДопСвойство.ЗначениеID;
				Если Метаданные.НайтиПоПолномуИмени(РеквизитТип) <> Неопределено Тогда
					
					СсылкаНаПотребителя = ИнтеграцияС1СДокументооборот.СсылкаИзUUID(РеквизитТип, РеквизитID);
					ИнтеграцияС1СДокументооборот.ЗаполнитьРеквизитыИзПотребителя(
						Прокси,
						ЗначениеСвойства,
						СсылкаНаПотребителя);
					
				ИначеЕсли РеквизитТип = "Строка" Тогда
					
					ЗначениеСвойства.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, "", "");
					ЗначениеСвойства.name = ДопСвойство.Значение;
					
					ВнешнийИД = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
					ВнешнийИД.ID = ДопСвойство.ЗначениеID;
					ВнешнийИД.type = РеквизитТип;
					ВнешнийИД.name = ДопСвойство.Значение;
					
					ЗначениеСвойства.externalObject = ВнешнийИД;
					
				Иначе
					
					ЗначениеСвойства.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(
						Прокси, ДопСвойство.ЗначениеID, ДопСвойство.ЗначениеТип);
					ЗначениеСвойства.name = ДопСвойство.Значение;
					
				КонецЕсли;
				
				ДополнительноеСвойство.propertyObjectValue = ЗначениеСвойства;
				
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(ДопСвойство.Значение) Тогда
			
			ДополнительноеСвойство.propertySimpleValue = ДопСвойство.Значение;
			
		КонецЕсли;
		
		ДополнительныеСвойства = ОбъектXDTO.additionalProperties; // СписокXDTO
		ДополнительныеСвойства.Добавить(ДополнительноеСвойство);
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет реквизит поиска в список условий XDTO, параллельно формируя представление условия
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   ИмяРеквизита - Строка - имя реквизита поиска.
//   ОписаниеУсловий - Структура - описание условий поиска.
//   СписокУсловий - ОбъектXDTO - ОбъектXDTO типа DMObjectListQuery, пополняемый список условий.
//   Представление - Строка - пополняемое представление условия.
//
Процедура ДобавитьРеквизитВСписокУсловий(Прокси, ИмяРеквизита, ОписаниеУсловия, СписокУсловий, Представление)
	
	Если Представление = "" Тогда
		Представление = Представление + ОписаниеУсловия.Представление;
	Иначе
		Представление = Представление + "; " + НРег(ОписаниеУсловия.Представление);
	КонецЕсли;
	
	Если ОписаниеУсловия.Свойство("ПредставлениеУсловия")
			И ЗначениеЗаполнено(ОписаниеУсловия.ПредставлениеУсловия) Тогда
		Представление = Представление + ": " + ОписаниеУсловия.ПредставлениеУсловия;
	Иначе
		Представление = Представление + ": "
			+ ?(ТипЗнч(ОписаниеУсловия.Значение) = Тип("Дата"),
				Формат(ОписаниеУсловия.Значение, "ДЛФ=D"),
				ОписаниеУсловия.Значение);
	КонецЕсли;	
	
	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = ИмяРеквизита;
	Если ОписаниеУсловия.ОператорСравнения = "<=" И ТипЗнч(ОписаниеУсловия.Значение) = Тип("Дата") Тогда
		Условие.value = КонецДня(ОписаниеУсловия.Значение);
	Иначе
		Если ОписаниеУсловия.Свойство("ЗначениеID") И ЗначениеЗаполнено(ОписаниеУсловия.ЗначениеID) Тогда
			ЗначениеРеквизита = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
				ОписаниеУсловия.ЗначениеID, ОписаниеУсловия.ЗначениеТип);
			Условие.value = ЗначениеРеквизита;
		Иначе // примитивный тип
			Условие.value = ОписаниеУсловия.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если ОписаниеУсловия.ОператорСравнения = "=" И ТипЗнч(ОписаниеУсловия.Значение) = Тип("Дата") Тогда
		Условие.comparisonOperator = ">=";
	Иначе
		Условие.comparisonOperator = ОписаниеУсловия.ОператорСравнения;
	КонецЕсли;
	
	Условия = СписокУсловий.conditions; // СписокXDTO
	Условия.Добавить(Условие);
	
	Если ОписаниеУсловия.Свойство("ОператорСравнения2") 
			И ЗначениеЗаполнено(ОписаниеУсловия.ОператорСравнения2) Тогда
		Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = ИмяРеквизита;
		Если ОписаниеУсловия.ОператорСравнения2 = "<=" И ТипЗнч(ОписаниеУсловия.Значение2) = Тип("Дата") Тогда
			Условие.value = КонецДня(ОписаниеУсловия.Значение2);
		Иначе
			Условие.value = ОписаниеУсловия.Значение2;
		КонецЕсли;
		Условие.comparisonOperator = ОписаниеУсловия.ОператорСравнения2;
		Условия.Добавить(Условие);
	ИначеЕсли ОписаниеУсловия.ОператорСравнения = "="
			И ТипЗнч(ОписаниеУсловия.Значение) = Тип("Дата")
			И ИмяРеквизита <> "anyDate" Тогда
		Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = ИмяРеквизита;
		Условие.value = КонецДня(ОписаниеУсловия.Значение);
		Условие.comparisonOperator = "<=";
		Условия.Добавить(Условие);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет поиск объектов ДО по реквизитам, сохраняя результат во временное хранилище
//
// Параметры:
//   ТипОбъекта - Строка - тип объекта, поиск по которому требуется выполнить
//   РеквизитыПоиска - Структура - описание реквизитов поиска
//   АдресВоВременномХранилище - Строка - параметр, в который помещается адрес результатов поиска
//   КоличествоРезультатов - Число - количество результатов поиска
//   ПредельноеКоличествоРезультатов - Число - предельное количество выбираемых объектов, если оно превышено
//
Процедура ВыполнитьПоискПоРеквизитам(ТипОбъекта, РеквизитыПоиска, АдресВоВременномХранилище,
		КоличествоРезультатов, ПредельноеКоличествоРезультатов) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	СписокУсловий = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	ПредельноеКоличествоРезультатов = ИнтеграцияС1СДокументооборот.ПредельноеКоличествоВыбираемыхОбъектов(ТипОбъекта);
	СписокУсловий.limit = ПредельноеКоличествоРезультатов;
	ВыбираемыеПоля = СписокУсловий.columnSet; // СписокXDTO
	
	НаборКолонок = Новый Массив;
	Если ТипОбъекта = "DMIncomingDocument"
			Или ТипОбъекта = "DMInternalDocument"
			Или ТипОбъекта = "DMOutgoingDocument" Тогда
		НаборКолонок.Добавить("documentType");
		НаборКолонок.Добавить("regNumber");
		НаборКолонок.Добавить("regDate");
		НаборКолонок.Добавить("sum");
		НаборКолонок.Добавить("organization");
		НаборКолонок.Добавить("correspondent");
		Если ТипОбъекта = "DMInternalDocument" Тогда
			НаборКолонок.Добавить("folder");
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "DMCorrespondent" Тогда
		НаборКолонок.Добавить("inn");
		НаборКолонок.Добавить("kpp");
		НаборКолонок.Добавить("legalPrivatePerson");
		
	КонецЕсли;
	
	Представление = "";
	Для Каждого РеквизитПоиска Из РеквизитыПоиска Цикл
		ИмяРеквизита = РеквизитПоиска.Ключ;
		ОписаниеУсловия = РеквизитПоиска.Значение;
		ДобавитьРеквизитВСписокУсловий(Прокси, ИмяРеквизита, ОписаниеУсловия, СписокУсловий, 
			Представление);
		// Реквизиты с отбором на равенство не информативны.
		Если ОписаниеУсловия.ОператорСравнения = "=" Тогда
			ПозицияВМассиве = НаборКолонок.Найти(ИмяРеквизита);
			Если ПозицияВМассиве <> Неопределено Тогда
				НаборКолонок.Удалить(ПозицияВМассиве);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// добавим оставшиеся колонки в требование выборки
	Для Каждого ИмяРеквизита Из НаборКолонок Цикл
		ВыбираемыеПоля.Добавить(ИмяРеквизита);
	КонецЦикла;
	
	Если Представление = "" Тогда
		Представление = НСтр("ru = 'Условия не заданы.'");
	Иначе
		Представление = Представление + ".";
	КонецЕсли;
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = ТипОбъекта;
	Запрос.query = СписокУсловий;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат); // возможно исключение
	
	НайденныеОбъекты = Новый Массив;
	Для Каждого ОбъектXDTO Из Результат.items Цикл
		НайденныйОбъект = Новый Структура;
		НайденныеОбъекты.Добавить(НайденныйОбъект);
		НайденныйОбъект.Вставить("name", ОбъектXDTO.object.name);
		НайденныйОбъект.Вставить("ID", ОбъектXDTO.object.objectID.ID);
		Для Каждого ОписаниеКолонки Из НаборКолонок Цикл
			ИмяРеквизита = ОписаниеКолонки;
			ЗначениеСвойства = ОбъектXDTO.object.Получить(ИмяРеквизита);
			Если ТипЗнч(ЗначениеСвойства) = Тип("ОбъектXDTO") Тогда // ссылочный тип ДО
				НайденныйОбъект.Вставить(ИмяРеквизита, ЗначениеСвойства.name);
				НайденныйОбъект.Вставить(ИмяРеквизита + "ID", ЗначениеСвойства.objectID.ID);
			Иначе // примитивный тип
				НайденныйОбъект.Вставить(ИмяРеквизита, ЗначениеСвойства);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// ранние версии ДО не поддерживают tooManyObjects
	Если ?(Результат.Установлено("tooManyObjects"), Результат.tooManyObjects, Ложь) Тогда
		Представление = Представление
			+ " " + НСтр("ru = 'Перетащите сюда любой реквизит, чтобы уточнить условия.'");
		
	Иначе
		ПредельноеКоличествоРезультатов = 0;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("НайденныеОбъекты", НайденныеОбъекты);
	Результат.Вставить("НаборКолонок", НаборКолонок);
	Результат.Вставить("ПредставлениеУсловийПоиска", Представление);
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Результат, Новый УникальныйИдентификатор);
	
	КоличествоРезультатов = НайденныеОбъекты.Количество();
	
КонецПроцедуры

// Стандартный обработчик печати для подсистемы УправлениеПечатью БСП.
//
// Параметры:
//   МассивОбъектов  - Массив из ЛюбаяСсылка - ссылки на объекты, которые нужно распечатать.
//   ПараметрыПечати - Структура - дополнительные настройки печати.
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы.
//   ОбъектыПечати - СписокЗначений - значение - ссылка на объект, представление - имя области,
//     в которой был выведен объект.
//   ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Для Каждого ПечатнаяФорма Из КоллекцияПечатныхФорм Цикл
		Если ПечатнаяФорма.ИмяМакета = "ЛистСогласования" Тогда
			ЛистСогласования = ПодготовитьЛистСогласования(МассивОбъектов); // возможно исключение
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				ПечатнаяФорма.ИмяМакета,
				ПечатнаяФорма.СинонимМакета,
				ЛистСогласования);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает в виде табличного документа печатную форму листа согласования по переданному предмету.
//
// Параметры:
//   ПредметыСогласования - ЛюбаяСсылка, Массив из ЛюбаяСсылка - согласуемые объекты ИС.
//
// Возвращаемое значение:
//   ТабличныйДокумент - печатная форма листа (листов) согласования.
//
Функция ПодготовитьЛистСогласования(ПредметыСогласования)
	
	// Без поддержки листа согласования со стороны веб-сервиса этой команды не должно быть в интерфейсе.
	// В случае неожиданной смены версии на стороне сервиса выдадим, однако, исключение.
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.8.1") Тогда
		ВызватьИсключение НСтр("ru = 'Для вывода листа согласования нужен 1С:Документооборот версии не ниже 1.4.8.1'");
	КонецЕсли;
	
	Результат = Новый ТабличныйДокумент;
	Результат.Защита = Истина;
	
	Макет = Обработки.ИнтеграцияС1СДокументооборот.ПолучитьМакет("ЛистСогласования");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьИсполнителиШапка = Макет.ПолучитьОбласть("ИсполнителиШапка");
	ОбластьИсполнители = Макет.ПолучитьОбласть("Исполнители");
	ОбластьСогласованиеНеЗапускалось = Макет.ПолучитьОбласть("СогласованиеНеЗапускалось");
	
	НуженРазделительСтраниц = Ложь;
	
	Если ТипЗнч(ПредметыСогласования) = Тип("Массив") Тогда
		МассивПредметов = ПредметыСогласования;
	Иначе // единственная ссылка
		МассивПредметов = Новый Массив;
		МассивПредметов.Добавить(ПредметыСогласования);
	КонецЕсли;
	
	Для Каждого ПредметСогласования Из МассивПредметов Цикл
		
		Если НуженРазделительСтраниц Тогда
			Результат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Результат.Вывести(ОбластьЗаголовок);
		ОбластьШапка.Параметры.Предмет = Строка(ПредметСогласования);
		Результат.Вывести(ОбластьШапка);
		
		// Проверим существование связанного объекта.
		ПредметВДО = ИнтеграцияС1СДокументооборотВызовСервера.
			ДанныеОбъектаДОПоВнешнемуОбъекту(ПредметСогласования);
		Если ПредметВДО = Неопределено Тогда // согласование не запускалось
			Результат.Вывести(ОбластьСогласованиеНеЗапускалось);
			
		Иначе
			Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
			Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetApprovalSheetRequest");
			Запрос.object = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, ПредметВДО.type);
			Запрос.object.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
				ПредметВДО.ID, ПредметВДО.type);
			Запрос.object.name = ПредметВДО.name;
			Ответ = Прокси.execute(Запрос);
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
			Если Ответ.items.Количество() = 0 Тогда
				Результат.Вывести(ОбластьСогласованиеНеЗапускалось);
			Иначе
				Результат.Вывести(ОбластьИсполнителиШапка);
				Для Каждого Пункт Из Ответ.items Цикл
					ОбластьИсполнители.Параметры.Должность = Пункт.position;
					ОбластьИсполнители.Параметры.Исполнитель = Пункт.name;
					ОбластьИсполнители.Параметры.РезультатСогласования = Пункт.result;
					ОбластьИсполнители.Параметры.ДатаИсполнения = Пункт.date;
					ОбластьИсполнители.Параметры.РезультатВыполнения = Пункт.comment;
					Результат.Вывести(ОбластьИсполнители);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		НуженРазделительСтраниц = Истина;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли